// ----------------------------------------------------------------

wordentry_set ЛогичСоюзЭлем = 
{
 союз:и{},
 союз:"и даже"{},
 союз:или{},
 союз:да{}, // мне бы поесть да поспать.
 союз:"да и"{},
 союз:либо{},
 союз:то есть{},
 союз:"т.е."{} /*,
 союз:"то бишь"{}*/
}

patterns ЛогичСоюз export { node:root_node }
pattern ЛогичСоюз
{
 ЛогичСоюзЭлем : export { node:root_node }
}



// работаем быстро, точно, хотя и дорого
//                         ^^^^^^
pattern ЛогичСоюз
{
 c=союз:хотя{} : export { node:root_node }
 c2=союз:и{}
} : links { c.<NEXT_COLLOCATION_ITEM>c2 }
: ngrams { 5 }


// Частица ЖЕ может выступать как постфикс для союза ИЛИ:
//
// галлюцинация или же запах добычи?
//              ^^^^^^
pattern ЛогичСоюз
{
 c=союз:или{} : export { node:root_node }
 p=частица:же{}
} : links { c.<POSTFIX_PARTICLE>p }


// Частица НУ может входить в союзный токен:
//
// Возьми яблоко, или грушу, ну или помидор.
//                           ^^^^^^
pattern ЛогичСоюз
{
 p=частица:ну{}
 c=ЛогичСоюз : export { node:root_node }
} : links { c.<PREFIX_PARTICLE>p }
