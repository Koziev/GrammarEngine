// *********************
// СУЩЕСТВИТЕЛЬНОЕ
// *********************
patterns Сущ0 { language=Russian } export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }

// ---------------------------------------

wordentry_set АппозитивныеСущ=существительное:{
ПОДТИП, ТРЕХЧЛЕН,
функция, множество, величина, угол, вектор, сторона,
буква, знак, символ, слово, фамилия, имя, цифра, АББРЕВИАТУРА,
АКРОНИМ, ВУЛЬГАРИЗМ, СЛОВЕЧКО, СЛОВЦО, БУКОВКА, ЦИФЕРКА,
ГЛАГОЛ, ЗВУК, значок,
ИДИОМА, ИМЕЧКО, ИНИЦИАЛ, ИНФИКС, ПРИСТАВКА, СУФФИКС, КОРЕНЬ, АФФИКС,
ОКОНЧАНИЕ, КЛИЧКА, КЛИКУХА,
КОД, ЛЕКСЕМА, МОНОФТОНГ, НАРЕЧИЕ, НИК, НОМЕР, НОТА, ОМОНИМ, ПАРОЛЬ,
ПАРОНИМ, частица,
ПЕРЕМЕННАЯ, ПОДМНОЖЕСТВО, ПОЛИНОМ, ПОМЕТКА,
ПРЕФИКС, ПРИЗНАК, ПРИЛАГАТЕЛЬНОЕ,
ПРИЧАСТИЕ, ПСЕВДОКОД, ПСЕВДОНИМ, СИНОНИМ, СЛАВЯНИЗМ,
СЛАГАЕМОЕ, /*СОГЛАСНАЯ*/
СУЩЕСТВИТЕЛЬНОЕ, ТЕРМИН, НАЗВАНИЕ, НАИМЕНОВАНИЕ,
ФОНЕМА, ФРАЗЕОЛОГИЗМ, ЧИСЛИТЕЛЬНОЕ, модель, план, модификация
 }

patterns ПроизвольноеСлово { language=Russian } export { node:root_node }

pattern ПроизвольноеСлово
{
 *:*{} : export { node:root_node }
} : ngrams { -7 }

// Названия обычно идут с большой буквы:
// Гора Бигмаунт возвышалась вдали
//      ^^^^^^^^
pattern ПроизвольноеСлово
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+") : export { node:root_node }
} : ngrams { -1 }

// Справа ты увидишь гору Обитель Бога
//                        ^^^^^^^^^^^^
pattern ПроизвольноеСлово
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+") : export { node:root_node }
 w2=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+")
}
: links { w1.<NEXT_COLLOCATION_ITEM>w2 }
: ngrams { -1 }




// В отношении других звёзд употребляется термин звёздный ветер
//                                               ^^^^^^^^^^^^^^
pattern ПроизвольноеСлово
{
 ГруппаСущ4{ падеж:им } : export { node:root_node }
} : ngrams { -4 }

// Аппозитивные конструкции для некоторых существительных
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=АппозитивныеСущ : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ПроизвольноеСлово
} : links { n1.<RIGHT_NAME>n2 }

wordentry_set СущСНазв=существительное:{
 АВИАЛАЙНЕР, АВИАРЕЙС, АВТОБАН, АВТОБУС, АГИТПУНКТ, АРХИПЕЛАГ,
 АТОЛЛ, АЭРОДРОМ, АЭРОПОРТ, БУЛЬВАР, ВОДОПАД, 
 ГАВАНЬ, ГОРА, ГОРОД, ГОСУДАРСТВО, ГРАФСТВО,
 ДЕРЕВЕНЬКА, ДЕРЕВНЯ, ДЕРЕВУШКА, ТРАССА, КАНТОН, КВАРТАЛ,
 КОСМОДРОМ, ЗАМОК, КУРОРТ, ЛЕДНИК, МОРЕ, МЫС, БУХТА,
 ЗАЛИВ, ОКРУГ, ОСТРОВ, ПАРОХОД, ПЕРЕУЛОК, ПИК, ПЛАТО, ПЛОСКОГОРЬЕ,
 ПОЕЗД, ПОРТ, ПРЕФЕКТУРА, ПРОВИНЦИЯ, ПРОЛИВ, ПУСТЫНЯ, РАЙОН,
 РЕКА, РЕСПУБЛИКА, САМОЛЕТ, СЕКТОР, СЕЛЕНИЕ, СЕЛО, СКАЛА,
 СТАНЦИЯ, УЕЗД, УТЕС, УЩЕЛЬЕ, ФЕРМА, ФОРТ, ФЬОРД, ХУТОР, ШТАТ, ЭМИРАТ
}

// Названия - начинаются с заглавной буквы, далее могут быть еще несколько
// лексем.

patterns НазваниеОбъекта { language=Russian } export { ПАДЕЖ node:root_node }

pattern НазваниеОбъекта
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД } : export { ПАДЕЖ node:root_node }
} : ngrams { -1 }

// Справа ты увидишь гору Обитель Бога
//                        ^^^^^^^^^^^^
pattern НазваниеОбъекта
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД } : export { ПАДЕЖ node:root_node }
 w2=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+")
}
: ngrams { -1 }
: links { w1.<NEXT_COLLOCATION_ITEM>w2 }

pattern НазваниеОбъекта
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД } : export { ПАДЕЖ node:root_node }
 w2='-'
 w3=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+")
}
: ngrams { -1 }
: links { w1.
           <NEXT_COLLOCATION_ITEM>w2.
            <NEXT_COLLOCATION_ITEM>w3
        }

// Арзамас-19
pattern НазваниеОбъекта
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД } : export { ПАДЕЖ node:root_node }
 w2='-'
 w3=@regex("[0-9]+")
}
: ngrams { -1 }
: links { w1.
           <NEXT_COLLOCATION_ITEM>w2.
            <NEXT_COLLOCATION_ITEM>w3
        }

// Арзамас 19
pattern НазваниеОбъекта
{
 w1=@regex_strict("[\\@А\\@Б\\@Й][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД } : export { ПАДЕЖ node:root_node }
 w2=@regex("[0-9]+")
}
: ngrams { -1 }
: links { w1.
           <NEXT_COLLOCATION_ITEM>w2
        }

// 13 августа 1944 года был освобожден город Витебск.
//                                           ^^^^^^^
pattern НазваниеОбъекта
{
 @and(
      @regex_strict("[\\@А\\@БЙ][\\@а\\@б\\@й]+"){ [-1]ПАДЕЖ:ИМ [-1]ЧИСЛО:ЕД }:export { ПАДЕЖ node:root_node },
      существительное:*{ ПАДЕЖ:ИМ }
     )
}

// Географические термины:
// проживают в штатах Калифорния, Флорида, Техас и Нью-Йорк
//             ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=СущСНазв : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=НазваниеОбъекта
}
: links { n1.<RIGHT_NAME>n2 }
: ngrams { -1 }


// Название может согласовываться с именуемым по падежу:
// Проходим деревню Кочубеевку.
//          ^^^^^^^^^^^^^^^^^^
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=СущСНазв : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=НазваниеОбъекта{ =n1:ПАДЕЖ }
}
: links { n1.<RIGHT_NAME>n2 }
: ngrams { 1 }



// ---------------------------------------

word_set КаталогГалактик = 
{
 "NGC", "Arp", "Messier"
}

wordentry_set ЭлементКаталогаГалактик =
{
 существительное:комета{},
 существительное:галактика{},
 существительное:туманность{},
 существительное:скопление{}  
}

// Найдите на фотографии галактику NGC 15
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=ЭлементКаталогаГалактик : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=КаталогГалактик
 n3=@regex("[[:digit:]].*")
} : links { n1.<RIGHT_NAME>n2.<NEXT_COLLOCATION_ITEM>n3 }


patterns Детализатор export { node:root_node }

// «Бритва (лезвие) Оккама» — методологический принцип, получивший название по имени английского монаха-францисканца
//  ^^^^^^^^^^^^^^^
// Низкоорганизованные многоклеточные животные — губки — тоже не имеют нервной системы.
//                                    ^^^^^^^^^^^^^^^^^^
pattern Сущ0
{
 n=существительное:*{} : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 attr=Детализатор
} : links { n.<DETAILS>attr }
: ngrams { -20 }

// Если детализатор - существительное или прилагательное, то достоверность связывания высокая

patterns ДетализаторСущ export { node:root_node ПАДЕЖ }

// Кошки (типичные ночные хищники)
pattern ДетализаторСущ
{
 beg='('
 n=ГруппаСущ4 : export { node:root_node ПАДЕЖ }
 end=')'
}
: links {
         n.{
            <PUNCTUATION>beg
            <PUNCTUATION>end
           }
        }


patterns ГруппаПрил2 export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

pattern ДетализаторСущ
{
 beg='('
 n=ГруппаПрил2 : export { node:root_node ПАДЕЖ }
 end=')'
}
: links {
         n.{
            <PUNCTUATION>beg
            <PUNCTUATION>end
           }
        }


word_set ПрефиксДетализаторСущ1=
{
 "т.е.",
 "то есть",
 "как",
 "да и",
 "или",
 "либо"
}


collocation_set ПрефиксДетализаторСущ2=
{
 "как и"
}


patterns ПрефиксДетализаторСущ export { node:root_node }
pattern ПрефиксДетализаторСущ
{
 ПрефиксДетализаторСущ1 : export { node:root_node }
}

pattern ПрефиксДетализаторСущ
{
 ПрефиксДетализаторСущ2 : export { node:root_node }
}

patterns ГруппаНареч2 export { node:root_node }

// Кошки (т.е. ночные хищники)
pattern ДетализаторСущ
{
 beg='('
 p=ПрефиксДетализаторСущ
 n=ГруппаСущ4 : export { node:root_node ПАДЕЖ }
 adv=@optional(ГруппаНареч2)
 end=')'
}
: links {
         n.{
            <PUNCTUATION>beg
            <BEG_INTRO>p
            ~<ATTRIBUTE>adv
            <PUNCTUATION>end
           }
        }

// Спутник, то есть первый искуственный орбитальный объект, был запущен.
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ДетализаторСущ
{
 beg=','
 p=ПрефиксДетализаторСущ2
 n=ГруппаСущ4 : export { node:root_node ПАДЕЖ }
 @noshift(ПравыйОграничительОборота)
 end=@coalesce(',')
}
: links {
         n.{
            <PUNCTUATION>beg
            <BEG_INTRO>p
            ~<PUNCTUATION>end
           }
        }
: ngrams { -4 }



pattern Сущ0
{
 n=существительное:*{} : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 attr=ДетализаторСущ
} : links { n.<DETAILS>attr }


// ------------------------------------


// в августе-сентябре.
//   ^^^^^^^^^^^^^^^^
pattern Сущ0 export { РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 noun=НазваниеМесяца:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 t='-'
 n2=НазваниеМесяца{ =Noun:Падеж }
} : links
{
 noun.<RIGHT_LOGIC_ITEM>t.<NEXT_COLLOCATION_ITEM>n2
}

 
// Бизнес-план мини-гостиницы
//             ^^^^^^^^^^^^^^
pattern Сущ0
{
 w1='мини'
 w2='-'{ /*tokenizer_flag:word_conjunction*/ }
 n=существительное:* {} :export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ ПАДЕЖВАЛ CharCasing }
} : links { n.<NEXT_COLLOCATION_ITEM>w2.<NEXT_COLLOCATION_ITEM>w1 }

// в первой половине невероятным образом промахнулся экс-форвард московского ЦСКА
//                                                   ^^^^^^^^^^^
pattern Сущ0
{
 w1='экс'
 w2='-'{ /*tokenizer_flag:word_conjunction*/ }
 n=существительное:* { одуш:одуш } :export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ ПАДЕЖВАЛ CharCasing }
} : links { n.<NEXT_COLLOCATION_ITEM>w2.<NEXT_COLLOCATION_ITEM>w1 }


// Составные существительные с известными префиксами:
// квази-обезьяны
// ^^^^^^^^^^^^^^
pattern Сущ0
{
 prefix=ПРЕФИКС_СОСТАВ_СУЩ:*{}
 t='-'{ [-1]tokenizer_flag:word_conjunction }
 n=существительное:*{} : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
}
: links { n.<PREFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>prefix }
: ngrams { 1 }


// составные существительные с неизвестными префиксами:
// фотографии лучших дизайн-проектов
//                   ^^^^^^^^^^^^^^^
pattern Сущ0
{
 w1=@regex("[\\@А\\@Б\\@Й]*")
 w2='-'{ tokenizer_flag:word_conjunction }
 n=существительное:* {} :export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ ПАДЕЖВАЛ CharCasing }
} : links { n.<PREFIX_PARTICLE>w2.<NEXT_COLLOCATION_ITEM>w1 }
  : ngrams { -3 }



// Найдите на фотографии галактику M31
//                       ^^^^^^^^^^^^^
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=ЭлементКаталогаГалактик : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=@regex("M[[:digit:]].*")
} : links { n1.<RIGHT_NAME>n2 }

// Найдите на фотографии NGC 15
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=КаталогГалактик : export{ РОД:СР ПАДЕЖ:ИМ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 n2=@regex("[[:digit:]].*")
} : links { n1.<RIGHT_NAME>n2 }

// Найдите на фотографии M31
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 @regex("M[[:digit:]].*"): export{ РОД:СР ПАДЕЖ:ИМ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
}

wordentry_set НазванияПланет =
{
 существительное:Меркурий{},
 существительное:Венера{},
 существительное:Земля{},
 существительное:Марс{},
 существительное:Юпитер{},
 существительное:Сатурн{},
 существительное:Нептун{},
 существительное:Уран{CharCasing:FirstCapitalized},
 существительное:Плутон{}
}

// Планету Юпитер можно наблюдать невооруженным глазом
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=существительное:планета{} : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=НазванияПланет{ ЧИСЛО:ЕД ПАДЕЖ:ИМ }
} : links { n1.<RIGHT_NAME>n2 }

word_set НазванияСозвездий = 
{
 "Волопас"
}

// После захода Солнца вы увидите созвездие Волопас
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=существительное:созвездие{} : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=НазванияСозвездий
} : links { n1.<RIGHT_NAME>n2 }

// ---------------------------------------

// нож
pattern Сущ0
{
 существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
}

// Для некоторых названий профессий существительное мужского рода
// может выступать в качестве женского:
// в комнату вошла администратор гостиницы
wordentry_set НазвПрофЖен=существительное:{
//ГЛАВА, АГРОМЕТЕОРОЛОГ, ВЕТИНСПЕКТОР, "ВИЦЕ-ПРЕДСЕДАТЕЛЬ", "ВИЦЕ-КОНСУЛ", ДИСТРИБЬЮТОР, 
//ДИРЕКТОР, СУДЬЯ, ЮРИСТКОНСУЛЬТ
администратор,
АБИТУРИЕНТ, АВИАДИСПЕТЧЕР, АВТОШТУРМАН, АГЕНТ, АГИТАТОР, АГРОТЕХНИК,
АДВОКАТ, АКАДЕМИК, АКВАЛАНГИСТ, АЛЛЕРГОЛОГ, АНАЛИТИК, АРБИТР, АСТРОНАВТ, АУДИТОР,
БИОЛОГ, БОРТИНЖЕНЕР, БОТАНИК, БРИГАДИР, БУХГАЛТЕР, ВЕНЕРОЛОГ, ВЕТВРАЧ, ВЕТЕРИНАР,
"ВИЦЕ-ГУБЕРНАТОР", "ВИЦЕ-ПРЕЗИДЕНТ",
ВОДИТЕЛЬ, ВОСПИТАТЕЛЬ, ВРАЧ, ГЕМАТОЛОГ, ГЕНДИРЕКТОР, ГЕНКОНСУЛ, ГЕНПРОКУРОР, ГЕНСЕК,
ГЕОГРАФ, ГЕРПЕТОЛОГ, ГИД, ГИНЕКОЛОГ,
ГЛАВВРАЧ, ГОССЕКРЕТАРЬ, ДАНТИСТ,  ДЕКОРАТОР, ДЕЛОПРОИЗВОДИТЕЛЬ, ДИЕТОЛОГ, ДИЗАЙНЕР,
ДИКТОР, ДИЛЕР, ЗАМГЛАВЫ, ЗАММИНИСТРА, ЗАМНАЧАЛЬНИКА, ЗАМПРЕД,
ЗАМРУКОВОДИТЕЛЯ,  ЗАЯВИТЕЛЬ,  ИЗДАТЕЛЬ, ИЛЛЮСТРАТОР, ИММУНОЛОГ, ИНЖЕНЕР, ИНСПЕКТОР,
ИНСТРУКТОР, ИНТЕРВЬЮЕР, КАНЦЛЕР, КАРДИОЛОГ, КАССИР, КОЛЛЕГА, КОНСУЛ, КООРДИНАТОР,
КРИТИК, НАЧАЛЬНИК, КУРАТОР, ПРОКУРОР, ЮРИСТ, КОНСУЛЬТАНТ,
ПРЕЗИДЕНТ, "ПРЕМЬЕР-МИНИСТР", МИНИСТР
}

pattern Сущ0
{
 НазвПрофЖен{ число:ед } : export{ РОД:ЖЕН ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : ngrams { -1 }


// Некоторые прилагательные субстантивируются без штрафа:
// пятеро военнослужащих коалиции
//        ^^^^^^^^^^^^^^
wordentry_set СубстантивПрилагМн=прилагательное:{
 военнослужащий,  провожающий,  митингующий, протестующий,
 голодающий, встречающий, выступающий, заключенный, осужденный
}

pattern Сущ0
{
 СубстантивПрилагМн{ СТЕПЕНЬ:АТРИБ ЧИСЛО:МН } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ:ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : ngrams { -2 }

wordentry_set СубстантивПрилагМуж=прилагательное:{
военнослужащий, иной, каждый, любой
//ВСЯКИЙ, ЕДИНСТВЕННЫЙ, ИНОЙ, КАЖДЫЙ, ВСЯКИЙ, ЛЮБОЙ, ПЕРВЫЙ,
//ПОСЛЕДНИЙ, ПРЕДПОСЛЕДНИЙ,
}
pattern Сущ0
{
 СубстантивПрилагМуж{ СТЕПЕНЬ:АТРИБ род:МУЖ ЧИСЛО:ЕД } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ:ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : ngrams { -2 }

// слона-то я и не заметил
// ^^^^^^^^
pattern Сущ0
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 t='-'
 p=частица:то{}
} : links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }


// Богиня-таки оказалась уязвимой...
// ^^^^^^^^^^^
pattern Сущ0
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 t='-'
 p=частица:таки{}
} : links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }


// а человек ли она?
//   ^^^^^^^^^^
pattern Сущ0
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 p=частица:ли{}
} : links { n.<POSTFIX_PARTICLE>p }


// вот часы ему было жаль
// ^^^^^^^^
pattern Сущ0
{
 p=частица:вот{}
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : links { n.<PREFIX_PARTICLE>p }
  : ngrams { -1 }



// Э.Свендсен принес Норвегии второе "золото"
//                                   ^^^^^^^^
pattern Сущ0
{
 q1=ОткрывающаяКавычка
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 q2=ЗакрывающаяКавычка
} : links
{
 n.{
    <PREPEND_QUOTE>q1
    <APPEND_QUOTE>q2
   }
}


// появление же посторонних грозит изменить мои планы.
// ^^^^^^^^^^^^
pattern Сущ0
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 x=частица:же{}
} : links { n.<POSTFIX_PARTICLE>x }


// появление бы посторонних помогло
// ^^^^^^^^^^^^
pattern Сущ0
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 x=ЧастицаБы
} : links { n.<POSTFIX_PARTICLE>x }



// -----------------------------------------------------
// существительное и левое определение (прилагательное)
// -----------------------------------------------------



// Аппарат управления компании располагается в г. Хабаровске.
//                                             ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 w1='г.' : export { node:root_node }
 w2=ГруппаСущ1{ ОДУШ:НЕОДУШ <в_класс>существительное:город{} } : export { РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ }
} : links { w1.<CONOUN>w2 }


// ???
// Он продолжался на протяжении января и февраля 2007 года.
//                                               ^^^^^^^^^
pattern ГруппаСущ1
{
 c=num_word:*{}
 y='года'{ ПАДЕЖ:РОД } : export { node:root_node РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ }
} : links { y.<ATTRIBUTE>c }
: ngrams { 1 }


// При пожаре погиб хозяин 1938 года рождения.
//                         ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=num_word:*{}
 y='года'{ ПАДЕЖ:РОД } : export { node:root_node РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ }
 n='рождения'{ ПАДЕЖ:РОД }
}
: links { y.{ <ATTRIBUTE>c <RIGHT_GENITIVE_OBJECT>n } }
: ngrams { 2 }



// Что за машина едет прямо в ее сторону?
//     ^^^^^^^^^
pattern ГруппаСущ1
{
 p1=местоим_сущ:что{ ПАДЕЖ:ИМ }
 p2=частица:за{}
 n=ГруппаСущ1{ ПАДЕЖ:ИМ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <PREFIX_PARTICLE>p1 <PREFIX_PARTICLE>p2 } }

// Что за камень ты нашел?
// ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 p1=местоим_сущ:что{ ПАДЕЖ:ВИН }
 p2=частица:за{}
 n=ГруппаСущ1{ ПАДЕЖ:ВИН } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <PREFIX_PARTICLE>p1 <PREFIX_PARTICLE>p2 } }



// уже одно это многого стоило
//     ^^^^^^^^
pattern ГруппаСущ1
{
 a='одно'{ class:прилагательное число:ед }
 n=местоим_сущ:это{ =a:ПАДЕЖ } : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links
{
 n.<ATTRIBUTE>a
}
: ngrams { 1 }


// Общая площадь возгорания составила свыше 1500 кв. метров.
//                                               ^^^^^^^^^^
pattern ГруппаСущ1
{
 a='кв.'
 dot='.'
 n=существительное:*{ одуш:неодуш } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links
{
 n.<ATTRIBUTE>a.<NEXT_COLLOCATION_ITEM>dot
}



// полное отсутствие чего бы то ни было
//                   ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 n=местоим_сущ:что{} : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 p1=частица:бы{}
 p2=местоим_сущ:то{ ПАДЕЖ:ИМ }
 ni=частица:ни{}
 v='было'
} : links
{
 n.{
    <POSTFIX_PARTICLE>p1
    <NEXT_COLLOCATION_ITEM>v.{
                              <NEGATION_PARTICLE>ni
                              <SUBJECT>p2
                             }
   }
}
: ngrams { 2 }

// никаких признаков кого бы то ни было
//                   ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 n=местоим_сущ:кто{} : export { РОД:МУЖ ПАДЕЖ ЧИСЛО:ЕД ОДУШ:ОДУШ МОДАЛЬНЫЙ ПАДЕЖВАЛ CHARCASING node:root_node }
 p1=частица:бы{}
 p2=местоим_сущ:то{ ПАДЕЖ:ИМ }
 ni=частица:ни{}
 v='было'
} : links
{
 n.{
    <POSTFIX_PARTICLE>p1
    <NEXT_COLLOCATION_ITEM>v.{
                              <NEGATION_PARTICLE>ni
                              <SUBJECT>p2
                             }
   }
}
: ngrams { 2 }




// --------------------------------------------

patterns ЭТО_Местоим export { node:root_node }
pattern ЭТО_Местоим
{
 местоим_сущ:это{падеж:им} : export { node:root_node }
}

// но была ли то настоящая безопасность?
//            ^^
pattern ЭТО_Местоим
{
 местоим_сущ:то{падеж:им} : export { node:root_node }
}

// все это было очень необычно
// ^^^^^^^
pattern ЭТО_Местоим
{
 местоим_сущ:все это{падеж:им} : export { node:root_node }
} : ngrams { 1 }

// --------------------------------------------


// Черт возьми, что это за сигналы поступили туда?
//              ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1 export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING (ПАДЕЖВАЛ) node:root_node }
{
 w1=местоим_сущ:что{ падеж:им }
 w2=ЭТО_Местоим
 w3=частица:за{}
 n=ГруппаСущ4{ ПАДЕЖ:ВИН } : export { РОД ПАДЕЖ:ИМ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING node:root_node }
} : links
{
 n.{
    <NEXT_COLLOCATION_ITEM>w1
    <NEXT_COLLOCATION_ITEM>w2
    <NEXT_COLLOCATION_ITEM>w3
   }
}
: ngrams { 2 }


pattern ГруппаСущ1 export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING (ПАДЕЖВАЛ) node:root_node }
{
 w1=местоим_сущ:что{ падеж:им }
 w2=ЭТО_Местоим
 w3=предлог:за{}
 n=ГруппаСущ4{ ПАДЕЖ:ИМ } : export { РОД ПАДЕЖ:ИМ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING node:root_node }
} : links
{
 n.{
    <NEXT_COLLOCATION_ITEM>w1
    <NEXT_COLLOCATION_ITEM>w2
    <NEXT_COLLOCATION_ITEM>w3
   }
}
: ngrams { 3 }



// только за последние несколько недель стало еще хуже
//           ^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ПАДЕЖ:ВИН ЧИСЛО:МН ОДУШ:НЕОДУШ }
 q=наречие:несколько{}
 n=Сущ0{ ЧИСЛО:МН ПАДЕЖ:РОД }:export{ РОД ПАДЕЖ:ВИН ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links
{
 n.{
    <ATTRIBUTE>adj
    <ATTRIBUTE>q
   }
}
: ngrams { adj_noun_score( adj, n ) }


// Наступило второе августа.
//           ^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 h=прилагательное:*{ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ЧИСЛО:ЕД РОД:СР ТИП_ПРИЛ:ПОРЯДК } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 t=НазваниеМесяца{ падеж:род число:ед }
} : links
{
 h.<RIGHT_GENITIVE_OBJECT>t
}





// последние несколько человек прошли
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ПАДЕЖ:ИМ ЧИСЛО:МН }
 q=наречие:несколько{}
 n=Сущ0{ ЧИСЛО:МН ПАДЕЖ:РОД }:export{ РОД ПАДЕЖ:ИМ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links
{
 n.{
    <ATTRIBUTE>adj
    <ATTRIBUTE>q
   }
}
: ngrams { adj_noun_score( adj, n ) }




// Суперлатив в атрибутивной роли:
// ленивейший кот
pattern ГруппаСущ1
{
 adj=Суперлатив0
 n=Сущ0{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams { adj_noun_score( adj, n ) }


// Особый случай - компаратив Б^ОЛЬШИЙ, который может быть левым модификатором существительных:
// вы могли бы проявить большее уважение.
//                      ^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:больший{}
 n=Сущ0{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }




// Из-за ремонта музея дети лишились крупнейшей в центре Риги игровой площадки
//                                   ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=Суперлатив0
 adj2=ГруппаПрил2{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД }
 n=Сущ0{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links
{
 n.{
    <ATTRIBUTE>adj
    <ATTRIBUTE>adj2
   }
}
: ngrams
{
 adj_noun_score( adj, n )
 adj_noun_score( adj2, n )
}


// В это самое время волновался скот.
//   ^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:этот{}
 adj2=прилагательное:самый{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД }
 n=Сущ0{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links
{
 n.{
    <ATTRIBUTE>adj
    <ATTRIBUTE>adj2
   }
}
: ngrams
{
 1
}



// нож
pattern ГруппаСущ1
{
 Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}

// Не отец приехал?
// ^^^^^^^
pattern ГруппаСущ1
{
 x=частица:не{}
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<NEGATION_PARTICLE>x }

// Не отец ли приехал?
// ^^^^^^^^^^
pattern ГруппаСущ1
{
 x=частица:не{}
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 y=частица:ли{}
} : links
{
 n.{
    <NEGATION_PARTICLE>x
    <POSTFIX_PARTICLE>y
   }
}

// Не отец же приехал?
// ^^^^^^^^^^
pattern ГруппаСущ1
{
 x=частица:не{}
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 y=частица:же{}
} : links
{
 n.{
    <NEGATION_PARTICLE>x
    <POSTFIX_PARTICLE>y
   }
}


// "своего рода" - адъективный оборот
// возникло своего рода равновесие
pattern ГруппаСущ1
{
 x1='своего'{ падеж:род род:муж }
 x2='рода' { падеж:род }
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>x1.<NEXT_COLLOCATION_ITEM>x2 }
  : ngrams { 1 }


// Димка налил себе еще чаю
//                  ^^^^^^^
pattern ГруппаСущ1
{
 q=наречие:еще{}
 n=Сущ0{ ПАДЕЖ:ПАРТ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>q }
  : ngrams { -1 }


// произвести "зачистку"
//            ^^^^^^^^^^
pattern ГруппаСущ1
{
 q1=ОткрывающаяКавычка
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 q2=ЗакрывающаяКавычка
} : links
{
 n.{
    <PREPEND_QUOTE>q1
    <APPEND_QUOTE>q2
   }
}


tree_scorers ВалентностьСущ

wordentry_set RuAdjCannotModifNoun=прилагательное:{ оба }

// тупой нож
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ЧИСЛО:ЕД ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:ЕД [-7]=Adj:ПАДЕЖ [-7]=Adj:РОД [-7]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}

// Шью интерьерных кукол
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ЧИСЛО:МН ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:МН [-7]=Adj:ПАДЕЖ [-2]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}


// Стимулирует улучшение естественных вкусовых качеств плодов.
//                       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj1=ГруппаПрил2{ ЧИСЛО:МН ~RuAdjCannotModifNoun }
 adj2=ГруппаПрил2{ ЧИСЛО:МН =Adj1:ПАДЕЖ =Adj1:ОДУШ ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:МН [-7]=Adj1:ПАДЕЖ [-2]=Adj1:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj1 <ATTRIBUTE>adj2 } }
: ngrams
{
 adj_noun_score( adj1, n )
 adj_noun_score( adj2, n )
 ВалентностьСущ(n)
}

// Никаких дополнительных материальных вложений не требуется!!!
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj1=ГруппаПрил2{ ЧИСЛО:МН ~RuAdjCannotModifNoun }
 adj2=ГруппаПрил2{ ЧИСЛО:МН =Adj1:ПАДЕЖ =Adj1:ОДУШ ~RuAdjCannotModifNoun }
 adj3=ГруппаПрил2{ ЧИСЛО:МН =Adj1:ПАДЕЖ =Adj1:ОДУШ ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:МН [-7]=Adj1:ПАДЕЖ [-2]=Adj1:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj1 <ATTRIBUTE>adj2 <ATTRIBUTE>adj3 } }
: ngrams
{
 adj_noun_score( adj1, n )
 adj_noun_score( adj2, n )
 adj_noun_score( adj3, n )
 ВалентностьСущ(n)
}

// Продам красивое розовое платье - тунику!
//        ^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj1=ГруппаПрил2{ ЧИСЛО:ЕД ~RuAdjCannotModifNoun }
 adj2=ГруппаПрил2{ ЧИСЛО:ЕД =Adj1:ПАДЕЖ =Adj1:ОДУШ =Adj1:РОД ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:ЕД [-7]=Adj1:ПАДЕЖ [-7]=Adj1:РОД [-2]=Adj1:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj1 <ATTRIBUTE>adj2 } }
: ngrams
{
 adj_noun_score( adj1, n )
 adj_noun_score( adj2, n )
 ВалентностьСущ(n)
}


// Продам новое белое пышное свадебное платье.
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj1=ГруппаПрил2{ ЧИСЛО:ЕД ~RuAdjCannotModifNoun }
 adj2=ГруппаПрил2{ ЧИСЛО:ЕД =Adj1:ПАДЕЖ =Adj1:ОДУШ =Adj1:РОД ~RuAdjCannotModifNoun }
 adj3=ГруппаПрил2{ ЧИСЛО:ЕД =Adj1:ПАДЕЖ =Adj1:ОДУШ =Adj1:РОД ~RuAdjCannotModifNoun }
 adj4=ГруппаПрил2{ ЧИСЛО:ЕД =Adj1:ПАДЕЖ =Adj1:ОДУШ =Adj1:РОД ~RuAdjCannotModifNoun }
 n=Сущ0{ [-7]ЧИСЛО:ЕД [-7]=Adj1:ПАДЕЖ [-7]=Adj1:РОД [-2]=Adj1:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj1 <ATTRIBUTE>adj2 <ATTRIBUTE>adj3 <ATTRIBUTE>adj4 } }
: ngrams
{
 adj_noun_score( adj1, n )
 adj_noun_score( adj2, n )
 adj_noun_score( adj3, n )
 adj_noun_score( adj4, n )
 ВалентностьСущ(n)
}


// Я беру др. книгу
//        ^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ПРИЛАГАТЕЛЬНОЕ:*{ СТЕПЕНЬ:СОКРАЩ }
 n=Сущ0 : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}


// Его сумасшествие сказывалось лишь негативно.
// ^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ПРИТЯЖ_ЧАСТИЦА:*{}
 n=Сущ0 : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}




// Шел 1937 год.
//     ^^^^^^^^
pattern ГруппаСущ1
{
 adj=num_word:*{}
 n=существительное:год{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}




// Армения встречает Рождество Христово
//                   ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 n=существительное:*{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 adj=прилагательное:*{ КРАТКИЙ ~RuAdjCannotModifNoun [-7]=n:ЧИСЛО [-7]=n:РОД }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 -5
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}



/*
// Несогласованная речь:
// он кушать свежий рыба
//           ^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ГруппаПрил2
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 -9
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}
*/



// Остальные 129 общежитий были переданы в муниципальную собственность.
// ^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ПАДЕЖ:ИМ ЧИСЛО:МН ~RuAdjCannotModifNoun }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ }
 c=num_word:*{}
 n=Сущ0{ ПАДЕЖ:РОД }:export{ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj <ATTRIBUTE>c } }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}

// Взять последние 3 кекса.
//       ^^^^^^^^^^^^^^^^^
// Мы обработали все 15 заявок.
//               ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ ПАДЕЖ:ВИН ЧИСЛО:МН ~RuAdjCannotModifNoun }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ }
 c=num_word:*{}
 n=Сущ0{ ПАДЕЖ:РОД =adj:ОДУШ }:export{ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.{ <ATTRIBUTE>adj <ATTRIBUTE>c } }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}

// --------------------------------------------------------------------



// --------------------------------------------------------------------

// +++++++++++++++++++++++

patterns РимскоеЧисло export { node:root_node }

#pragma language_mix off
// Римские числа - не проверяем корректность, просто смотрим набор
// символов в верхнем регистре. Встречаются случаи с использованием
// букв кириллицы Х и М.
pattern РимскоеЧисло
{
 @regex_strict( "[IVXMХМ]*" ) : export { node:root_node }
}
#pragma language_mix on


// В Москве стартует XIV Чемпионат мира по легкой атлетике.
//                   ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=РимскоеЧисло
 n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}

// +++++++++++++++++++++++

patterns СущСПредложДоп { language=Russian } export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }


// начал писать свою «Полярную записку»
//              ^^^^^^^^^^^^^^^^^^^^^^^
// создать легальный "склад фильмов"
//         ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING (ПАДЕЖВАЛ) node:root_node }
{
 adj=ГруппаПрил2{ ~RuAdjCannotModifNoun }
 q1=ОткрывающаяКавычка
 n=СущСПредложДоп{ [-4]=Adj:ЧИСЛО [-4]=Adj:ПАДЕЖ [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING node:root_node }
 q2=ЗакрывающаяКавычка
} : links
{
 n.{
    <PREPEND_QUOTE>q1
    <APPEND_QUOTE>q2
    <ATTRIBUTE>adj
   }
}
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}


// Особый порядок согласования ОБА для винительном падеже
// она мыла оба уха
//          ^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:оба{ падеж:вин род:ср }
 n=ГруппаСущ1{ число:ед падеж:род род:ср }:export{ РОД ПАДЕЖ:ВИН ЧИСЛО:МН ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>adj }
  : ngrams { 1 }

// она примерила обе шляпки
//               ^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:оба{ падеж:вин род:жен }
 n=ГруппаСущ1{ число:ед падеж:род род:жен }:export{ РОД ПАДЕЖ:ВИН ЧИСЛО:МН ОДУШ МОДАЛЬНЫЙ ПАДЕЖВАЛ CharCasing node:root_node }
} : links { n.<ATTRIBUTE>adj }
  : ngrams { 1 }


// Пилот болида пропустил оба заезда
//                        ^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:оба{ падеж:вин род:муж }
 n=ГруппаСущ1{ число:ед падеж:род род:муж =adj:одуш }:export{ РОД ПАДЕЖ:ВИН ЧИСЛО:МН ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>adj }
  : ngrams { 1 }

// Оба сына уехали
// ^^^^^^^^
// Обе дочери уехали
// ^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:оба{ падеж:им }
 n=ГруппаСущ1{ число:ед падеж:род =adj:род }:export{ РОД ПАДЕЖ:ИМ ЧИСЛО:МН ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>adj }
  : ngrams { 1 }

// оба моих спутника молчали.
// ^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=прилагательное:оба{ падеж:им }
 adj2=ГруппаПрил2{ ПАДЕЖ:РОД ЧИСЛО:МН }
 n=ГруппаСущ1{ число:ед падеж:род =adj:род }:export{ РОД ПАДЕЖ:ИМ ЧИСЛО:МН ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : links { n.{
               <ATTRIBUTE>adj
               <ATTRIBUTE>adj2
              }
          }
: ngrams { 1 }


 // подталкивать обеими руками
 //              ^^^^^^^^^^^^^
 pattern ГруппаСущ1
 {
  adj=прилагательное:оба{ ПАДЕЖ:ТВОР }
  n=ГруппаСущ1{ ПАДЕЖ:ТВОР =Adj:ЧИСЛО =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>adj }
 : ngrams { adj_noun_score( adj, n ) }

 // Кидать с обеих рук
 //          ^^^^^^^^^
 pattern ГруппаСущ1
 {
  adj=прилагательное:оба{ ПАДЕЖ:РОД }
  n=ГруппаСущ1{ ПАДЕЖ:РОД =Adj:ЧИСЛО =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>adj }
 : ngrams { adj_noun_score( adj, n ) }

 // обеим рукам
 pattern ГруппаСущ1
 {
  adj=прилагательное:оба{ ПАДЕЖ:ДАТ }
  n=ГруппаСущ1{ ПАДЕЖ:ДАТ =Adj:ЧИСЛО =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>adj }
 : ngrams { adj_noun_score( adj, n ) }

 // об обеих руках
 pattern ГруппаСущ1
 {
  adj=прилагательное:оба{ ПАДЕЖ:ПРЕДЛ }
  n=ГруппаСущ1{ ПАДЕЖ:ПРЕДЛ =Adj:ЧИСЛО =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>adj }
 : ngrams { adj_noun_score( adj, n ) }


 // Только человек справится.
 // ^^^^^^^^^^^^^^
 patterns ЧастМодифСущ export { node:root_node }
 pattern ЧастМодифСущ
 {
  наречие:*{ ТИП_МОДИФ:СУЩ2 } : export { node:root_node }
 }

 // В пруду разводили не только карпов
 //                   ^^^^^^^^^
 pattern ЧастМодифСущ
 {
  p=частица:не{}
  q=наречие:только{} : export { node:root_node }
 } : links { q.<NEGATION_PARTICLE>p }


 // вдруг послышался легкий шорох или даже скрежет
 //                                   ^^^^^^^^^^^^
 // пусть здесь будут жить только хорошие люди!
 //                        ^^^^^^^^^^^^^^^^^^^^
 pattern ГруппаСущ1
 {
  p=ЧастМодифСущ
  n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>p }
 : ngrams { -1 }

 // но прошло еще мгновение
 //           ^^^^^^^^^^^^^
 pattern ГруппаСущ1
 {
  p=наречие:еще{}
  n=ГруппаСущ1{ ИнтервалВремени }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<ATTRIBUTE>p }
 : ngrams { -1 }

 // Чтобы не путать с формой прилагательного ВЕСЬ:
 // Мы затратили всего час
 pattern ГруппаСущ1
 {
  p=наречие:всего{}
  n=ГруппаСущ1{ ~ПАДЕЖ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }
 : links { n.<PREFIX_PARTICLE>p }
 : ngrams { -1 }


// существительное в партитиве согласуется с прилагательным в родительном падеже:
// выпей крепкого чаю
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ПАДЕЖ:РОД}
 n=Сущ0{ ПАДЕЖ:ПАРТ [-4]=Adj:ЧИСЛО [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}


// существительное в локативе согласуется с прилагательным в предложном падеже:
// на лугу пасутся коровы
pattern ГруппаСущ1
{
 adj=ГруппаПрил2{ПАДЕЖ:ПРЕДЛ}
 n=Сущ0{ ПАДЕЖ:МЕСТ [-4]=Adj:ЧИСЛО [-4]=Adj:РОД [-4]=Adj:ОДУШ }:export{ РОД ПАДЕЖ:ПРЕДЛ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
} : links { n.<ATTRIBUTE>adj }
: ngrams
{
 adj_noun_score( adj, n )
 ВалентностьСущ(n)
}






// ни с чьими знакомыми не встречаться
//      ^^^^^^^^^^^^^^^
pattern ГруппаСущ1 export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING (ПАДЕЖВАЛ) node:root_node }
{
 a1=прилагательное:*{ ~КРАТКИЙ число:мн }
 a2=прилагательное:*{ ~КРАТКИЙ число:мн =a1:падеж } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING node:root_node }
} : links { a2.<ATTRIBUTE>a1 }
  : ngrams { -4 }


 // ---------------------------------------------

 patterns ЧислоДляНомера {language=Russian } export { node:root_node }

 pattern ЧислоДляНомера
 {
  числительное:*{ падеж:им } : export { node:root_node }
 }


 pattern ЧислоДляНомера
 {
  NUM_WORD:*{} : export { node:root_node }
 }

 // Питер номер три продолжал молчать.
 // ^^^^^^^^^^^^^^^
 // Питер номер 3 продолжал молчать.
 // ^^^^^^^^^^^^^^^
 pattern ГруппаСущ1
 {
  n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
  w1='номер'{ ПАДЕЖ:ИМ }
  c=ЧислоДляНомера
 } : links
 {
  n.<ATTRIBUTE>w1.<NEXT_COLLOCATION_ITEM>c
 }

 // На энергоблоке № 1 продолжается плановый капитальный ремонт.
 //    ^^^^^^^^^^^^^^^
 pattern ГруппаСущ1
 {
  n=Сущ0:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
  w1='№'
  c=ЧислоДляНомера
 } : links
 {
  n.<ATTRIBUTE>w1.<NEXT_COLLOCATION_ITEM>c
 }




 // Мороз дошел ночью до 16°, днем немного понизился.
 pattern ГруппаСущ1
 {
  n=@regex('[0-9]+°'):export{ РОД:СР ПАДЕЖ ЧИСЛО:МН ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
 }

 // Мощность каждого гребного электродвигателя составляла 600 л. с.
 //                                                           ^^^^^
 pattern ГруппаСущ1
 {
  n1='л':export{ РОД ПАДЕЖ ЧИСЛО ОДУШ:НЕОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
  dot1='.'
  n2='с'
  dot2='.'
 }
 : links { n1.<NEXT_COLLOCATION_ITEM>dot1.
               <NEXT_COLLOCATION_ITEM>n2.
                <NEXT_COLLOCATION_ITEM>dot2
         }



// --------------------------------------------------

// Присоединяемый справа к существительному объект в родительном падеже
patterns РодОбъектСущ export { node:root_node }

 wordentry_set CannotBeGenAdjunct={
 местоим_сущ:себя{},
 местоим_сущ:ничто{}, // Название улицы ничего мне не говорило.
 местоим_сущ:никто{}, // У окна никого не было.
 местоим_сущ:нечто{},
 местоим_сущ:некто{},
 местоимение:я{} // А казаки меня лупят.
}


patterns ГлДополнение export { ПАДЕЖ node:root_node }

// начало большого пути
//                 ^^^^
pattern РодОбъектСущ
{
 ГлДополнение{ ПАДЕЖ:РОД ~CannotBeGenAdjunct } : export { node:root_node }
}





wordentry_set РодДопМест={ местоимение:я{} }

// эта часть меня уже умерла
//           ^^^^
pattern РодОбъектСущ
{
 ГруппаСущ4{ ПАДЕЖ:РОД РодДопМест } : export { node:root_node }
} : ngrams { -4 }



// -----------------------------------------------------------------------------
// существительное с присоединенным справа дополнением в родительном падеже
// -----------------------------------------------------------------------------
patterns СущСРодДоп export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

// группа
pattern СущСРодДоп
{
 ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}


word_set ОбъектнМестоим=
{
 "него", "нее", "ним", "ней", "нему", "нем", "них", "ними"
}

patterns ГруппаМест export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }

// при виде него все замерли
//     ^^^^^^^^^
//
// (-) словно был виноват перед ней
//                        ~~~~~~~~~
pattern СущСРодДоп
{
 n=ГруппаСущ1{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 obj=ГруппаМест{ ОбъектнМестоим }
}
: links { n.<RIGHT_GENITIVE_OBJECT>obj }
: ngrams { -1 }



// На энергоблоке №1 продолжается плановый капитальный ремонт.
//    ^^^^^^^^^^^^^^
// Все дети посещали детский сад №8.
//                   ^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 w1=@regex('№[0-9]+')
} : links
{
 n.<ATTRIBUTE>w1
}


// мы должны найти лестницу наверх
//                 ^^^^^^^^^^^^^^^
wordentry_set ПравНаречДляСущЭлем=наречие:{
ЗДЕСЬ,
наружу,
ВПЕРЕД,
НАЗАД,
ТУТ,
ТУДА,
ОБРАТНО,
ОТСЮДА,
ОТТУДА,
СЮДА,
ТАМ,
ВСМЯТКУ,
ВНУТРИ,
//АНДАНТЕ,
ВБЛИЗИ,
ВБРОД,
ВДАЛИ,
ВВЕРХУ,
ВВЕРХ,
ВКЛЮЧИТЕЛЬНО,
ВНИЗ,
ВНИЗУ,
ВНИЧЬЮ,
ВДВОЕМ,
ВТРОЕМ,
ВЧЕТВЕРОМ,
ВШЕСТЕРОМ,
ВПЯТЕРОМ,
ВСЕМЕРОМ,
ВДЕСЯТЕРОМ,
ВСЕРЬЕЗ,
ВСТЫК,
КВЕРХУ,
//КЗАДИ,
'КРЕСТ-НАКРЕСТ',
ЛИЧНО,
'НА ПЛАВУ',
НАВЕРХ,
НАВЕРХУ,
НАГИШОМ,
НАЗЛО,
НАПОКАЗ,
НАПРОТИВ,
НАПРЯМУЮ,
НЕКСТАТИ,
НЕПОДАЛЕКУ,
ПОВЕРХ,
ПОПОЛУНОЧИ,
'ПРЕЖДЕ ВСЕГО',
ПРЕИМУЩЕСТВЕННО,
ПРЯМИКОМ,
СБОКУ,
СЛЕДОМ,
СНИЗУ,
СООТВЕТСТВЕННО,
//ФРИ,
ОПТОМ,
//+'В РОЗНИЦУ',
ВХОЛОСТУЮ,
наоборот,
ДОМОЙ,
налево,
слева,
справа,
посередине,
направо
}

patterns ПравНаречДляСущ export { node:root_node }
pattern ПравНаречДляСущ
{
 ПравНаречДляСущЭлем : export { node:root_node }
}

// Последовал резкий рывок вперед и вверх
//                         ^^^^^^^^^^^^^^
pattern ПравНаречДляСущ
{
 a1=ПравНаречДляСущЭлем : export { node:root_node }
 conj=ЛогичСоюз
 a2=ПравНаречДляСущЭлем
} : links { a1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a2 }


tree_scorers СущИНаречие

pattern СущСРодДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 adv=ПравНаречДляСущ
}
: links { n.<ATTRIBUTE>adv }
: ngrams
{
 -10
 СущИНаречие(n)
 adv_noun_score(adv,n)
}



patterns ПравНаречСопостДляСущ export { node:root_node }
pattern ПравНаречСопостДляСущ
{
 наречие:*{ ОБСТ_ВАЛ:СОПОСТАВЛЕНИЕ } : export { node:root_node }
}

// Последовал резкий рывок вперед и вверх
//                         ^^^^^^^^^^^^^^
pattern ПравНаречСопостДляСущ
{
 a1=наречие:*{ ОБСТ_ВАЛ:СОПОСТАВЛЕНИЕ } : export { node:root_node }
 conj=ЛогичСоюз
 a2=наречие:*{ ОБСТ_ВАЛ:СОПОСТАВЛЕНИЕ }
} : links { a1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a2 }


// Большое количество наречий типа ПО-АНГЛИЙСКИ помечены тегом ОБСТ_ВАЛ:СОПОСТАВЛЕНИЕ:
// шашлык по-грузински
pattern СущСРодДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 adv=ПравНаречСопостДляСущ
}
: links { n.<ATTRIBUTE>adv }
: ngrams
{
 -2
 СущИНаречие(n)
 adv_noun_score(adv,n)
}


// Ни одна из предложенных на сегодняшний день моделей
// ^^^^^^^^^^ ...
pattern СущСРодДоп
{
 ni=частица:ни{}
 a=прилагательное:один{ ЧИСЛО:ЕД }:export{ node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }
 p=предлог:из{}
 n=ГруппаСущ4{ =a:РОД ПАДЕЖ:РОД ЧИСЛО:МН }
}
: links
{
 a.{
    <NEGATION_PARTICLE>ni
    <PREPOS_ADJUNCT>p.<OBJECT>n
   }
}
: ngrams
{
 1
}


// большинство из нас уже мертвы или умирают
// ^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 a=существительное:большинство{ ЧИСЛО:ЕД }:export{ node:root_node РОД ПАДЕЖ ЧИСЛО:МН ОДУШ }
 p=предлог:из{}
 n=РодОбъектСущ
}
: links { a.<PREPOS_ADJUNCT>p.<OBJECT>n }
: ngrams
{
 1
}


 patterns ИнострСлово export { node:root_node }

 // В роли присоединяемого существительного в родительном падеже выступает цепочка
 // слов на латинице:
 // Генеральный директор Nakamura International Corporation
 pattern СущСРодДоп
 {
  n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  n2=ИнострСлово
 } : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }


// ... выселить из квартиры бывшую супругу Ирину
//                          ^^^^^^^^^^^^^^^^^^^^
// По дороге ему встретилась герцогиня Ангулемская.
//                           ^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ одуш:одуш число:ед }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=существительное:*{ одуш:одуш =n1:РОД =n1:падеж CHARCASING:FirstCapitalized }
}
: ngrams { -1 }
: links { n1.<RIGHT_NAME>n2 }




// Расходы составляют 80% бюджета
//                    ^^^^^^^^^^^
// Топ-менеджер Google избавится от 42% акций компании
//                               ^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп export{ РОД (ПАДЕЖ) ЧИСЛО ОДУШ node:root_node }
{
 n1=@regex("[[:digit:]]*[023456789]%"):export{ РОД:МУЖ ЧИСЛО:МН ОДУШ:НЕОДУШ node:root_node }
 n2=ГруппаСущ4{ ПАДЕЖ:РОД }
} : links
{
 n1.<RIGHT_GENITIVE_OBJECT>n2
}

// Топ-менеджер Google избавится от 41% акций компании
//                               ^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп export{ РОД (ПАДЕЖ) ЧИСЛО ОДУШ node:root_node }
{

 n1=@regex("[[:digit:]]*1%"):export{ РОД:МУЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 n2=ГруппаСущ4{ ПАДЕЖ:РОД }
} : links
{
 n1.<RIGHT_GENITIVE_OBJECT>n2
}


 // журнал "Охота и рыболовство"
 pattern СущСРодДоп
 {
  n1=ГруппаСущ1{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  q1=ОткрывающаяКавычка
  n2=ГруппаСущ4{ ПАДЕЖ:ИМ }
  q2=ЗакрывающаяКавычка
 } : links
 {
  n1.<RIGHT_NAME>n2.{
                     <PREPEND_QUOTE>q1
                     <APPEND_QUOTE>q2
                    }
 }
 : ngrams { 3 }


 // Для разбора названий надо сопоставлять цепочки произвольных слов
 patterns ЭлементВНазвании export { ПАДЕЖ node:root_node }
 pattern ЭлементВНазвании
 {
  *:*{ падеж:им } : export { ПАДЕЖ node:root_node }
 }

 pattern ЭлементВНазвании
 {
  *:*{ ~падеж:им } : export { ПАДЕЖ node:root_node }
 } : ngrams { -1 } // предпочитаем именительный падеж для названий

 patterns ЦепочкаСловКакНазвание export { ПАДЕЖ node:root_node }

 // Число жертв снежной бури "Немо" выросло до 9 человек
 //                           ^^^^
 pattern ЦепочкаСловКакНазвание
 {
  ЭлементВНазвании : export { ПАДЕЖ node:root_node }
 }

 // Ракета-носитель «Зенит-3SL» упала в Тихий океан
 //                  ^^^^^^^^^
 pattern ЦепочкаСловКакНазвание
 {
  head=ЭлементВНазвании : export { ПАДЕЖ node:root_node }
  tail=ЭлементВНазвании
 } : links { head.<NEXT_COLLOCATION_ITEM>tail }

 pattern ЦепочкаСловКакНазвание
 {
  head=ЭлементВНазвании : export { ПАДЕЖ node:root_node }
  tail=ЭлементВНазвании
  tail2=ЭлементВНазвании
 } : links { head.<NEXT_COLLOCATION_ITEM>tail.<NEXT_COLLOCATION_ITEM>tail2 }


 // Итальянская авиакомпания "Алиталия" дышит на ладан.
 //             ^^^^^^^^^^^^^^^^^^^^^^^            
 // журнал "Понурому птючу"
 pattern СущСРодДоп
 {
  n1=ГруппаСущ1{ ОДУШ:НЕОДУШ } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  q1=ОткрывающаяКавычка
  n2=ЦепочкаСловКакНазвание{ [-4]ПАДЕЖ:ИМ }
  q2=ЗакрывающаяКавычка
 } : links
 {
  n1.<RIGHT_NAME>n2.{
                     <PREPEND_QUOTE>q1
                     <APPEND_QUOTE>q2
                    }
 }
 : ngrams { 2 }




 // САМ СЕБЯ в роди родительного дополнения:
 // ты сейчас есть бледный отблеск самого себя.
 pattern СущСРодДоп
 {
  n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  a=ПРИЛАГАТЕЛЬНОЕ:САМ{ ПАДЕЖ:РОД }
  n2=МЕСТОИМ_СУЩ:СЕБЯ{ ПАДЕЖ:РОД }
 } : links { n1.<RIGHT_GENITIVE_OBJECT>n2.<ATTRIBUTE>a }
   : ngrams { 1 }

 // ------------------------------------------------






// Причастный оборот справа от именной группы, с отбивкой одной или
// двумя запятыми
patterns ПравПричОборот export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }



 // студенты, изучающие физику
 pattern СущСРодДоп
 {
  @mark(ГруппаСущ1,N):export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  p=ПравПричОборот{ =N:ПАДЕЖ =N:РОД =N:ЧИСЛО }
 } : links { n.<ADJ_PARTICIPLE>p }


 // в лесу, засыпанном снегом
 pattern СущСРодДоп
 {
  n=ГруппаСущ1{ПАДЕЖ:МЕСТ}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  p=ПравПричОборот{ ПАДЕЖ:ПРЕДЛ =N:РОД =N:ЧИСЛО }
 } : links { n.<ADJ_PARTICIPLE>p }

 // группа студентов, изучающая физику
 pattern СущСРодДоп
 {
  n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  n2=РодОбъектСущ
  p=ПравПричОборот{ =N:ПАДЕЖ =N:РОД =N:ЧИСЛО }
 } : links
 {
  n.{
     <RIGHT_GENITIVE_OBJECT>n2
     <ADJ_PARTICIPLE>p
    }
 }


 // ----------------------------------------------------



// --------------------------------------------------------------
// Некоторые существительные управляют другими существительными
// в не-родительном падеже.
// --------------------------------------------------------------

// Управляющее существительное
patterns СущУправлТвор export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
pattern СущУправлТвор
{
 ГруппаСущ1{ ПАДЕЖВАЛ:ТВОР } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// Управляемый объект.
// Может быть не только именем существительным:
// получение наличными
//           ^^^^^^^^^
patterns ТворОбъект export { node:root_node }

// управление транспортным средством
pattern СущСРодДоп
{
 n1=СущУправлТвор:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ТворОбъект
}
: links { n1.<OBJECT>n2 }
: ngrams { сущ_сущ(n1,n2) }


// Полиция проверяет информацию о наезде на пешеходов сотрудницей Центральной избирательной комиссии в Москве
pattern СущСПредложДоп
{
 n1=СущУправлТвор:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 pn=ПредлогИСущ { сущ_предл_сущ( N1.root_node, _.prepos, _.n2 ) }
 obj=ТворОбъект
}
: links
{
 n1.{
     <PREPOS_ADJUNCT>pn
     <OBJECT>obj
    }
}
: ngrams
{
 сущ_сущ(n1,obj)
 prepos_score( n1, pn.prepos )
 ngram3( n1, pn.prepos, pn.n2 )
 ВалентностьСущ(n1)
}


// насыщение железа углеродом
// Первые опыты с раздражением отдельных точек коры мозга электрическим током
pattern СущСРодДоп
{
 n1=СущУправлТвор:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=РодОбъектСущ
 n3=ТворОбъект
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <OBJECT>n3
    }
}
: ngrams
{
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}

// Следующим шагом должно стать создание Брюсселем единой системы финансового надзора
//                              ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=СущУправлТвор:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ТворОбъект
 n3=РодОбъектСущ
} : links
{
 n1.{
     <OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams
{
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}

/*
// легкий укол зонтиком
pattern СущСРодДоп
{
 a=ГруппаПрил2
 n1=СущУправлТвор{ =a:ПАДЕЖ =a:ЧИСЛО =a:РОД =a:ОДУШ } :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:ТВОР }
} : links
{
 n1.{
     <ATTRIBUTE>a
     <RIGHT_GENITIVE_OBJECT>n2
    }
}
: ngrams
{
 adj_noun_score(a,n1)
 сущ_сущ(n1,n2)
}
*/

/*
// ПРИЛ + СУЩ + СУЩ/род/ + СУЩ/твор/
// ежедневные массовые проверки бизнеса налоговой инспекцией
pattern СущСРодДоп
{
 a=ГруппаПрил2
 n1=СущУправлТвор{ =a:ПАДЕЖ =a:ЧИСЛО =a:РОД =a:ОДУШ } :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:РОД }
 n3=ГруппаСущ1{ ПАДЕЖ:ТВОР }
} : links
{
 n1.{
     <ATTRIBUTE>a
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams
{
 adj_noun_score(a,n1)
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}
*/

/*
// ПРИЛ + СУЩ + СУЩ/твор/ + СУЩ/род/
// Почему преодоление самолетом звукового барьера сопровождается взрывоподобным хлопком?
pattern СущСРодДоп
{
 n1=СущУправлТвор :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:ТВОР }
 n3=ГруппаСущ1{ ПАДЕЖ:РОД }
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams
{
 -1 // так как естественным связыванием все-таки является n2+n3
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}
*/






/*
// ---------------------------------------------------
// ну и наконец редчайший случай двойной валентности
// ---------------------------------------------------

wordentry_set СущУправлТворРод=существительное:{
аннулирование, // аннулирование директором вашего заказа (ts аннулирование чего-то)
введение, // введение избиркомом нового формата бюллетеней (введение кем-то)
выдвижение, // выдвижение им своих умозаключений вызвало бурную реакцию (выдвижение + твор)
задержание, // задержание правоохранительными органами преступника (задержание + твор)
запуск // после запуска Индией геостационарного спутника (запуск + твор.п.)
}

pattern СущСРодДоп
{
 n1=СущУправлТворРод:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:ТВОР }
 n3=СущСРодДоп{ ПАДЕЖ:РОД }
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams { сущ_сущ(n1,n3.root_node) }

// выдвижение им своих умозаключений вызвало бурную реакцию
pattern СущСРодДоп
{
 n1=СущУправлТворРод:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=местоимение:я{ ПАДЕЖ:ТВОР }
 n3=СущСРодДоп{ ПАДЕЖ:РОД }
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams { сущ_сущ(n1,n3.root_node) }



// передача секретной информации врагу
pattern СущСРодДоп
{
 n1=существительное:передача{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:РОД }
 n3=ГруппаСущ1{ ПАДЕЖ:ДАТ }
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams { сущ_сущ(n1,n2.root_node) }

// передача врагу секретной информации
pattern СущСРодДоп
{
 n1=существительное:передача{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:ДАТ }
 n3=ГруппаСущ1{ ПАДЕЖ:РОД }
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams
{
 1
 сущ_сущ(n1,n3.root_node)
}
*/ 


// ---------------------------------------------------
// отдельно - партитив
// ---------------------------------------------------

// стакан чаю
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=существительное:*{ ПАДЕЖ:ПАРТ }
}
: links { n1.<RIGHT_GENITIVE_OBJECT>n2 }
: ngrams
{
 -1 // штраф за партитив:
    // она открыла глаза свету
    //                   ^^^^^
 сущ_сущ(n1.root_node,n2)
}


// стакан крепкого чаю
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=прилагательное:*{ падеж:род }
 n2=существительное:*{ ПАДЕЖ:ПАРТ =A:РОД =A:ЧИСЛО }
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2.<ATTRIBUTE>a }
: ngrams { сущ_сущ(n1.root_node,n2) }


// стакан крепкого ароматного чаю
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=прилагательное:*{ падеж:род }
 a2=прилагательное:*{ ПАДЕЖ:РОД =A:РОД =A:ЧИСЛО }
 n2=существительное:*{ ПАДЕЖ:ПАРТ =A:РОД =A:ЧИСЛО }
} : links
{
 n1.<RIGHT_GENITIVE_OBJECT>n2.
     <ATTRIBUTE>a.
      <NEXT_COLLOCATION_ITEM>a2
}
: ngrams { сущ_сущ(n1.root_node,n2) }


// счетные наречия - присоединяют родительный падеж существительного и выражают идею количества.

wordentry_set НаречДляРод1 =
{
 наречие:немножко{},
 наречие:немножечко{}, // попробуем организовать для вас немножко природы
 наречие:немного{},
 наречие:много{},
 наречие:мало{}
}


patterns НаречДляРод export { node:root_node }

// одно счетное наречие:
// Я выпил немного компота
//         ^^^^^^^
pattern НаречДляРод
{
 a=НАРЕЧИЕ:*{ ТИП_МОДИФ:СУЩ } : export { node:root_node }
}

// Много ли шишек ты собрал?
// ^^^^^^^^
pattern НаречДляРод
{
 a=НАРЕЧИЕ:*{ ТИП_МОДИФ:СУЩ } : export { node:root_node }
 p=ЧАСТИЦА:ЛИ{}
} : links { a.<POSTFIX_PARTICLE>p }


// сколько же еще человек заметили это?
// ^^^^^^^^^^
pattern НаречДляРод
{
 a=НАРЕЧИЕ:*{ ТИП_МОДИФ:СУЩ } : export { node:root_node }
 p=частица:же{}
} : links { a.<POSTFIX_PARTICLE>p }



patterns МодифНареч export { node:root_node }

// Счетное наречие в положительной степени с модификатором:
// Я выпил слишком много чаю
//         ^^^^^^^^^^^^^
// им нужно совсем немного времени.
//          ^^^^^^^^^^^^^^
pattern НаречДляРод
{
 mod=МодифНареч
 a=НаречДляРод1 { степень:атриб } : export { node:root_node }
}
: links { a.<ATTRIBUTE>mod }
: ngrams
{
 2
 adv_adv_score(mod,a)
}


// для этого потребовалось бы лишь несколько минут.
//                            ^^^^^^^^^^^^^^^^^^^^
// до башни было всего несколько футов
//               ^^^^^^^^^^^^^^^^^^^^^
wordentry_set МодифДляНесколько=наречие:{ еще, всего, лишь, "всего-то" }
pattern НаречДляРод
{
 mod=МодифДляНесколько
 a=НАРЕЧИЕ:*{ ТИП_МОДИФ:СУЩ степень:атриб } : export { node:root_node }
} : links { a.<ATTRIBUTE>mod }
  : ngrams { 4 }

// вон сколько крови потерял!
// ^^^^^^^^^^^
pattern НаречДляРод
{
 mod=наречие:вон{}
 a=наречие:сколько {} : export { node:root_node }
} : links { a.<ATTRIBUTE>mod }
  : ngrams { 5 }


wordform_set БольшеМеньше=
{
 'больше'{ class:наречие степень:сравн },
 'меньше'{ class:наречие степень:сравн }
}

patterns ГруппаСрНареч2 export { node:root_node }

// В разговор включалось все больше мужчин.
//                       ^^^^^^^^^^
pattern НаречДляРод
{
 mod=частица:всё{}
 a=ГруппаСрНареч2{ БольшеМеньше } : export { node:root_node }
} : links { a.<PREFIX_PARTICLE>mod }
  : ngrams { 5 }




 wordentry_set МодифСравнНареч = {
                                  наречие:вдвое{},
                                  наречие:втрое{},
                                  наречие:вчетверо{},
                                  наречие:впятеро{},
                                  наречие:вшестеро{},
                                  наречие:всемеро{},
                                  наречие:вдевятеро{},
                                  наречие:вдесятеро{},
                                  наречие:гораздо{},
                                  наречие:неисчислимо{},
                                  наречие:невообразимо{},
                                  наречие:слегка{},
                                  наречие:чуть{},
                                  наречие:чуть-чуть{},
                                  наречие:чуточку{},
                                  наречие:чуток{},
                                  наречие:немного{},
                                  наречие:намного{}, // остальные кони выглядели намного лучше.
                                  наречие:немножко{}, // попробуй один раз немножко меньше торопиться
                                  наречие:немножечко{},
                                  наречие:ненамного{}, // В городах ситуация сложилась ненамного лучше.
                                  наречие:существенно{},
                                  наречие:ощутимо{},
                                  наречие:еще{},
                                  наречие:значительно{}, // из города цель его путешествия казалась значительно ближе
                                  наречие:несколько{},
                                  наречие:всего лишь{}
                                 }


// Счетное наречие в сравнительной форме с модификатором:
// потребуется гораздо меньше труда
pattern НаречДляРод
{
 mod=МодифСравнНареч
 a=НаречДляРод1 { степень:сравн } : export { node:root_node }
} : links { a.<ATTRIBUTE>mod }
  : ngrams { 2 }

// а много ли денег у нас осталось?
//   ^^^^^^^^
pattern НаречДляРод
{
 a=НаречДляРод1 : export { node:root_node }
 p=частица:ли{}
} : links { a.<POSTFIX_PARTICLE>p }
  : ngrams { 2 }

// много же добра принесло мне это знание!
// ^^^^^^^^
pattern НаречДляРод
{
 a=НаречДляРод1 : export { node:root_node }
 p=частица:же{}
} : links { a.<POSTFIX_PARTICLE>p }
  : ngrams { 2 }


patterns ГруппаЧисл export { node:root_node ПАДЕЖ ЧИСЛО РОД }

// удаляет в пять раз больше грязи
//         ^^^^^^^^^^^^^^^^^
pattern НаречДляРод
{
 p=предлог:*{}
 c=ГруппаЧисл{ =p:ПАДЕЖ }
 n=существительное:раз{ падеж:род }
 a=НаречДляРод1 : export { node:root_node }
} : links
{
 a.<PREPOS_ADJUNCT>p.<OBJECT>n.<ATTRIBUTE>c
}
: ngrams { 9 }



// --------------------------------------


// Он больше ничего не сказал
//    ~~~~~~~~~~~~~
wordentry_set НеСчитабельныеСущ=
{
 местоим_сущ:ничто{},
 местоим_сущ:никто{},
 местоим_сущ:нечто{}, // мне больше нечего делать
 местоим_сущ:некто{}
}

patterns СчитаемыйНаречиемОбъект export { node:root_node }

// Много их полегло в войну.
// ^^^^^^^^
// Много его утекло.
// ^^^^^^^^^
pattern СчитаемыйНаречиемОбъект
{
 ГруппаМест{ ПАДЕЖ:РОД /*ЧИСЛО:МН*/ } : export { node:root_node }
}




// На улицах было много народу.
//                ^^^^^^^^^^^^
pattern СчитаемыйНаречиемОбъект
{
 ГруппаСущ4{ ПАДЕЖ:ПАРТ ~НеСчитабельныеСущ } : export { node:root_node }
}


// На улицах было много людей.
//                ^^^^^^^^^^^
// А сколько времени и сил было убито!
//   ^^^^^^^^^^^^^^^^^^^^^
pattern СчитаемыйНаречиемОбъект
{
 ГруппаСущ4/*СущСРодДоп*/{ ПАДЕЖ:РОД ~НеСчитабельныеСущ } : export { node:root_node }
}


// Счетный паттерн с прилагательным:
// в карман,  где было много  таких  же.
//                     ^^^^^^^^^^^^^^^^
pattern СчитаемыйНаречиемОбъект
{
 ГруппаПрил2{ ПАДЕЖ:РОД } : export { node:root_node }
} : ngrams { -4 }


// Мы увидели много интересного в последнее время
//            ^^^^^^^^^^^^^^^^^
pattern СчитаемыйНаречиемОбъект
{
 ГруппаПрил2{ ПАДЕЖ:РОД ЧИСЛО:ЕД РОД:СР } : export { node:root_node }
} : ngrams { -3 }



// --------------------------------------


// Я видел много их
//         ^^^^^^^^
// Вижу несколько стаканов чаю на столе
//      ^^^^^^^^^^^^^^^^^^^^^^
// я выпил несколько стаканов чаю
//         ^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 q=НаречДляРод
 n=СчитаемыйНаречиемОбъект : export { ПАДЕЖ:ВИН ЧИСЛО:ЕД ОДУШ:НЕОДУШ РОД:СР node:root_node }
} : links { n.<ATTRIBUTE>q }
  : ngrams { 5 } // чтобы не дать наречию привязаться к сказуемому


// паттерн со счетным словом НЕСКОЛЬКО может выступать в роли множественого числа, хотя
// это звучит несколько неграмматично:
// несколько мужчин бросились за ними туда
// ^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 q=НаречДляРод
 n=СущСРодДоп{ ПАДЕЖ:РОД ~НеСчитабельныеСущ } : export { ПАДЕЖ:ИМ ЧИСЛО:МН ОДУШ РОД node:root_node }
} : links { n.<ATTRIBUTE>q }
  : ngrams { -2 }


// Димка налил себе еще чаю
//                  ^^^^^^^
pattern СущСРодДоп
{
 q=наречие:еще{}
 n=СчитаемыйНаречиемОбъект : export { ПАДЕЖ:РОД ЧИСЛО ОДУШ РОД node:root_node }
} : links { n.<ATTRIBUTE>q }
  : ngrams { -1 }





// чтобы производить как можно меньше шума.
//                   ^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 w1=наречие:как{}
 w2='можно'
 a=НаречДляРод1{ степень:сравн }
 n=СчитаемыйНаречиемОбъект : export { ПАДЕЖ:ВИН ЧИСЛО:ЕД ОДУШ:НЕОДУШ РОД:СР node:root_node }
} : links
{
 n.<ATTRIBUTE>a.<ATTRIBUTE>w2.<ATTRIBUTE>w1
}
: ngrams { 5 }


wordentry_set НаречДляПарт = { наречие:много{}, наречие:немного{}, наречие:чуть-чуть{}  }

/*
// я выпил немного чаю с булочкой
//         ^^^^^^^^^^^
pattern СущСРодДоп export { (РОД) ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
{
 q=НаречДляРод
 n=СчитаемыйНаречиемОбъект : export { ПАДЕЖ:ВИН ЧИСЛО:МН ОДУШ:НЕОДУШ node:root_node }
} : links { n.<ATTRIBUTE>q }
  : ngrams { 3 } // чтобы не дать наречию привязаться к сказуемому
*/


// -----------------------------------------------




// с прилагательным ОБА есть несколько хитростей. В частности, винительный и именительный падеж для неодушевленных предметов может
// образовываться в сочетании с существительным необычно: ВИЖУ ОБА(вин) ДИСКА(род)

// оба мальчика учат уроки
pattern СущСРодДоп
{
 adj=прилагательное:оба{ ПАДЕЖ:ИМ } : export { ПАДЕЖ:ИМ ЧИСЛО:МН }
 n=СущСРодДоп{ =Adj:ОДУШ =Adj:РОД ПАДЕЖ:РОД }:export{ ОДУШ РОД node:root_node }
} : links { n.<ATTRIBUTE>adj }

// вижу обеих девочек
pattern СущСРодДоп
{
 adj=прилагательное:оба{ ПАДЕЖ:ВИН } : export { ПАДЕЖ:ВИН ЧИСЛО:МН }
 n=СущСРодДоп{ =Adj:ОДУШ =Adj:РОД /*ПАДЕЖ:РОД*/ПАДЕЖ:ВИН }:export{ ОДУШ РОД node:root_node }
} : links { n.<ATTRIBUTE>adj }

// Составные существительные с дефисом ЧЕЛОВЕК-ПАУК
// человек-паук
pattern СущСРодДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 tiret='-'{ tokenizer_flag:word_conjunction }
 n2=Сущ0{ =N1:ПАДЕЖ =N1:ЧИСЛО }
} : links { n1.<CONOUN>tiret.<CONOUN>n2 }
  : ngrams { -2 } // чтобы не мешать распознаванию вариантов типа "рок-группа"

// Ракета-носитель «Зенит-3SL» упала в Тихий океан
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 tiret='-'{ tokenizer_flag:word_conjunction }
 n2=Сущ0{ =N1:ПАДЕЖ =N1:ЧИСЛО }
 q1=ОткрывающаяКавычка
 n3=ЦепочкаСловКакНазвание
 q2=ЗакрывающаяКавычка
} : links
{
 n1.{
     <CONOUN>tiret.<CONOUN>n2
     <RIGHT_NAME>n3.{
                     <PREPEND_QUOTE>q1
                     <APPEND_QUOTE>q2
                    }
    }
}
: ngrams { -1 }


// Ракета-носитель «Зенит» упала в Тихий океан
// ^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 tiret='-'{ tokenizer_flag:word_conjunction }
 n2=Сущ0{ =N1:ПАДЕЖ =N1:ЧИСЛО }
 q1=ОткрывающаяКавычка
 n3=ГруппаСущ4{ ПАДЕЖ:ИМ }
 q2=ЗакрывающаяКавычка
} : links
{
 n1.{
     <CONOUN>tiret.<CONOUN>n2
     <RIGHT_NAME>n3.{
                     <PREPEND_QUOTE>q1
                     <APPEND_QUOTE>q2
                    }
    }
}
: ngrams { -1 }



patterns ЛатинНазвание export { node:root_node }

// Латинизированные наименования фирм, товаров и т.д.
collocation_set НазваниеФирмы =
{
 "Hewlett-Packard",
 "Goldman Sachs"
}

pattern ЛатинНазвание
{
 НазваниеФирмы : export { node:root_node }
}

// Microsoft
pattern ЛатинНазвание
{
 @regex_strict("[A-Z][a-z]*") : export { node:root_node }
} 

// Lehman Brothers
pattern ЛатинНазвание
{
 n1=@regex_strict("[A-Z][a-z]*") : export { node:root_node }
 n2=@regex_strict("[A-Z][a-z]*")
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }


// Корпорация Microsoft
pattern СущСРодДоп
{
 n1=существительное:*{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ЛатинНазвание
}
: links { n1.<RIGHT_NAME>n2 }
: ngrams
{
 1
 n_name(n1,n2)
}


// Корпорация Hewlett-Packard закрыла проект WebOS
//                                    ^^^^^^^^^^^^

pattern СущСРодДоп
{
 n1=существительное:проект{ ОДУШ:НЕОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=@regex("[a-z]*[0-9]*")
} : links { n1.<RIGHT_NAME>n2 }




// --------------------------------------------------------------------------------------
// существительные с дополнительным определением справа, присоединенным через предлог
// --------------------------------------------------------------------------------------


// совет специалиста
pattern СущСПредложДоп
{
 СущСРодДоп:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}


// мы применяем такие команды, как сложение и вычитание
pattern СущСПредложДоп
{
 adj=прилагательное:такой{}
 n=СущСРодДоп{ =adj:ПАДЕЖ =adj:ЧИСЛО =adj:РОД =adj:ОДУШ } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 conj=союз:как{}
 n2=ГруппаСущ4{ ПАДЕЖ:ИМ }
}
: links
{
 n.{
    <ATTRIBUTE>adj
    <ATTRIBUTE>comma.
     <NEXT_COLLOCATION_ITEM>conj.
      <NEXT_COLLOCATION_ITEM>n2
   }
}
: ngrams { 10 }



// +++++++++++++++++++++++++++++

patterns ЧислоДробь export { node:root_node }
pattern ЧислоДробь
{
 num_word:*{}:export { node:root_node }
}

// без света остались 5,5 тыс. человек
//                    ^^^
pattern ЧислоДробь
{
 @regex("[0-9]+[,.][0-9]+"):export { node:root_node }
}

wordentry_set НаречиеПримерно=наречие:{ более, менее, свыше, примерно, почти, приблизительно, около, "почти что" }

pattern ЧислоДробь
{
 adv=НаречиеПримерно
 c=ЧислоДробь : export { node:root_node }
}
: links { c.<ATTRIBUTE>adv }
: ngrams { 1 }


// Таким образом через 3 млрд. лет наши галактики столкнутся во второй раз.
word_set NumberSuffix=
{
 'тыс.',
 'тыс',
 'млн',
 'млн.',
 'млрд',
 'млрд.',
 'трлн',
 'трлн.'
}

patterns ЧислоПроцент export { node:root_node }

// 70% из них являются выходцами из Чечни
// ^^^
pattern ЧислоПроцент
{
 @regex("[0-9]+%") : export { node:root_node }
}

// Статистическая погрешность исследования составляет 5 %.
//                                                    ^^^
pattern ЧислоПроцент
{
 n=@regex("[0-9]+") : export { node:root_node }
 p='%'
} : links { n.<NEXT_COLLOCATION_ITEM>p }

// Статистическая погрешность исследования составляет 5.3 %.
//                                                    ^^^^^
pattern ЧислоПроцент
{
 n=@regex("[0-9]+[.,][0-9]+") : export { node:root_node }
 p='%'
} : links { n.<NEXT_COLLOCATION_ITEM>p }


// Потребительские цены во Франции увеличились на 0,3% 
//                                                ^^^^
pattern ЧислоПроцент
{
 @regex("[0-9]+[.,][0-9]+%") : export { node:root_node }
}

// У человека кора занимает более 40% от объема мозга.
//                          ^^^^^^^^^
pattern ЧислоПроцент
{
 q=НаречиеПримерно
 n=ЧислоПроцент : export { node:root_node }
}
: links { n.<ATTRIBUTE>q }
: ngrams { 1 }


pattern ЧислоДробь
{
 adv=НаречиеПримерно
 c=ЧислоДробь : export { node:root_node }
}
: links { c.<ATTRIBUTE>adv }
: ngrams { 1 }





patterns ЧислоСущБезПадежа export { node:root_node }


// 70% из них являются выходцами из Чечни
// ^^^
pattern ЧислоСущБезПадежа
{
 ЧислоПроцент : export { node:root_node }
}



// На президентских выборах в Армении проголосовали 60,5% избирателей
//                                                  ^^^^^^^^^^^^^^^^^
pattern ЧислоСущБезПадежа
{
 n=ЧислоПроцент : export { node:root_node }
 g=ГлДополнение{ ПАДЕЖ:РОД }
}
: links { n.<RIGHT_GENITIVE_OBJECT>g }
: ngrams { 2 }



patterns ОбъектВЭлективном export { node:root_node РОД }


// элективная конструкция:
// 70% из них являются выходцами из Чечни
// ^^^^^^^^^^
pattern ЧислоСущБезПадежа
{
 a=ЧислоПроцент:export{ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{},предлог:от{})
 n=ГлДополнение{ =prepos:ПАДЕЖ }
} : links { a.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 2 }

pattern ЧислоСущБезПадежа
{
 a=ЧислоДробь:export{ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{},предлог:от{})
 n=ГлДополнение{ =prepos:ПАДЕЖ }
} : links { a.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 2 }




// 3 тыс. сирийцев госпитализированы с нейротоксикозом
// ^^^^^^
pattern ЧислоСущБезПадежа
{
 c=ЧислоДробь
 q=NumberSuffix
 obj=ГруппаСущ1{ ЧИСЛО:МН ПАДЕЖ:РОД }:export { node:root_node }
}
: links { obj.<ATTRIBUTE>q.<ATTRIBUTE>c }
: ngrams { 1 }

// Млечный Путь, состоящий из около 200 млрд. звезд
//                            ^^^^^^^^^^^^^^^^^^^^^
// Дефицит врачей составляет примерно 40 тыс. специалистов
//                           ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^

pattern ЧислоСущБезПадежа
{
 adv=НаречиеПримерно
 c=ЧислоДробь
 q=NumberSuffix
 obj=ГруппаСущ1{ ЧИСЛО:МН ПАДЕЖ:РОД }:export { node:root_node }
}
: links { obj.<ATTRIBUTE>q.<ATTRIBUTE>c.<ATTRIBUTE>adv }
: ngrams { 1 }




pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:ИМ ЧИСЛО:МН node:root_node }
}

// Остальные 10% принадлежат правительству Армении.
// ^^^^^^^^^^^^^
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 a=ГруппаПрил2{ ПАДЕЖ:ИМ ЧИСЛО:МН }
 n=ЧислоСущБезПадежа : export { ПАДЕЖ:ИМ ЧИСЛО:МН node:root_node }
} : links { n.<ATTRIBUTE>a }



// На амортизацию инвентаря ежегодно отчисляется 8% его стоимости.
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:ИМ ЧИСЛО:ЕД node:root_node }
}

// от 3 млн. долларов
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:РОД ЧИСЛО:МН node:root_node }
} : ngrams { -2 }

// Дефицит врачей составляет примерно 40 тыс. специалистов
//                                    ^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:ВИН ЧИСЛО:МН node:root_node }
} : ngrams { -1 }

// к 5 млн. долларам
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:ДАТ ЧИСЛО:МН node:root_node }
} : ngrams { -2 }

// под 3 млрд. тонн
pattern СущСПредложДоп export{ (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ЧислоСущБезПадежа : export{ ПАДЕЖ:ТВОР ЧИСЛО:МН node:root_node }
} : ngrams { -2 }


// ++++++++++++++++++++++++++++++++++

// Существительное с присоединенным справа предложным дополнением:
//
// ошейник от блох
pattern СущСПредложДоп
{
 n1=СущСРодДоп:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ПредлогИСущ{ сущ_предл_сущ( N1.root_node, _.prepos, _.n2 ) }
}
: links { n1.<PREPOS_ADJUNCT>n2 }
: ngrams
{
 prepos_score( n1, n2.prepos )
 ngram3( n1, n2.prepos, n2.n2 )
 ВалентностьСущ(n1)
}

// ---------------------------------------------------------
// Некоторые существительные с семантикой размерности могут
// выступать в роли предлогов:
//
// осталась только нижняя часть высотой около дюйма!
//                              ^^^^^^^
// Создавать новые предлоги не всегда разумно, так как внутри
// оборота могут появляться опциональные слова типа "ПОЧТИ" или "ПОЧТИ ЧТО"

wordentry_set СущИзмерение=существительное:{
диаметр, // Ствол дерева диаметром в метр
размер, // они размером почти с человека
высота, // дом высотой в три метра
масса,
длина, // Сети длиной в 2 километра!
величина,
вес, // ящик весом в двадцать килограммов
глубина, // яма глубиной в рост человека
протяженность,
длительность,
водоизмещение, // Он  владел большой черной шхуной водоизмещением двадцать тонн
рост, // Рядом резвились два детеныша ростом с небольших оцелотов.
мощность // мотор мощностью в 150 л.с.
}

patterns ВеличинаИзмерения export { node:root_node }

wordentry_set ПредлогДляВеличиныИзмер=предлог:{ около, до, под, с, в }
pattern ВеличинаИзмерения
{
 prep=ПредлогДляВеличиныИзмер : export { node:root_node }
 obj=ГруппаСущ4{ =prep:ПАДЕЖ }
} : links { prep.<OBJECT>obj }


// ---------------------

word_set ЕдиницаИзмерения0=
{ "л.с.", "кг", "кг.", "м", "м.", "км", "км.", "а.е.", "т.", "гр", "гр." }

patterns ЕдиницаИзмерения export { node:root_node }
pattern ЕдиницаИзмерения
{
 ЕдиницаИзмерения0 : export { node:root_node }
}

wordentry_set СущЕдиницаИзмерения=существительное:{ метр, миллиметр, сантиметр, дециметр,
километр, микрометр, нанометр, ангстрем, верста, миля }

// Аппарат находился на глубине 190 метров.
//                                  ^^^^^^
pattern ЕдиницаИзмерения
{
 СущЕдиницаИзмерения{ ~ПАДЕЖ:ИМ } : export { node:root_node }
}

// ---------------------

// мотор мощностью в 150 л.с.
//                 ^^^^^^^^^^
pattern ВеличинаИзмерения
{
 prep=ПредлогДляВеличиныИзмер : export { node:root_node }
 q=ЧислоДробь
 val=ЕдиницаИзмерения
} : links { prep.<OBJECT>val.<ATTRIBUTE>q }


// Контейнер весом 100 кг
pattern ВеличинаИзмерения
{
 obj=ЕдиницаИзмерения : export { node:root_node }
}

// Ствол дерева диаметром в метр
//                          ^^^^
pattern ВеличинаИзмерения
{
 obj=ГруппаСущ4 : export { node:root_node }
} : ngrams { -2 }


wordentry_set МодифИзмерения={ наречие:почти{}, наречие:почти что{} }
// они размером почти с человека
//     ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ВеличинаИзмерения
{
 mod=МодифИзмерения
 prep=ПредлогДляВеличиныИзмер : export { node:root_node }
 obj=ГруппаСущ4{ =prep:ПАДЕЖ }
} : links { prep.{
                  <ATTRIBUTE>mod
                  <OBJECT>obj
                 } }



pattern СущСПредложДоп
{
 n1=СущСРодДоп:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prep=СущИзмерение{ ПАДЕЖ:ТВОР ЧИСЛО:ЕД }
 n2=ВеличинаИзмерения
}
: links { n1.<PREPOS_ADJUNCT>prep.<OBJECT>n2 }
: ngrams
{
 1
 ВалентностьСущ(n1)
}


// Аппарат находился на глубине 190 метров.
//                      ^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n=СущИзмерение : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 q=ЧислоДробь
 val=ЕдиницаИзмерения
} : links { n.<ATTRIBUTE>val.<ATTRIBUTE>q }


// --------------------------------------------------------------------


// Некоторые существительные допускают присоединение предложного дополнение В+им.падеж:
// кандидат в президенты Армении
pattern СущСПредложДоп
{
 n1=ГруппаСущ1{ ОДУШ:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prep=предлог:в{}
 n2=ГруппаСущ4 { падеж:им число:мн сущ_предл_сущ( N1.root_node, prep, _.n2 ) }
}
: links { n1.<PREPOS_ADJUNCT>prep.<OBJECT>n2 }
: ngrams
{
 ngram3(n1,prep,n2)
 ВалентностьСущ(n1)
}



// получилось нечто вроде звезды.
//            ^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n1=местоим_сущ:*{} : export{ РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ node:root_node }
 n2=ПредлогИСущ { сущ_предл_сущ( n1, _.prepos, _.n2 ) }
} : links
{
 n1.<PREPOS_ADJUNCT>n2
}
: ngrams
{
 -1
 ngram3(n1,n2.prepos,n2.n2)
 ВалентностьСущ(n1)
}




// ни малейшего следа крови
// ^^^^^^^^^^^^
// босс ни словом не обмолвился  об этом.
//      ^^^^^^^^^
pattern СущСПредложДоп
{
 p=частица:ни{}
 n=СущСПредложДоп{ ЧИСЛО:ЕД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links { n.<PREFIX_PARTICLE>p }



// Н.Цискаридзе ответил на обвинения в причастности к нападению на С.Филина
//                                                    ^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n1=СущСРодДоп{ Сущ_НА_Вин }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=предлог:на{}
 n2=@regex_strict("[\\@А\\@Б]\\.[\\@А\\@БЙ][\\@а\\@б\\@й]+")
} : links { n1.<PREPOS_ADJUNCT>prepos.<OBJECT>n2 }
: ngrams
{
 ВалентностьСущ(n1)
}


// Добавим в этот паттерн конструкции, когда справа от существительного стоит слово САМ.
// твой народ сам решит твою участь.
//            ^^^
pattern СущСПредложДоп
{
 n=СущСРодДоп:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=прилагательное:сам{ =n:ПАДЕЖ =n:ЧИСЛО =n:РОД }
} : links { n.<ATTRIBUTE>a }


 // -------------------------------------------------------
 // Причастный оборот для местоимений и местоименных слов:
 // -------------------------------------------------------

 // я вижу его, пьющего из ручья.
 //        ^^^^^^^^^^^^^^^^^^^^^
 pattern СущСПредложДоп
 {
  n=местоимение:я{} : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
  p=ПравПричОборот{ =N:ПАДЕЖ =N:РОД =N:ЧИСЛО }
 } : links { n.<ADJ_PARTICIPLE>p }
 

 // я вижу нечто, пьющее из ручья
 //        ^^^^^^^^^^^^^^^^^^^^^^
 wordentry_set МестоимСрРод={
  местоим_сущ:что-то{}, // Что-то чудесное скоро произойдёт
  местоим_сущ:нечто{}, // Рассказывать кому-либо близкому нечто интересное
  местоим_сущ:всё{}, // делать всё возможное
  местоим_сущ:что-либо{}
 }

 pattern СущСПредложДоп
 {
  n=МестоимСрРод : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ node:root_node }
  p=ПравПричОборот{ =N:ПАДЕЖ РОД:СР ЧИСЛО:ЕД }
 } : links { n.<ADJ_PARTICIPLE>p }

 // ---------------------------------------------------
 // Левое определение для некоторых местоименных слов:
 // ---------------------------------------------------
 // странное нечто
 pattern СущСПредложДоп
 {
  a=ГруппаПрил2{ РОД:СР ЧИСЛО:ЕД }
  n=МестоимСрРод{ =a:ПАДЕЖ } : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ node:root_node }
 } : links { n.<ATTRIBUTE>a }
   : ngrams { -1 }

patterns ПричОборот2 export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

 // пьющее из ручья нечто
 pattern СущСПредложДоп
 {
  a=ПричОборот2{ РОД:СР ЧИСЛО:ЕД }
  n=МестоимСрРод{ =a:ПАДЕЖ} : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ node:root_node }
 } : links { n.<ATTRIBUTE>a }




// Некоторые существительные, имеющие семантику глагола,
// имеют двойную валентность.
// Поищи упоминание у коллеги об этом инциденте
//       ^^^^^^^^^^ ^^^^^^^^^ ^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 pn1=ПредлогИСущ { сущ_предл_сущ( N1.root_node, _.prepos, _.n2 ) }
 pn2=ПредлогИСущ { сущ_предл_сущ( N1.root_node, _.prepos, _.n2 ) }
} : links
{
 n1.{
     <PREPOS_ADJUNCT>pn1
     <PREPOS_ADJUNCT>pn2
    }
}
: ngrams
{
 ngram3( n1, pn1.prepos, pn1.n2 )
 ngram3( n1, pn2.prepos, pn2.n2 )
 ВалентностьСущ(n1)
}


// -------------------------------------------------
// путешествие с планеты Земля на планету Марс
// -------------------------------------------------
/*
wordentry_set Сущ_ПУТЕШЕСТВИЕ=существительное:{
 путешествие
}

pattern СущСРодДоп
{
 n1=ГруппаСущ1{ Сущ_ПУТЕШЕСТВИЕ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p1=предлог:с{}
 pn1=ГруппаСущ1{ ПАДЕЖ:РОД }
 p2=предлог:на{}
 pn2=ГруппаСущ1{ ПАДЕЖ:ВИН }
} : links
{
 n1.{
     <PREPOS_ADJUNCT>p1.<OBJECT>pn1
     <PREPOS_ADJUNCT>p2.<OBJECT>pn2
    }
}
: ngrams { 1 }
*/

/*
// отсрочка приведения в исполнение
pattern СущСРодДоп
{
 // Запрещаем участие имён, то есть паттерны типы "Коля рыл погреб"
 n1=ГруппаСущ1{~CHARCASING:FirstCapitalized}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=СущСПредложДоп{ ПАДЕЖ:РОД ~CannotBeGenAdjunct }
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }
  : ngrams { сущ_сущ( n1.root_node, n2.root_node ) }
*/





// Кто будет главой МИД Литвы.
//           ^^^^^^^^^^
pattern СущСРодДоп
{
 // Запрещаем участие имён собственных, то есть паттерны типы "Коля рыл погреб"
 n1=ГруппаСущ1{ /*~CHARCASING:FirstCapitalized*/ [-10]ПАДЕЖВАЛ:РОД /*~НеПрисоедРод*/ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=РодОбъектСущ
}
: links { n1.<RIGHT_GENITIVE_OBJECT>n2 }
: ngrams { сущ_сущ(n1,n2) }

// Продается обогреватель газовый электрический инфракрасного излучения.
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a1=ГруппаПрил2{ =n1:ЧИСЛО =n1:РОД =n1:ПАДЕЖ [-1]=n1:ОДУШ ~RuAdjCannotModifNoun }
 a2=ГруппаПрил2{ =n1:ЧИСЛО =n1:РОД =n1:ПАДЕЖ [-1]=n1:ОДУШ ~RuAdjCannotModifNoun }
 n2=РодОбъектСущ
}
: links { n1.{ <ATTRIBUTE>a1 <ATTRIBUTE>a2 <RIGHT_GENITIVE_OBJECT>n2 } }
: ngrams
{
 -3
 adj_noun_score(a1,n1)
 adj_noun_score(a2,n1)
 сущ_сущ(n1,n2)
}


// ПРОДАМ СПОРТИВНЫЕ ШТАНЫ ЖЕНСКИЕ МАЛОГО РАЗМЕРА.
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a1=ГруппаПрил2{ =n1:ЧИСЛО =n1:РОД =n1:ПАДЕЖ [-1]=n1:ОДУШ ~RuAdjCannotModifNoun }
 n2=РодОбъектСущ
}
: links { n1.{ <ATTRIBUTE>a1 <RIGHT_GENITIVE_OBJECT>n2 } }
: ngrams
{
 -3
 adj_noun_score(a1,n1)
 сущ_сущ(n1,n2)
}



// Продам новые туфли, темно-синего цвета
//              ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 n2=РодОбъектСущ
}
: links { n1.<RIGHT_GENITIVE_OBJECT>n2.<PUNCTUATION>comma }
: ngrams{ -2 сущ_сущ(n1,n2) }




// "Солдаты удачи" на службе у США
// ^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 q1=ОткрывающаяКавычка
 n=СущСРодДоп:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 q2=ЗакрывающаяКавычка
} : links
{
 n.{
    <PREPEND_QUOTE>q1
    <APPEND_QUOTE>q2
   }
}

// ???
// Прилагательное может выступать в роли генитивного дополнения:
// парень наслаждался вниманием окружающих.
//                              ^^^^^^^^^^
pattern СущСРодДоп
{
 n1=ГруппаСущ1{ ~CHARCASING:FirstCapitalized [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=прилагательное:*{ падеж:род ~краткий ~степень:сравн ~CannotBeGenAdjunct }
}
: links { n1.<RIGHT_GENITIVE_OBJECT>a }
: ngrams
{
 -1
 ВалентностьСущ(n1)
 сущ_сущ( n1, a )
}




// Родительное дополнение слева:
// из воздуха возник скромного вида юноша в зеленой одежде
//                   ^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 g=ГруппаСущ1{ ПАДЕЖ:РОД ~CannotBeGenAdjunct }
 n=ГруппаСущ1{ [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}
: links { n.<RIGHT_GENITIVE_OBJECT>g }
: ngrams
{
 -5
 сущ_сущ( n, g )
}

// Вдвойне редкий случай - родительное дополнение И слева, И справа:
//
// а через минуту налетел чудовищной силы грохот взрыва
//                        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 g=ГруппаСущ1{ ПАДЕЖ:РОД ~CannotBeGenAdjunct }
 n=ГруппаСущ1{ [-10]ПАДЕЖВАЛ:РОД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 g2=ГруппаСущ1{ ПАДЕЖ:РОД ~CannotBeGenAdjunct }
}
: links { n.{ <RIGHT_GENITIVE_OBJECT>g <RIGHT_GENITIVE_OBJECT>g2 } }
: ngrams
{
 -5
 сущ_сущ( n, g )
 сущ_сущ( n, g2 )
}


wordentry_set РусТитул = {
                          существительное:господин{},
                          существительное:товарищ{},
                          существительное:мистер{},
                          существительное:госпожа{},
                          существительное:рядовой{},
                          существительное:старшина{},
                          существительное:майор{},
                          существительное:лейтенант{},
                          существительное:полковник{},
                          существительное:генерал{},
                          существительное:леди{},
                          существительное:сэр{} // Входит сэр Энтони.
                         }

// Аппозитивные конструкции.
patterns СущОбращение { language=Russian } export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

// господин премьер-министр Гондураса
pattern СущОбращение
{
 n=РусТитул:export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=СущСРодДоп{ =N:Падеж =N:ЧИСЛО }
} : links { n.<RIGHT_NAME>n2 }

// стул господина премьер-министра Зимбабве
pattern СущСРодДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=СущОбращение{ ПАДЕЖ:РОД }
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }

// рассказ о господине премьер-министре
pattern СущСПредложДоп
{
 n1=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=предлог:*{}
 n2=СущОбращение{ =Prepos:Падеж }
} : links
{
 n1.<PREPOS_ADJUNCT>prepos.<OBJECT>n2
} : ngrams { ВалентностьСущ(n1) }



// ----------------------------------------------------------------



// ЭЛЕКТИВНЫЙ ОБОРОТ
// паттерны "ОДИН ИЗ"
wordentry_set ПрилагДляПаттернаДОЛЯ =
{
 прилагательное:который{}, // Но  который из них его Стасик?
 прилагательное:некоторый{}, // сквозь некоторые из них были видны кости
 прилагательное:какой{}, // какое из моих имен тебе известно?
 прилагательное:немногий{},
 прилагательное:многий{}, // многим из них это удавалось
 прилагательное:каждый{},
 прилагательное:любой{},
 прилагательное:один{},
 прилагательное:первый{},
 прилагательное:хороший{/*лучший*/}
}

patterns ПрилагДляПаттернаДОЛЯ_2 export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

patterns ГруппаПрил1 { language=Russian } export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ }

// почти любой из них
pattern ПрилагДляПаттернаДОЛЯ_2
{
 ГруппаПрил1{ ПрилагДляПаттернаДОЛЯ /*ЧИСЛО:ЕД*/ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// в зал вошёл второй из нас
//             ^^^^^^
pattern ПрилагДляПаттернаДОЛЯ_2
{
 ПРИЛАГАТЕЛЬНОЕ:*{ ТИП_ПРИЛ:ПОРЯДК СТЕПЕНЬ:АТРИБ /*ЧИСЛО:ЕД*/ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// лучшая из них
// ^^^^^^^^^
pattern ПрилагДляПаттернаДОЛЯ_2
{
 Суперлатив0{ ПрилагДляПаттернаДОЛЯ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// самая лучшая из них
// ^^^^^^^^^^^^
pattern ПрилагДляПаттернаДОЛЯ_2
{
 mod=прилагательное:самый{}
 a=прилагательное:хороший{ степень:превосх =mod:падеж =mod:род =mod:число } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links { a.<ATTRIBUTE>mod }


// двое из четверых рыцарей двинулись навстречу ему
//         ^^^^^^^^^^^^^^^^
pattern ОбъектВЭлективном
{
 ГруппаСущ4{ ПАДЕЖ:РОД число:мн } : export { node:root_node РОД }
}



// многим из них это удавалось
//           ^^^
pattern ОбъектВЭлективном
{
 ГруппаМест{ ПАДЕЖ:РОД число:мн } : export { node:root_node РОД }
}


// пришел одним из последних
//                 ^^^^^^^^^
pattern ОбъектВЭлективном
{
 ГруппаПрил1{ падеж:род число:мн } : export { node:root_node РОД }
} : ngrams { -1 }





// каждый из нас
// каждый из сотрудников фирмы
// лучшая из них всех
pattern СущСПредложДоп
{
 a=ПрилагДляПаттернаДОЛЯ_2:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{})
 n=ОбъектВЭлективном{ =A:РОД }
} : links { a.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 3 }


patterns КтоЭлективн export { node:root_node ПАДЕЖ }

// Дети, кто из вас сделал это?
//       ^^^
pattern КтоЭлективн
{
 n=местоим_сущ:кто{} : export { node:root_node ПАДЕЖ }
}

wordentry_set МногоМалоНаречие = { наречие:мало{}, наречие:много{} }

// мало кто из людей знал об этом
// ^^^^^^^^^^^^^^^^^
pattern КтоЭлективн
{
 q=МногоМалоНаречие
 n=местоим_сущ:кто{}:export{ ПАДЕЖ node:root_node }
}
: links { n.<ATTRIBUTE>q }

// кто же из нас - предатель?
// ^^^^^^
pattern КтоЭлективн
{
 n=местоим_сущ:кто{} : export { node:root_node ПАДЕЖ }
 p=частица:же{}
} : links { n.<POSTFIX_PARTICLE>p }
: ngrams { 1 }




pattern СущСПредложДоп
{
 a=КтоЭлективн:export{ РОД:МУЖ ПАДЕЖ ЧИСЛО:ЕД ОДУШ:ОДУШ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{})
 n=ОбъектВЭлективном{ =A:РОД }
} : links { a.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 2 }




// четверо из них встали без всякой готовности
// ^^^^^^^^^^^^^^
// у пяти из них комнаты во дворце
//   ^^^^^^^^^^^
pattern СущСПредложДоп export{ ПАДЕЖ ЧИСЛО ОДУШ (РОД) node:root_node }
{
 n=ЧИСЛИТЕЛЬНОЕ:*{}:export{ ПАДЕЖ ЧИСЛО:МН ОДУШ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{})
 n2=ОбъектВЭлективном
}
: links { n.<PREPOS_ADJUNCT>prepos.<OBJECT>n2 }
: ngrams { 2 }



// команды, каждая из которых представлена пятнадцатью полевыми игроками.
// ^^^^^^^^^^^^^^^^^^^^.....
pattern СущСПредложДоп export{ ПАДЕЖ ЧИСЛО ОДУШ (РОД) node:root_node }
{
 n=СущСРодДоп:export{ ПАДЕЖ ЧИСЛО:МН ОДУШ node:root_node }
 comma=','
 @noshift(ПрилагДляПаттернаДОЛЯ_2{ =n:РОД ЧИСЛО:ЕД })
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
}
: links { n.<ATTRIBUTE>comma.<SUBORDINATE_CLAUSE>p.~<PUNCTUATION>comma2 }
: ngrams { 2 }


// --------------------------------------------------------

// инверсия прилагательного и атрибутируемого существительного
// руки мои сильно закоченели
wordentry_set ПостфиксПрилагАтрибут =
{
 прилагательное:мой{},
 прилагательное:наш{},
 прилагательное:ваш{},
 прилагательное:твой{}
}

wordentry_set НеПостфиксПрилагАтрибут =
{
 прилагательное:этот{},
 прилагательное:тот{}
}



wordentry_set КТО_ТО_ЛИБО = { местоим_сущ:кто-то{}, местоим_сущ:кто-либо{} }

// у кого-то из учеников есть учебник.
//   ^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 q=КТО_ТО_ЛИБО:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{})
 n=СущСПредложДоп{ падеж:род число:мн }
} : links { q.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 2 }


// у кого-то из вас есть учебник.
//   ^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 q=КТО_ТО_ЛИБО:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=@or(предлог:из{},предлог:среди{})
 n=местоимение:я{ падеж:род число:мн }
} : links { q.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 2 }



patterns ПравоеПрилагательное export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }

// Руки мои онемели
// ^^^^^^^^
pattern ПравоеПрилагательное
{
 ПостфиксПрилагАтрибут : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}


// Машина папина сломалась
// ^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 прилагательное:*{ ~ПостфиксПрилагАтрибут ~НеПостфиксПрилагАтрибут ТИП_ПРИЛ:ПРИТЯЖ } : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -1 }


// но власть эта была недостаточно сильной
//           ^^^
pattern ПравоеПрилагательное
{
 прилагательное:этот{} : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -3 }

// охота та отличалась от других
//       ^^
pattern ПравоеПрилагательное
{
 прилагательное:тот{} : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -3 }



// однако причина такая имелась.
//        ^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 прилагательное:*{ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ~ПостфиксПрилагАтрибут ~НеПостфиксПрилагАтрибут ~ТИП_ПРИЛ:ПРИТЯЖ } : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -4 }


// Кот ученейший живет на дереве
// ^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 прилагательное:*{ СТЕПЕНЬ:ПРЕВОСХ ~ПостфиксПрилагАтрибут ~ТИП_ПРИЛ:ПРИТЯЖ } : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -5 }


tree_scorers ВалентностьПрил


// сперва мы разговаривали о вещах совершенно посторонних.
//                                 ^^^^^^^^^^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 adv=ГруппаНареч1{ ~CannotAdvModifAdj }
 adj=прилагательное:*{ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ~ПостфиксПрилагАтрибут ~ТИП_ПРИЛ:ПРИТЯЖ } : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: links { adj.<ATTRIBUTE>adv }
: ngrams
{
 -5
 adv_adj_score(adv,adj)
 ВалентностьПрил(adj)
}



// Перед нами обнаружатся вещи неслыханные и невиданные
//                             ^^^^^^^^^^^^^^^^^^^^^^^^
// Друг мой любимый стал землею
//      ^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 ГруппаПрил2 : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -5 }


// Глаза ее горели огнем
//       ^^
pattern ПравоеПрилагательное
{
 притяж_частица:*{} : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -3 }


// - Полк свой ищу.
//        ^^^^
pattern ПравоеПрилагательное
{
 прилагательное:свой{} : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -3 }


// Платье все перемазала.
// ^^^^^^^^^^
pattern ПравоеПрилагательное
{
 прилагательное:весь{} : export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
}
: ngrams { -3 }


// Она врач, и очень хороший.
//         ^^^^^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 comma1=','
 a=ГруппаПрил2{ ~ПостфиксПрилагАтрибут ~НеПостфиксПрилагАтрибут } : export { node:root_node РОД ЧИСЛО ПАДЕЖ:ИМ ОДУШ }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 a.{
    <PUNCTUATION>comma1
    ~<PUNCTUATION>comma2
   }
}
: ngrams
{
 -5
}


patterns КраткПрил2 export { node:root_node РОД ЧИСЛО ПРИЧАСТИЕ СТРАД ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ }

// Колеблется воздух, прозрачен и чист, в сияющих звездах колеблется лист
//            ^^^^^^^^^^^^^^^^^^^^^^^^
pattern ПравоеПрилагательное
{
 comma1=','
 a=КраткПрил2 : export { node:root_node РОД ЧИСЛО ПАДЕЖ:ИМ ОДУШ }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 a.{
    <PUNCTUATION>comma1
    ~<PUNCTUATION>comma2
   }
}
: ngrams
{
 -4
}


// товарищ твой оказался предателем
// ^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
}
: links { n.<ATTRIBUTE>a }
: ngrams
{
 adj_noun_score(a,n)
 ВалентностьСущ(n)
}

// Котята шотландские вислоухие ждут своих друзей!!!
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a1=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
 a2=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
}
: links { n.{ <ATTRIBUTE>a1 <ATTRIBUTE>a2 } }
: ngrams
{
 adj_noun_score(a1,n)
 adj_noun_score(a2,n)
 ВалентностьСущ(n)
}

// Продам часы женские позолоченные маленькие
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a1=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
 a2=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
 a3=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
}
: links { n.{ <ATTRIBUTE>a1 <ATTRIBUTE>a2 <ATTRIBUTE>a3 } }
: ngrams
{
 adj_noun_score(a1,n)
 adj_noun_score(a2,n)
 adj_noun_score(a3,n)
 ВалентностьСущ(n)
}



// Продам комбинезон велюровый в отличном состоянии
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
 pn=ПредлогИСущ { сущ_предл_сущ( n.root_node, _.prepos, _.n2 ) }
}
: links { n.{ <ATTRIBUTE>a <PREPOS_ADJUNCT>pn } }
: ngrams
{
 -1
 adj_noun_score(a,n)
 ВалентностьСущ(n)
}



// Существительное в звательном падеже разрешаем соединять с именительным прилагательным:
// Боже мой, как он вырос!
// ^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1{ ПАДЕЖ:ЗВАТ ЧИСЛО:ЕД }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=ПравоеПрилагательное{ =N:РОД ЧИСЛО:ЕД ПАДЕЖ:ИМ }
}
: links { n.<ATTRIBUTE>a }
: ngrams
{
 -1
 adj_noun_score(a,n)
 ВалентностьСущ(n)
}



patterns ГруппаСравнПрил export { node:root_node }

// ПРАВЫЙ АТРИБУТ - КОМПАРАТИВ:
// Вероятно, стражники догадались  принести таран помощнее.
//                                                ^^^^^^^^
pattern СущСПредложДоп
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=ГруппаСравнПрил
}
: links { n.<ATTRIBUTE>a }
: ngrams
{
 -2
 adj_noun_score(a,n)
 ВалентностьСущ(n)
}



// а вот папа ваш бежал сегодня ночью
// ^^^^^^^^^^
pattern СущСПредложДоп
{
 p=частица:вот{}
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
} : links
{
 n.{
    <PREFIX_PARTICLE>p
    <ATTRIBUTE>a
   }
}
: ngrams
{
 -1
 adj_noun_score(a,n)
 ВалентностьСущ(n)
}




// все слова человеческие забыл.
pattern СущСПредложДоп
{
 a1=ГруппаПрил1
 n=существительное:*{ =a1:ЧИСЛО =a1:ПАДЕЖ =a1:РОД =a1:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 a2=ПравоеПрилагательное{ =N:РОД =N:ЧИСЛО =N:ПАДЕЖ }
}
: links
{
 n.{
    <ATTRIBUTE>a1
    <ATTRIBUTE>a2
   }
}
: ngrams
{
 -1
 ВалентностьСущ(n)
}



// *******************************************************
// распознаем в качестве наречий:
// сразу после обеда
// сразу же после восхода солнца
// еще до наступления ночи
// уже до обеда
// *******************************************************

wordentry_set НачДляНаречГр_ДоПосле =
{
 наречие:сразу{},
 наречие:тотчас{},
 наречие:немедленно{},
 наречие:вскоре{}
}

patterns НачНаречнойГруппы_ДоПосле export { node:root_node }

// сразу после восхода солнца
pattern НачНаречнойГруппы_ДоПосле
{ НачДляНаречГр_ДоПосле:export { node:root_node } }

// сразу же после восхода солнца
pattern НачНаречнойГруппы_ДоПосле
{
 a=наречие:сразу{}:export { node:root_node }
 p=ЧастицаЖе
} : links { a.<POSTFIX_PARTICLE>p }
: ngrams { 1 }

// уже вскоре после восхода солнца
pattern НачНаречнойГруппы_ДоПосле
{
 a1=наречие:уже{}
 a2=наречие:вскоре{}:export { node:root_node }
} : links { a2.<PREFIX_PARTICLE>a1 }
  : ngrams { 1 }


pattern ГруппаНареч1
{
 a=НачНаречнойГруппы_ДоПосле:export { node:root_node }
 prepos=@or(предлог:до{},предлог:после{})
 n=СущСРодДоп{ Падеж:РОД }
} : links { a.<PREPOS_ADJUNCT>prepos.<OBJECT>n }
  : ngrams { 1 }


// Обороты с "ТО", выступающие в роли существительного среднего рода
// То, что он согласится на это предложение, было несомненным
// ^^^^^^....
pattern ГруппаСущ4
{
 n=местоим_сущ:то{} : export { node:root_node РОД:СР ЧИСЛО:ЕД ПАДЕЖ ОДУШ:НЕОДУШ }
 comma1=','
 conj=союз:что{}
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
}
: links
{
 n.{
    <PUNCTUATION>comma1
    <SUBORDINATE_CLAUSE>conj.<NEXT_COLLOCATION_ITEM>p
    ~<PUNCTUATION>comma2
   }
}
: ngrams
{
 1
}


// То, куда он уехал, оставалось загадкой
// ^^^^^^^^ ...
pattern ГруппаСущ4
{
 n=местоим_сущ:то{} : export { node:root_node РОД:СР ЧИСЛО:ЕД ПАДЕЖ ОДУШ }
 comma1=','
 @noshift(наречие:*{}) // тут надо бы проверять вопросительность
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
}
: links
{
 n.{
    <PUNCTUATION>comma1
    <SUBORDINATE_CLAUSE>p
    ~<PUNCTUATION>comma2
   }
}
: ngrams
{
 20
}









wordentry_set СоединПарнСоюзЭлем =
{
 союз:то{},
 союз:и{},
 союз:или{},
 союз:либо{},
 союз:ни{},
 наречие:иногда{}
}

patterns СоединПарнСоюз export { node:root_node }
pattern СоединПарнСоюз
{
 СоединПарнСоюзЭлем : export { node:root_node }
}

pattern СоединПарнСоюз
{
 conj=союз:то{} : export { node:root_node }
 p=частица:ли{}
} : links { conj.<POSTFIX_PARTICLE>p }
: ngrams { 1 }


// ------------------------------------------------

// Управляющее существительное с дательной валентностью
patterns СущУправлДат export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ node:root_node }
pattern СущУправлДат
{
 ГруппаСущ1{ ПАДЕЖВАЛ:ДАТ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ node:root_node }
}



patterns ДатОбъектЛевый export { node:root_node }
pattern ДатОбъектЛевый
{
 местоим_сущ:*{ ПАДЕЖ:ДАТ } : export { node:root_node }
}

patterns МестоимСПрефиксом export { ПАДЕЖ ЧИСЛО РОД ЛИЦО node:root_node }

pattern ДатОбъектЛевый
{
 МестоимСПрефиксом{ ПАДЕЖ:ДАТ } : export { node:root_node }
}

pattern ДатОбъектЛевый
{
 Сущ0{ ПАДЕЖ:ДАТ } : export { node:root_node }
}


patterns ДатОбъект export { node:root_node }

// команда собаке
pattern СущСРодДоп
{
 n1=СущУправлДат:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ДатОбъект
} : links { n1.<OBJECT>n2 }
: ngrams
{
 -1
 сущ_сущ(n1,n2)
}


// Россияне поддерживают запрет чиновникам иметь счета и активы за рубежом.
//                       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=СущУправлДат{ МОДАЛЬНЫЙ } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ДатОбъект
 inf=Инф2
} : links { n1.{ <OBJECT>n2 <INFINITIVE>inf } }
: ngrams
{
 -1
 сущ_сущ(n1,n2)
}


// ты - яркое тому подтверждение
//      ^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 a=ГруппаПрил2
 n2=ДатОбъект
 n1=СущУправлДат{ =A:РОД =A:ЧИСЛО =A:ПАДЕЖ =A:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links
{
 n1.{
     <ATTRIBUTE>a
     <OBJECT>n2
    }
}
: ngrams
{
 -2
 adj_noun_score(a,n1)
 сущ_сущ(n1,n2)
}

// Разве я не поклялась, что буду хранить тебе верность?
//                                        ^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n2=ДатОбъектЛевый
 n1=СущУправлДат:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links
{
 n1.<OBJECT>n2
}
: ngrams
{
 -2
 сущ_сущ(n1,n2)
}



// СУЩ + род + дат
// Британский миллионер сознался в поставках оружия Ирану
pattern СущСРодДоп
{
 n1=СущУправлДат:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=РодОбъектСущ
 n3=ДатОбъект
} : links
{
 n1.{
     <RIGHT_GENITIVE_OBJECT>n2
     <OBJECT>n3
    }
}
: ngrams
{
 -1
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}


// СУЩ + дат + род
// Цель такого сопоставления - придание им исключительно оборонительной направленности
pattern СущСРодДоп
{
 n1=СущУправлДат:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ДатОбъект
 n3=РодОбъектСущ
} : links
{
 n1.{
     <OBJECT>n2
     <RIGHT_GENITIVE_OBJECT>n3
    }
}
: ngrams
{
 -1 // так как обычно связываются n2+n3
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
}



// --------------------------------------

patterns СущУправлТворДат export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
pattern СущУправлТворДат
{
 ГруппаСущ1{ ПАДЕЖВАЛ:ТВОР ПАДЕЖВАЛ:ДАТ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// эти обстоятельства могли быть решающими в предоставлении банку государством столь широких возможностей
//                                           ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n1=СущУправлТворДат:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ДатОбъект
 n3=ТворОбъект
 n4=РодОбъектСущ
} : links
{
 n1.{
     <OBJECT>n2
     <OBJECT>n3
     <RIGHT_GENITIVE_OBJECT>n4
    }
}
: ngrams
{
 сущ_сущ(n1,n2)
 сущ_сущ(n1,n3)
 сущ_сущ(n1,n4)
}


// ---------------------------------------------------

// езда верхом
pattern СущСРодДоп
{
 n=существительное:езда{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 adv=наречие:верхом{}
} : links { n.<ATTRIBUTE>adv }
: ngrams { 1 }

// безрассудная и бесшабашная езда верхом
pattern СущСРодДоп
{
 @mark(ГруппаПрил2,A)
 n=существительное:езда{ =A:ЧИСЛО =A:ПАДЕЖ =A:РОД =A:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 adv=наречие:верхом{}
} : links { n.{
               <ATTRIBUTE>a
               <ATTRIBUTE>adv
              } }
: ngrams { 1 }

// +++++++++++++++++++++++++++++++++

patterns ПеречислСущИм export { (РОД) (ПАДЕЖ) (ЧИСЛО) (ОДУШ) node:root_node }

// гвоздики, флоксы, тюльпаны
pattern ПеречислСущИм
{
 n1=СущСПредложДоп{ ПАДЕЖ:ИМ } : export { node:root_node }
 comma1=','
 n2=СущСПредложДоп{ ПАДЕЖ:ИМ }
 comma2=','
 n3=СущСПредложДоп{ ПАДЕЖ:ИМ }
} : links
{
 n1.<RIGHT_LOGIC_ITEM>comma1.
     <NEXT_COLLOCATION_ITEM>n2.
      <RIGHT_LOGIC_ITEM>comma2.
       <NEXT_COLLOCATION_ITEM>n3
}

// гвоздики, флоксы, тюльпаны, розы
pattern ПеречислСущИм
{
 n1=СущСПредложДоп{ ПАДЕЖ:ИМ } : export { node:root_node }
 comma1=','
 n2=СущСПредложДоп{ ПАДЕЖ:ИМ }
 comma2=','
 n3=СущСПредложДоп{ ПАДЕЖ:ИМ }
 comma3=','
 n4=СущСПредложДоп{ ПАДЕЖ:ИМ }
} : links
{
 n1.<RIGHT_LOGIC_ITEM>comma1.
     <NEXT_COLLOCATION_ITEM>n2.
      <RIGHT_LOGIC_ITEM>comma2.
       <NEXT_COLLOCATION_ITEM>n3.
        <RIGHT_LOGIC_ITEM>comma3.
         <NEXT_COLLOCATION_ITEM>n4
}

// и гвоздики, и флоксы
pattern ПеречислСущИм
{
 conj1=союз:и{}
 n1=СущСПредложДоп{ ПАДЕЖ:ИМ } : export { node:root_node }
 comma=','
 conj2=союз:и{}
 n2=СущСПредложДоп{ ПАДЕЖ:ИМ }
} : links
{
 n1.{
     <PREFIX_CONJUNCTION>conj1
     <RIGHT_LOGIC_ITEM>comma.
      <NEXT_COLLOCATION_ITEM>conj2.
       <NEXT_COLLOCATION_ITEM>n2
    }
}

// и гвоздики, и флоксы, и розы
pattern ПеречислСущИм
{
 conj1=союз:и{}
 n1=СущСПредложДоп{ ПАДЕЖ:ИМ } : export { node:root_node }
 comma1=','
 conj2=союз:и{}
 n2=СущСПредложДоп{ ПАДЕЖ:ИМ }
 comma2=','
 conj3=союз:и{}
 n3=СущСПредложДоп{ ПАДЕЖ:ИМ }
} : links
{
 n1.{
     <PREFIX_CONJUNCTION>conj1
     <RIGHT_LOGIC_ITEM>comma1.
      <NEXT_COLLOCATION_ITEM>conj2.
       <NEXT_COLLOCATION_ITEM>n2.
        <RIGHT_LOGIC_ITEM>comma2.
         <NEXT_COLLOCATION_ITEM>conj3.
          <NEXT_COLLOCATION_ITEM>n3
    }
}


// +++++++++++++++++++++++++++++++++++++++++++


// ----------------------------------------
// перечисление элементов с обобщением:
// ----------------------------------------

// принесли  всякой еды: и свежей морковки, и зеленых огурчиков, и гороховых  стручков.
//           ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
// В саду росли цветы: гвоздики, флоксы, тюльпаны
//              ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 n=СущСПредложДоп{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p1=':'
 n2=ГруппаСущ4{ ЧИСЛО:МН =N:ПАДЕЖ }
} : links
{
 n.<DETAILS>p1.
    <NEXT_COLLOCATION_ITEM>n2
}


/*
// В саду росли цветы: гвоздики, флоксы, тюльпаны, розы
pattern ГруппаСущ4
{
 @mark(СущСПредложДоп{},N):export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p1=':'
 n2=СущСПредложДоп{ =N:ПАДЕЖ }
 p2=','
 n3=СущСПредложДоп{ =N:ПАДЕЖ }
 p3=','
 n4=СущСПредложДоп{ =N:ПАДЕЖ }
 p4=','
 n5=СущСПредложДоп{ =N:ПАДЕЖ }
} : links
{
 n.<DETAILS>p1.
    <NEXT_COLLOCATION_ITEM>n2.
     <RIGHT_LOGIC_ITEM>p2.
      <NEXT_COLLOCATION_ITEM>n3.
       <RIGHT_LOGIC_ITEM>p3.
        <NEXT_COLLOCATION_ITEM>n4.
         <RIGHT_LOGIC_ITEM>p4.
          <NEXT_COLLOCATION_ITEM>n5
}

// на завод поступило оборудование: фрезерные и токарные станки, станки с программным управлением и запчасти к ним
pattern ГруппаСущ4
{
 @mark(СущСПредложДоп{},N):export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p1=':'
 n2=СущСПредложДоп{ =N:ПАДЕЖ }
 p2=','
 n3=СущСПредложДоп{ =N:ПАДЕЖ }
 p3=союз:и{}
 n4=СущСПредложДоп{ =N:ПАДЕЖ }
} : links
{
 n.<DETAILS>p1.
    <NEXT_COLLOCATION_ITEM>n2.
     <RIGHT_LOGIC_ITEM>p2.
      <NEXT_COLLOCATION_ITEM>n3.
       <RIGHT_LOGIC_ITEM>p3.
        <NEXT_COLLOCATION_ITEM>n4
}
*/

// Покушение готовят члены так называемой «Боевой организации»
//                                        ^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 adj=ПричОборот2
 q1=ОткрывающаяКавычка
 adj2=ГруппаПрил2{ =Adj:ЧИСЛО =Adj:[ПАДЕЖ] =Adj:РОД =Adj:ОДУШ }
 n=существительное:*{ =Adj:ЧИСЛО =Adj:[ПАДЕЖ] =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
 q2=ЗакрывающаяКавычка
} : links
{
 n.{
    <ADJ_PARTICIPLE>adj
    <ATTRIBUTE>adj2
    <PREPEND_QUOTE>q1
    <APPEND_QUOTE>q2
   }
}
: ngrams
{
 adj_noun_score( adj, n )
 adj_noun_score( adj2, n )
}

/*
// последнее сказанное им слово
pattern ГруппаСущ1
{
 adj=ГруппаПрил2
 adj2=ПричОборот2{ =Adj:ЧИСЛО =Adj:[ПАДЕЖ] =Adj:РОД =Adj:ОДУШ }
 n=существительное:*{ =Adj:ЧИСЛО =Adj:[ПАДЕЖ] =Adj:РОД =Adj:ОДУШ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ МОДАЛЬНЫЙ CharCasing ПАДЕЖВАЛ node:root_node }
} : links
{
 n.{
    <ATTRIBUTE>adj
    <ADJ_PARTICIPLE>adj2
   }
}
: ngrams
{
 adj_noun_score( adj, n )
 adj_noun_score( adj2, n )
}
*/


// дополнительно - причастный оборот может идти после существительного
patterns СущСПричОборотом export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

// Имитация отдельных нейронов, выполняющих эти элементарные функции
pattern ПравПричОборот
{
 comma1=','
 p=ПричОборот2:export { node:root_node ЧИСЛО ПАДЕЖ РОД ОДУШ }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 p.{
    <PUNCTUATION>comma1
    ~<PUNCTUATION>comma2
   }
}



pattern СущСПричОборотом
{
 @mark(СущСПредложДоп,N2):export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p=ПравПричОборот{ =N2:ЧИСЛО =N2:[ПАДЕЖ] =N2:РОД =N2:ОДУШ }
} : links
{
 N2.<ADJ_PARTICIPLE>p
}



// существительные типа "стоимость",
// способные на втором месте подчинять "денежную" распределительную
// группу в именительном падеже:
// яблоки по цене два рубля за килограм
wordentry_set СущЦена=существительное:{ СТОИМОСТЬ, ТАКСА, ТАРИФ, ЦЕНА }
pattern СущСПредложДоп
{
 n=ГруппаСущ1{СущЦена}:export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ4{ ПАДЕЖ:ИМ }
} : links { n.<ATTRIBUTE>n2 }
  : ngrams { -1 }



wordentry_set СвязНаречие=наречие:{ где, куда, откуда }

// правый адъективный оборот СУЩ, ГДЕ ....:
//
// то место, где было животное
//         ^^^^^^^^^^^^^^^^^^^
//
// Созревшие семенные экземпляры раскладывают на солнце или в теплом месте, где они делаются мягкими
//                                                                        ^^^^^ ...
pattern СущСПричОборотом
{
 N2=СущСПредложДоп{ одуш:неодуш }:export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma1=','
 adv=@noshift(СвязНаречие)
 p=ПридаточноеВОбороте
 comma2=@optional(',')
}
: links
{
 N2.<ATTRIBUTE>comma1.<NEXT_COLLOCATION_ITEM>p.~<PUNCTUATION>comma2
}
: ngrams
{
 1
}


// -------------------

patterns ПравГруппаПрил2 export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

wordentry_set ПрилКоторый=прилагательное:{ который }

pattern ПравГруппаПрил2
{
 adj=прилагательное:*{ ~ПрилКоторый ~причастие } : export { node:root_node РОД ПАДЕЖ ЧИСЛО ОДУШ } 
 pn=ПредлогИСущ
 @noshift(ПравыйОграничительОборота)
 comma=@optional(',')
} : links
{
 adj.{
      <PREPOS_ADJUNCT>pn
      ~<PUNCTUATION>comma
     }
}
: ngrams
{
 ВалентностьПрил(adj)
}

// Группа прилагательного может находится справа от существительного через ','
// промышленная база, необходимая для полного перевооружения
//                  ^^^^^^^^^^^^^ ...
pattern СущСПричОборотом
{
 N2=СущСПредложДоп:export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma1=','
 adj=ПравГруппаПрил2{ =N2:ЧИСЛО =N2:[ПАДЕЖ] =N2:РОД =N2:ОДУШ }
} : links
{
 N2.{
     <ATTRIBUTE>adj
     <PUNCTUATION>comma1
    }
}
: ngrams { -1 }




// +++++++++++++++++++++++++++++++++++

// Необычный паттерн - союзное соединение местоимения и наречия:
// Никто и никогда не делал так
// Кто и когда сказал это?
// Что и где вы видели?

wordentry_set НаречСочаетсяМест=наречие:{ никогда, когда, где, нигде, куда, никуда, зачем, почему }
wordentry_set МестоимСочетаетсяНареч_ОДУШ=местоим_сущ:{ кто, никто }
wordentry_set МестоимСочетаетсяНареч_НЕОДУШ=местоим_сущ:{ что, ничто }

patterns МестоимСочетаетсяНареч export { node:root_node ПАДЕЖ ОДУШ РОД ЧИСЛО }

pattern МестоимСочетаетсяНареч
{
 МестоимСочетаетсяНареч_ОДУШ : export { node:root_node ПАДЕЖ ОДУШ:ОДУШ РОД:МУЖ ЧИСЛО:ЕД }
}

pattern МестоимСочетаетсяНареч
{
 МестоимСочетаетсяНареч_НЕОДУШ : export { node:root_node ПАДЕЖ ОДУШ:НЕОДУШ РОД:СР ЧИСЛО:ЕД }
}





pattern СущСРодДоп
{
 n=МестоимСочетаетсяНареч : export{ ПАДЕЖ РОД ЧИСЛО ОДУШ node:root_node }
 conj=союз:и{}
 a=НаречСочаетсяМест
} : links { n.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a }


pattern СущСРодДоп
{
 a=НаречСочаетсяМест
 conj=союз:и{}
 n=МестоимСочетаетсяНареч : export{ ПАДЕЖ РОД ЧИСЛО ОДУШ node:root_node }
} : links { n.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a }


// Кто, когда и почему сделал это?
pattern СущСРодДоп
{
 n=МестоимСочетаетсяНареч : export{ ПАДЕЖ РОД ЧИСЛО ОДУШ node:root_node }
 comma=','
 a1=НаречСочаетсяМест
 conj=союз:и{}
 a2=НаречСочаетсяМест
} : links
{
 n.<RIGHT_LOGIC_ITEM>comma.
    <NEXT_COLLOCATION_ITEM>a1.
     <RIGHT_LOGIC_ITEM>conj.
      <NEXT_COLLOCATION_ITEM>a2
}

// Когда, кто и где сделал это?
pattern СущСРодДоп
{
 a1=НаречСочаетсяМест
 comma=','
 n=МестоимСочетаетсяНареч : export{ ПАДЕЖ РОД ЧИСЛО ОДУШ node:root_node }
 conj=союз:и{}
 a2=НаречСочаетсяМест
} : links
{
 n.<RIGHT_LOGIC_ITEM>comma.
    <NEXT_COLLOCATION_ITEM>a1.
     <RIGHT_LOGIC_ITEM>conj.
      <NEXT_COLLOCATION_ITEM>a2
}


// Я ничего и никому не был должен.
//   ^^^^^^^^^^^^^^^
pattern СущСРодДоп
{
 n=местоим_сущ:ничто{} : export{ ПАДЕЖ РОД:СР ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 conj=союз:и{}
 a=местоим_сущ:никто{}
} : links { n.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a }






// модальные существительные
// твердое намерение начать работу
// умение понимать речь по движениям губ
pattern СущСПредложДоп
{
 n=ГруппаСущ1{ МОДАЛЬНЫЙ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 inf=Инф2
} : links { n.<INFINITIVE>inf }

// Одновременно он упомянул желание Москвы взять под свой контроль вопрос размещения миротворческого контингента
//                          ^^^^^^^^^^^^^^^^^^^^...
pattern СущСПредложДоп
{
 n=ГруппаСущ1{ МОДАЛЬНЫЙ }:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ГруппаСущ1{ ПАДЕЖ:РОД }
 inf=Инф2
} : links
{
 n.{
    <RIGHT_GENITIVE_OBJECT>n2
    <INFINITIVE>inf
   }
}
: ngrams { -1 }



// Оборот xxx, КРОМЕ ....
// Конский волос покрывал все, кроме лица.
//                             ^^^^^^^^^^


pattern ПредлогИСущ
{
 comma1=','
 p=ПредлогДляВсе : export { node:root_node node:root_node->prepos }
 obj=ГлДополнение{ =p:ПАДЕЖ }:export { node:root_node->n2 }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 p.{
    <PUNCTUATION>comma1
    <OBJECT>obj.~<PUNCTUATION>comma2
   }
}
: ngrams { 2 }

// +++++++++++++++++++++++++++++++++++++++++++++


// -------------------------------------------------------
// Существительное (обычно соответствующее субъективному
// ментальному процессу) с правым придаточным оборотом.

wordentry_set СущСПридаточнЧто=существительное:{
сомнение, идея, подозрение, предположение, гипотеза, идейка,
мысль
}

// -----------------------------------------------------------

wordentry_set СоединДляПридаточного0_0={
 союз:что{}, союз:чтобы{}, союз:"с тем чтобы"{}
}

wordentry_set СоединДляПридаточного0_1={
 союз:как{}
}

patterns СоединДляПридаточного export { node:root_node }

pattern СоединДляПридаточного
{
 СоединДляПридаточного0_0 : export { node:root_node }
}

pattern СоединДляПридаточного
{
 СоединДляПридаточного0_1 : export { node:root_node }
} : ngrams { -1 }

// -----------------------------------------------------------



// у меня нет сомнений, что ты прав
//            ^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 n=ГруппаСущ1{ СущСПридаточнЧто } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 conj=СоединДляПридаточного
 p=ПридаточноеВОбороте
} : links
{
 n.<SUBORDINATE_CLAUSE>comma.
    <NEXT_COLLOCATION_ITEM>conj.
     <NEXT_COLLOCATION_ITEM>p
}
: ngrams { 1 }





// ***************************************************
// АНАЛИТИЧЕСКАЯ КОНСТРУКЦИЯ ПРИЧАСТНОГО ОБОРОТА
// ***************************************************


patterns КоторыйПрепаттерн export { РОД ЧИСЛО }

wordentry_set КакойКоторый=прилагательное:{ который, какой, чей }

// Он уважал людей, которые не боятся спрашивать.
//                  ^^^^^^^
pattern КоторыйПрепаттерн
{
 КакойКоторый : export { РОД ЧИСЛО }
}

pattern КоторыйПрепаттерн
{
 p=предлог:*{}
 КакойКоторый{ =p:ПАДЕЖ } : export { РОД ЧИСЛО }
}

// Автобус подрулил к углу стоянки, в центре которой находилась большая закусочная.
//                                  ^^^^^^^^^^^^^^^^
pattern КоторыйПрепаттерн
{
 p=предлог:*{}
 n=ГруппаСущ4{ =p:падеж }
 КакойКоторый{ ПАДЕЖ:РОД } : export { РОД ЧИСЛО }
}


// Он предвидел множество перемен, часть которых не будет поддаваться объяснению.
//                                 ^^^^^^^^^^^^^
pattern КоторыйПрепаттерн
{
 n1=ГруппаСущ1
 n2=КакойКоторый{ ПАДЕЖ:РОД } : export { РОД ЧИСЛО }
}

// Транспорт, управление которым требует наличия прав
//            ^^^^^^^^^^^^^^^^^^
pattern КоторыйПрепаттерн
{
 n1=СущУправлТвор
 n2=КакойКоторый{ ПАДЕЖ:ТВОР } : export { РОД ЧИСЛО }
}

// сарай, дверь  в  который  никогда не запиралась.
//        ^^^^^^^^^^^^^^^^^
pattern КоторыйПрепаттерн
{
 СущСРодДоп
 p=предлог:*{}
 КакойКоторый{ =p:ПАДЕЖ } : export { РОД ЧИСЛО }
}

// +++++++++++++++++

patterns ЧейПрепаттерн

pattern ЧейПрепаттерн
{
 прилагательное:чей{}
}

pattern ЧейПрепаттерн
{
 p=предлог:*{}
 прилагательное:чей{ =p:ПАДЕЖ }
}

// +++++++++++++++++++

// Первым, кого он встретил, был Кролик.
//         ^^^^
// Первое, что он увидел, было яркое свечение
//         ^^^
patterns КтоЧтоПрепаттерн

pattern КтоЧтоПрепаттерн
{
 местоим_сущ:кто{}
}

pattern КтоЧтоПрепаттерн
{
 местоим_сущ:что{}
}

// Первым, на кого они натолкнулись, был Тигрёнок.
pattern КтоЧтоПрепаттерн
{
 p=предлог:*{}
 местоим_сущ:кто{ =p:ПАДЕЖ }
}

pattern КтоЧтоПрепаттерн
{
 p=предлог:*{}
 местоим_сущ:что{ =p:ПАДЕЖ }
}

pattern ГруппаПрил1
{
 a=прилагательное:*{ТИП_ПРИЛ:ПОРЯДК}:export { node:root_node РОД ЧИСЛО ПАДЕЖ ОДУШ }
 comma1=','
 @noshift(КтоЧтоПрепаттерн)
 p=ПридаточноеВОбороте
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 a.{
    <PUNCTUATION>comma1
    <SUBORDINATE_CLAUSE>p.~<PUNCTUATION>comma2
   }
}
: ngrams { 1 }




// Оборот с вложенным предикатом:
//
// Те, кто замечает.
// ^^^^^^^^^^^^^^^^
pattern ГруппаПрил1
{
 a=прилагательное:тот{}:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma1=','
 @noshift(КтоЧтоПрепаттерн)
 p=ПридаточноеВОбороте
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 a.{
    <PUNCTUATION>comma1
    <SUBORDINATE_CLAUSE>p.~<PUNCTUATION>comma2
   }
}
: ngrams { 10 }










// +++++++++++++++++++

patterns ОборотКоторый export { node:root_node }

// Я думал о людях, чья судьба меня волновала
//                ^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОборотКоторый
{
 p=ПридаточноеВОбороте:export { node:root_node }
 @noshift(ПравыйОграничительОборота)
 comma=@coalesce(',')
}
: links { p.~<PUNCTUATION>comma }



// +++++++++++++++++++++++++++++++++++++++

patterns ОсноваДляПрисоедКоторый export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
patterns ОсноваДляПрисоедКоторый0 export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

// Дайте мне книгу, которую я просил
//           ^^^^^^^^^^^^^^^^^^...
pattern ОсноваДляПрисоедКоторый0
{
 СущСРодДоп : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// Дайте мне ту книгу, которую я просил
//              ^^^^^^^^^^^^^^^^^^...
pattern ОсноваДляПрисоедКоторый0
{
 a=ГруппаПрил2
 n=Сущ0{ =a:ПАДЕЖ =a:РОД =a:ЧИСЛО =a:ОДУШ } : export { РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links { n.<ATTRIBUTE>a }

pattern ОсноваДляПрисоедКоторый0
{
 n=СущСРодДоп : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 p=предлог:*{}
 n2=СущСРодДоп{ =p:ПАДЕЖ }
} : links
{
 n.<PREPOS_ADJUNCT>p.<OBJECT>n2
}



pattern ОсноваДляПрисоедКоторый
{
 ОсноваДляПрисоедКоторый0 : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// Горожан и полицию, которые пытались организовать сопротивление
pattern ОсноваДляПрисоедКоторый
{
 n1=ОсноваДляПрисоедКоторый0 : export{ РОД ПАДЕЖ ЧИСЛО:МН ОДУШ node:root_node }
 conj=ЛогичСоюз
 n2=ОсноваДляПрисоедКоторый0{ =n1:ПАДЕЖ }
} : links { n1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2 }


// Он часто просыпался от рева, лая и рычания, которые доносились со стороны канала.
pattern ОсноваДляПрисоедКоторый
{
 n1=ОсноваДляПрисоедКоторый0 : export{ РОД ПАДЕЖ ЧИСЛО:МН ОДУШ node:root_node }
 comma=','
 n2=ОсноваДляПрисоедКоторый{ =n1:ПАДЕЖ }
} : links { n1.<RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>n2 }


// часть яруса  загораживалась  тем,  который   был непосредственно над ним.
//                              ^^^
pattern ОсноваДляПрисоедКоторый
{
 прилагательное:тот{} : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
}

// Дайте мне ту книгу, которую я просил
//              ^^^^^^^^^^^^^^^^^^...
pattern СущСПричОборотом
{
 n=ОсноваДляПрисоедКоторый :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 @noshift(КоторыйПрепаттерн{ =n:РОД =n:ЧИСЛО })
 a=ОборотКоторый
} : links
{
 n.<ATTRIBUTE>comma.<SUBORDINATE_CLAUSE>a
}
: ngrams
{
 10
 ВалентностьСущ(n)
}

// Я думал о людях, чья судьба меня волновала
pattern СущСПричОборотом
{
 n=ОсноваДляПрисоедКоторый :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 @noshift(ЧейПрепаттерн)
 a=ОборотКоторый
} : links
{
 n.<ATTRIBUTE>comma.<SUBORDINATE_CLAUSE>a
}
: ngrams
{
 10
 ВалентностьСущ(n)
}


// ---------------------------------------------------------------
// ПРАВОАТРИБУТИВНЫЕ ОБОРОТЫ С ПРИДАТОЧНЫМ "ТОТ, КТО" и подобные
// ---------------------------------------------------------------

patterns ТотКтоПрепаттерн

pattern ТотКтоПрепаттерн
{
 местоим_сущ:кто{}
}

pattern ТотКтоПрепаттерн
{
 p=предлог:*{}
 местоим_сущ:кто{ =p:ПАДЕЖ }
}

// Мы работаем для тех, кто ценит удобство
//                 ^^^^^^^^^^^^^^^^^^^^^^^
//
// Он рассказывает о тех, кого знал
//                   ^^^^^^^^^^^^^^

wordentry_set ПрилДляТотКто=
{
 прилагательное:тот{},
 прилагательное:каждый{},
 прилагательное:любой{},
 прилагательное:единственный{},
 прилагательное:один{}
}

patterns ТотКто_1 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }

// Любой, кто прочтет эту книгу, останется в недоумении
pattern ТотКто_1
{
 ПрилДляТотКто{ РОД:МУЖ } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ:ОДУШ node:root_node }
}


// Все, кто читал эту книгу, остались в недоумении
pattern ТотКто_1
{
 прилагательное:весь{ число:мн } : export{ РОД ПАДЕЖ ЧИСЛО:МН ОДУШ:ОДУШ node:root_node }
}


// Кто-то, кто отключил подачу свежего воздуха
pattern ТотКто_1
{
 местоим_сущ:кто-то{} : export{ РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ:ОДУШ node:root_node }
}


// Смерть тому, кто убежал, кто сдался!
//        ^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПричОборотом
{
 n=ТотКто_1 :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 @noshift(ТотКтоПрепаттерн)
 a=ОборотКоторый
} : links
{
 n.<ATTRIBUTE>comma.<SUBORDINATE_CLAUSE>a
}
: ngrams
{
 10
 ВалентностьСущ(n)
}


// ----------------------------------------------------------

patterns ВсеЧтоПрепаттерн

pattern ВсеЧтоПрепаттерн
{
 местоим_сущ:что{}
}

pattern ВсеЧтоПрепаттерн
{
 p=предлог:*{}
 местоим_сущ:что{ =p:ПАДЕЖ }
}

// Оборот "ВСЁ, ЧТО ...." выступает в роли именной группы
// Всё, что он говорит, правильно.
// ^^^^^^^^^...
pattern СущСПричОборотом
{
 n=прилагательное:весь{} :export{ РОД:СР ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 comma=','
 @noshift(ВсеЧтоПрепаттерн)
 a=ОборотКоторый
} : links
{
 n.<ATTRIBUTE>comma.<SUBORDINATE_CLAUSE>a
}
: ngrams
{
 10
 ВалентностьПрил(n)
}


// все вместе они поползли назад.
// ^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 a=прилагательное:весь{ ЧИСЛО:МН }
 mod=наречие:вместе{}
 n=местоимение:я{ число:мн } :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links
{
 n.<ATTRIBUTE>a.<ATTRIBUTE>mod
}
: ngrams { 2 }


// почти все они были опасны.
// ^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 mod=наречие:почти{}
 a=прилагательное:весь{ ЧИСЛО:МН }
 n=местоимение:я{ число:мн } :export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
} : links
{
 n.<ATTRIBUTE>a.<ATTRIBUTE>mod
}
: ngrams { 2 }



// к нему присоединились еще двое.
//                       ^^^^^^^^
pattern СущСРодДоп
{
 mod=наречие:еще{}
 n=числительное:*{ КАТЕГОРИЯ_ЧИСЛ:СОБИР } :export{ РОД:МУЖ ПАДЕЖ ЧИСЛО:МН ОДУШ:ОДУШ node:root_node }
} : links
{
 n.<ATTRIBUTE>mod
}
: ngrams { 2 }


// тем временем те трое зашевелились.
//              ^^^^^^^
pattern СущСРодДоп
{
 a=прилагательное:тот{ число:мн }
 n=числительное:*{ =a:ПАДЕЖ КАТЕГОРИЯ_ЧИСЛ:СОБИР } :export{ РОД:МУЖ ПАДЕЖ ЧИСЛО:МН ОДУШ:ОДУШ node:root_node }
} : links
{
 n.<ATTRIBUTE>a
}


// --------------------------------------------------------



// *******************************************
// Конструкция с дополнением через предлог
// *******************************************

// то, с чем нам придется работать
// мы увидели то, о чем на лекции говорил преподаватель
pattern СущСПредложДоп
{
 n=местоим_сущ:то{} : export { РОД ПАДЕЖ ЧИСЛО ОДУШ:НЕОДУШ node:root_node }
 comma=','
 @mark(предлог:*{},Prep)
 n2=местоим_сущ:что{ =Prep:ПАДЕЖ }
 v=Предикат
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 n.{
    <PUNCTUATION>comma
    <PREPOS_ADJUNCT>prep.<OBJECT>n2.<SUBORDINATE_CLAUSE>v
    ~<PUNCTUATION>comma2
   }
}
: ngrams { 2 }


patterns ТО_Сложн export { ПАДЕЖ node:root_node }

// Он вспомнил то, что было накануне.
//             ^^
pattern ТО_Сложн
{
 местоим_сущ:то{} : export { ПАДЕЖ node:root_node }
}

// Он вспомнил всё то, что было накануне.
//             ^^^^^^
pattern ТО_Сложн
{
 a=прилагательное:весь{ род:ср число:ед }
 x=местоим_сущ:то{ =a:ПАДЕЖ } : export { ПАДЕЖ node:root_node }
} : links
{
 x.<ATTRIBUTE>a
}
: ngrams { 1 }



// Человек делает то, что ему надо было сделать вчера
//                ^^^^^^^...
pattern СущСПредложДоп
{
 x=ТО_Сложн : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 comma=','
 w=ПридаточноеВОбороте
 comma2=@coalesce(',')
} : links
{
 x.{
    <PUNCTUATION>comma
    <SUBORDINATE_CLAUSE>w
    ~<PUNCTUATION>comma2
   }
}
: ngrams { 3 } // чтобы подавить вариант то=союз

wordentry_set СоюзДляТо=союз:{ что, чтобы, как }

// Я удивился тому, что он рано вернулся
//            ^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 x=ТО_Сложн : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 comma=','
 conj=СоюзДляТо
 w=ПридаточноеВОбороте
 comma2=@coalesce(',')
} : links
{
 x.{
    <PUNCTUATION>comma
    <SUBORDINATE_CLAUSE>conj.<NEXT_COLLOCATION_ITEM>w
    ~<PUNCTUATION>comma2
   }
}
: ngrams { 2 } // чтобы подавить вариант то=союз


// Я пришёл специально для того, чтобы с вами поговорить.
//                         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 x=ТО_Сложн : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 comma=','
 conj=союз:чтобы{}
 w=Инф2
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 x.{
    <PUNCTUATION>comma
    <SUBORDINATE_CLAUSE>conj.<NEXT_COLLOCATION_ITEM>w
    ~<PUNCTUATION>comma2
   }
}
: ngrams { 7 }


// Он пишет о том, что видел
//            ^^^^^^^^^^^^^^
pattern СущСПредложДоп
{
 x=ТО_Сложн : export { РОД:СР ПАДЕЖ ЧИСЛО:ЕД ОДУШ:НЕОДУШ node:root_node }
 comma=','
 w=Сказуемое
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 x.{
    <PUNCTUATION>comma
    <SUBORDINATE_CLAUSE>w
    ~<PUNCTUATION>comma2
   }
}
: ngrams { 7 }


// **************************************************************
// АНАЛИТИЧЕСКАЯ КОНСТРУКЦИЯ ДЛЯ АТРИБУТА СУЩЕСТВИТЕЛЬНОГО
// **************************************************************

wordentry_set СоюзДляАналитАтриб2 =
{ союз:как{}, наречие:где{}, союз:что{}, наречие:зачем{}, наречие:почему{} }

patterns СоюзДляАналитАтриб export { node:root_node }
pattern СоюзДляАналитАтриб { СоюзДляАналитАтриб2:export { node:root_node } }
pattern СоюзДляАналитАтриб
{
 prep=предлог:для{}:export { node:root_node }
 n='чего'
} : links { prep.<PREPOS_ADJUNCT>n }

patterns ГруппаМестоимДляДоп export { node:root_node ПАДЕЖ }

// рассказ о том, как пираты искали клад
pattern ГруппаМестоимДляДоп
{
 n=местоим_сущ:то{}:export { ПАДЕЖ node:root_node }
 comma=','
 conj=СоюзДляАналитАтриб
 v=Предикат
} : links
{
 n.<ATTRIBUTE>comma.
    <NEXT_COLLOCATION_ITEM>conj.
     <SUBORDINATE_CLAUSE>v
}
: ngrams { 5 }


// рассказ о том, что ищут пираты на острове
pattern ГруппаМестоимДляДоп
{
 n=местоим_сущ:то{}:export { ПАДЕЖ node:root_node }
 comma=','
 v=ПредикатКтоИлиЧто
} : links
{
 n.<ATTRIBUTE>comma.
     <SUBORDINATE_CLAUSE>v
}
: ngrams { 3 }


pattern ГруппаСущ4
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 prepos=предлог:*{}
 n2=ГруппаМестоимДляДоп{ =Prepos:Падеж }
} : links { n.<PREPOS_ADJUNCT>prepos.<OBJECT>n2 }


// Нашел сварщика - курносого русского мужика.
//       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 conj='-'{ tokenizer_flag:word_separation }
 n2=ГруппаСущ4{ =n:Падеж }
}
: links { n.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2 }
: ngrams { -1 }


// Центр имеет определенную структуру - кафедральную.
//                          ^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4
{
 n=ГруппаСущ1:export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 conj='-'{ tokenizer_flag:word_separation }
 a=ГруппаПрил2{ =n:Падеж =n:РОД =n:ЧИСЛО }
}
: links { n.{ <ATTRIBUTE>a <PUNCTUATION>conj } }
: ngrams { -1 }



// ----------------------------------------------






