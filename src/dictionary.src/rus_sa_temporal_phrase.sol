
// Конструкции с днями, часами, минутами, секундами...

// сражение продолжалось две минуты восемнадцать секунд.
//                       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
// полет занял шесть часов двадцать минут.
//             ^^^^^^^^^^^^^^^^^^^^^^^^^^


wordentry_set ТермВремен0 = существительное:{
день,
минута,
секунда,
час,
месяц,
год,
лето
}

patterns ТермГруппыВремени export { node:root_node ПАДЕЖ }

pattern ТермГруппыВремени
{
 c = Числ1 : export { ПАДЕЖ }
 n = ТермВремен0 : export { node:root_node }
} : links { n.<ATTRIBUTE>c }

pattern ТермГруппыВремени
{
 ТермВремен0 : export { node:root_node ПАДЕЖ }
} : ngrams { -3 }

// ----------------------------------------------------------------

patterns ГруппаВремени { bottomup } export { node:root_node ПАДЕЖ }

pattern ГруппаВремени
{
 ТермГруппыВремени : export { node:root_node ПАДЕЖ }
}

pattern ГруппаВремени
{
 head=ГруппаВремени : export { node:root_node ПАДЕЖ }
 tail=ТермГруппыВремени{ =head:ПАДЕЖ }
}
: links { head.<NEXT_COLLOCATION_ITEM>tail }
: ngrams { 3 }


// --------------------------------------------------------
// сражение продолжалось две минуты восемнадцать секунд.
//                       ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
// полет занял шесть часов двадцать минут.
//             ^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern Обст
{
 ГруппаВремени : export { node:root_node }
}
