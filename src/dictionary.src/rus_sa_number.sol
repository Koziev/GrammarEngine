wordentry_set МодификаторЧисла={ наречие:даже{}, наречие:лишь{},
наречие:только{}, наречие:всего{},
наречие:эдак{}, наречие:почти{}, наречие:сразу{}, наречие:примерно{},
наречие:приблизительно{}, наречие:почти что{}
}


// ------------------------------------------------
//     ЧИСЛИТЕЛЬНЫЕ В ВИДЕ ЦИФРОВОЙ ЗАПИСИ
// ------------------------------------------------

patterns ЧислоЦифрами0 export { ЧИСЛО node:root_node }

// 36 спелых яблок лежат на столе
// ^^
pattern ЧислоЦифрами0
{
 @regex("[[:digit:]]*[023456789]"):export{ ЧИСЛО:МН node:root_node }
}

// 21 сочное яблоко лежит в миске
// ^^
pattern ЧислоЦифрами0
{
 @regex("[[:digit:]]*1"):export{ ЧИСЛО:ЕД node:root_node }
}



pattern ЧислоЦифрами0
{
 @regex("[[:digit:]]+,[[:digit:]]+"):export{ ЧИСЛО:МН node:root_node }
}

// 2. миллиметра
pattern ЧислоЦифрами0
{
 @regex("[[:digit:]]+\\.[[:digit:]]*"):export{ ЧИСЛО:МН node:root_node }
} : ngrams { -1 }

// .36 дюймов
pattern ЧислоЦифрами0
{
 @regex("[[:digit:]]*\\.[[:digit:]]+"):export{ ЧИСЛО:МН node:root_node }
}

// «Зенит» проиграл «Атлетико» со счетом 1:3 в матче Лиги чемпионов
//                                       ^^^
pattern ЧислоЦифрами0
{
 n1=num_word:*{}:export{ ЧИСЛО:МН node:root_node }
 d=':'
 n2=num_word:*{}
} : links { n1.<NEXT_COLLOCATION_ITEM>d.<NEXT_COLLOCATION_ITEM>n2 }


// Материальный ущерб оценивается в 20 000 рублей.
//                                  ^^^^^^
pattern ЧислоЦифрами0
{
 n1=@regex("[1-9][0-9]{1,2}") : export { ЧИСЛО:МН node:root_node }
 n2=@regex("[0-9][0-9][0-9]")
}
: links { n1.<NEXT_COLLOCATION_ITEM>n2 }
: ngrams { 1 }


// Внешнеторговый оборот Кузбасса за год составил более $7 млрд.
//                                                      ^^
pattern ЧислоЦифрами0
{
 n1=@regex("[$₤€￥¥][0-9]+") : export { ЧИСЛО:МН node:root_node }
}


// Долг составил только $25 млрд
//               ^^^^^^^^^^
pattern ЧислоЦифрами0
{
 mod=МодификаторЧисла
 n=ЧислоЦифрами0 : export { ЧИСЛО:МН node:root_node }
}
: links { n.<ATTRIBUTE>mod }
: ngrams { 1 }

// Долги составили более $25 млрд
//                 ^^^^^^^^^
pattern ЧислоЦифрами0
{
 mod=ГруппаСрНареч1{ БолееМенее }
 n=ЧислоЦифрами0 : export { ЧИСЛО:МН node:root_node }
}
: links { n.<ATTRIBUTE>mod }
: ngrams { 1 }


// ----------------------------

patterns ЧислоЦифрами export { ЧИСЛО node:root_node }

// 13 воробьев клюют просо
// ^^
pattern ЧислоЦифрами
{
 n1=ЧислоЦифрами0 : export { ЧИСЛО node:root_node }
}

// 13 или 14 ворон сидят на крыше
// ^^^^^^^^^
pattern ЧислоЦифрами
{
 n1=ЧислоЦифрами0 : export { node:root_node }
 conj=ЛогичСоюз
 n2=ЧислоЦифрами0 : export { ЧИСЛО }
} : links { n1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2 }


// 26-27 воробьев клевали крошки
// ^^^^^
pattern ЧислоЦифрами
{
 n1=ЧислоЦифрами0 : export { node:root_node }
 t='-'
 n2=ЧислоЦифрами0 : export { ЧИСЛО }
} : links { n1.<RIGHT_LOGIC_ITEM>t.<NEXT_COLLOCATION_ITEM>n2 }

// ------------------------------

// числительное после существительного
// "калашников" советской или российской сборки калибра 7,62
//                                              ^^^^^^^^^^^^
pattern Сущ0 export{ РОД ПАДЕЖ ЧИСЛО ОДУШ (МОДАЛЬНЫЙ) (CharCasing) (ПАДЕЖВАЛ) node:root_node }
{
 n1=существительное:*{ одуш:неодуш } : export{ РОД ПАДЕЖ ЧИСЛО ОДУШ node:root_node }
 n2=ЧислоЦифрами
} : links { n1.<RIGHT_NAME>n2 }
: ngrams { -5 }




// ------------------------------


// ----- СЛОВОСОЧЕТАНИЯ ИЗ ЧИСЛИТЕЛЬНОГО И СУЩЕСТВИТЕЛЬНОГО ----

// 125 картофелин лежат на столе
// ^^^^^^^^^^^^^^
// На борту находятся 25 членов экипажа.
//                    ^^^^^^^^^^^^^^^^^
// 5 попыток дозвонится провалились
// ^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:ИМ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// Взять 3 кекса.
//       ^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:ВИН ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// Откажись от 120 рублей!
//             ^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:РОД ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// Мы обошлись 100 рублями
//             ^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ТВОР }:export { РОД ПАДЕЖ:ТВОР ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// Передай 5 сотрудникам
//         ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ДАТ }:export { РОД ПАДЕЖ:ДАТ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// они остановились на 8 штуках
//                     ^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ПРЕДЛ }:export { РОД ПАДЕЖ:ПРЕДЛ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.<ATTRIBUTE>c }


// ----- СЛОВОСОЧЕТАНИЯ ИЗ ЧИСЛИТЕЛЬНОГО, ПРИЛАГАТЕЛЬНОГО И СУЩЕСТВИТЕЛЬНОГО ----


// ------------------------------

// Группа прилагательных для модификации
// существительного в паттерне с числом.

patterns ГруппаПрилДляСущ export { ЧИСЛО ПАДЕЖ node:root_node }

// Мы взяли 4 престижнейших приза
//            ^^^^^^^^^^^^^
pattern ГруппаПрилДляСущ
{
 ГруппаПрил2 : export { ЧИСЛО ПАДЕЖ node:root_node }
}

// ------------------------------


// 5 спелых яблок лежат на полу
// ^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:РОД =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:ИМ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }

// я съел 3 сочных яблока
//        ^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:РОД =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:ВИН ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }


// Они остались без 6 новых велосипедов
//                  ^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:РОД =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:РОД }:export { РОД ПАДЕЖ:РОД ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }


// Они воспользовались 3 длинными шестами
//                     ^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:ТВОР =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ТВОР }:export { РОД ПАДЕЖ:ТВОР ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }


// Волны ударили по 8 новым щитам
//                  ^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:ДАТ =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ДАТ }:export { РОД ПАДЕЖ:ДАТ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }


// Мы поехали на 7 новых машинах
//               ^^^^^^^^^^^^^^^
pattern ГруппаСущ1
{
 c=ЧислоЦифрами : export { ЧИСЛО }
 adj=ГруппаПрилДляСущ{ ПАДЕЖ:ПРЕДЛ =c:ЧИСЛО }
 obj=Сущ0{ ПАДЕЖ:ПРЕДЛ }:export { РОД ПАДЕЖ:ПРЕДЛ ОДУШ МОДАЛЬНЫЙ CHARCASING ПАДЕЖВАЛ node:root_node }
}
: links { obj.{ <ATTRIBUTE>c <ATTRIBUTE>adj } }


// ================================================================

// ------------------------------------------------------

word_set АббревиатураДенежн={"млн","млрд","тыс","трлн","млн.","млрд.","тыс.","трлн."}

patterns КоличДенегНескл export { node:root_node }

pattern КоличДенегНескл
{
 c=ЧислоЦифрами
 n=АббревиатураДенежн:export{ node:root_node }
}
: links { n.<ATTRIBUTE>c }
: ngrams { 1 }


pattern ГруппаСущ1 export { (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) (МОДАЛЬНЫЙ) (CHARCASING) (ПАДЕЖВАЛ) node:root_node }
{
 КоличДенегНескл:export { ЧИСЛО:МН ПАДЕЖ:ИМ node:root_node }
}

// Общая стоимость проекта оценивается в $482 млн.
//                                            ^^^^
pattern ГруппаСущ1 export { (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) (МОДАЛЬНЫЙ) (CHARCASING) (ПАДЕЖВАЛ) node:root_node }
{
 КоличДенегНескл:export { ЧИСЛО:МН ПАДЕЖ:ВИН node:root_node }
}

// Внешнеторговый оборот Кузбасса за год составил более $7 млрд.
//                                                         ^^^^^
pattern ГруппаСущ1 export { (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) (МОДАЛЬНЫЙ) (CHARCASING) (ПАДЕЖВАЛ) node:root_node }
{
 КоличДенегНескл:export { ЧИСЛО:МН ПАДЕЖ:РОД node:root_node }
}

pattern ГруппаСущ1 export { (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) (МОДАЛЬНЫЙ) (CHARCASING) (ПАДЕЖВАЛ) node:root_node }
{
 КоличДенегНескл:export { ЧИСЛО:МН ПАДЕЖ:ДАТ node:root_node }
}



