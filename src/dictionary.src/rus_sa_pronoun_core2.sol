
// ------------------------------------------------------------------

patterns УказМест0 export { ПАДЕЖ РОД ОДУШ node:root_node }

wordentry_set УказМест0_Муж = местоим_сущ:{ "кое-кто", кто, никто, некто, "кто-либо", "кто-нибудь", "кто-то" }
wordentry_set УказМест0_Ср = местоим_сущ:{ себя, всё, то, это, "что-то", "кое-что", ничто, что, нечто, "что-либо", "что-нибудь" }

pattern УказМест0
{
 УказМест0_Муж : export { ПАДЕЖ РОД:МУЖ ОДУШ:ОДУШ node:root_node }
}

pattern УказМест0
{
 УказМест0_Ср : export { ПАДЕЖ РОД:СР ОДУШ:НЕОДУШ node:root_node }
}

// --------------------------------------------------------------------

patterns УказМест export { ПАДЕЖ РОД ОДУШ node:root_node }

// все будет хорошо
// ^^^
// никто не старался занимать гостей
// ^^^^^
// кто спит
// ^^^
//
// это случилось летом
// ^^^
//
// что происходит
// ^^^
//
// - У тебя что-нибудь болит ?
//          ^^^^^^^^^^
//
// кое-что случилось
// ^^^^^^^
//
// Ничто не трогало его.
// ^^^^^
//
// нечто произошло внутри дворца.
// ^^^^^
//
// все это должно было случиться
// ^^^^^^^
//
// то было по моей вине
// ^^
//
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
}

// У себя-ка посмотри!
//   ^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 t='-'
 p=частица:ка{}
}
: links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }
: ngrams { 1 }


// У себя-то будешь долеживать?
//   ^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 t='-'
 p=частица:то{}
}
: links { n.<POSTFIX_PARTICLE>t.<NEXT_COLLOCATION_ITEM>p }
: ngrams { 1 }


// это ли многие называют душой?
// ^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 p=частица:ли{}
} : links { n.<POSTFIX_PARTICLE>p }

// кого бы нам взять?
// ^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 p=ЧастицаБы
} : links { n.<POSTFIX_PARTICLE>p }


// ничего же тут нет!
// ^^^^^^^^^
// это ж менять надо!
// ^^^^^
// кто же спит
// ^^^^^^
//
// кто-то же заснул
// ^^^^^^
//
// что же там случилось
// ^^^^^^
//
// что-то же там случилось
// ^^^^^^^^^
//
// да кто ж меня узнает?
//    ^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 p=ЧастицаЖе
} : links { n.<POSTFIX_PARTICLE>p }



// кого это еще демоны принесли?
// ^^^^^^^^
// - Что это она делает?
//   ^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 x=частица:это{}
} : links { n.<POSTFIX_PARTICLE>x }
  : ngrams { -3 }


// чем же это ты занимаешься?
// ^^^^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 z=частица:же{}
 x=частица:это{}
} : links { n.{
               <POSTFIX_PARTICLE>z
               <POSTFIX_PARTICLE>x
              }
          }
  : ngrams { -3 }



// именно это мы должны выяснить
// ^^^^^^^^^^
pattern УказМест
{
 mod=УсилитНаречие
 n=УказМест : export { ПАДЕЖ РОД ОДУШ node:root_node }
}
: links { n.<ATTRIBUTE>mod }
: ngrams { 1 }



// - Я знаю и это.
//          ^^^^^
pattern УказМест
{
 p=частица:и{}
 n=УказМест : export { ПАДЕЖ РОД ОДУШ node:root_node }
} : links { n.<PREFIX_PARTICLE>p }
: ngrams { -3 }



// я не это имел в виду.
//   ^^^^^^
pattern УказМест
{
 p=частица:не{}
 n=УказМест : export { ПАДЕЖ РОД ОДУШ node:root_node }
} : links { n.<NEGATION_PARTICLE>p }




// уж это она могла сказать точно!
// ^^^^^^
pattern УказМест
{
 p=частица:уж{}
 n=УказМест : export { ПАДЕЖ РОД ОДУШ node:root_node }
} : links { n.<PREFIX_PARTICLE>p }


// вот чего боится враг!
// ^^^^^^^^
pattern УказМест
{
 p=частица:вот{}
 n=УказМест : export { ПАДЕЖ РОД ОДУШ node:root_node }
} : links { n.<PREFIX_PARTICLE>p }



// старик мало кому об этом рассказывал
//        ^^^^^^^^^
pattern УказМест
{
 a=ГруппаНареч1{ МногоМалоНаречие }
 n=местоим_сущ:кто{} : export { ПАДЕЖ РОД:МУЖ ОДУШ:ОДУШ node:root_node }
} : links { n.<ATTRIBUTE>a }
  : ngrams { 4 }


// кто ж это привязал здесь коня?
// ^^^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 z=ЧастицаЖе
 x=частица:это{}
} : links { n.{
               <POSTFIX_PARTICLE>z
               <POSTFIX_PARTICLE>x
              } }
  : ngrams { -5 }


// кто бы это мог быть?
// ^^^^^^^^^^
pattern УказМест
{
 n=УказМест0 : export { ПАДЕЖ РОД ОДУШ node:root_node }
 z=ЧастицаБы
 x=частица:это{}
} : links { n.{
               <POSTFIX_PARTICLE>z
               <POSTFIX_PARTICLE>x
              } }
  : ngrams { -5 }


// -----------------------------------------------------------------

patterns ГруппаМестоимСущ export { РОД ПАДЕЖ ОДУШ node:root_node } 

pattern СущСРодДоп
{
 ГруппаМестоимСущ : export{ РОД ПАДЕЖ ЧИСЛО:ЕД ОДУШ node:root_node }
}



pattern ГруппаМестоимСущ
{
 n=УказМест : export { РОД ПАДЕЖ ОДУШ node:root_node } 
}


// -------------------------------------------------------------------

wordentry_set ТоЭто_УказМест=местоим_сущ:{ то, это }

// Дело закончилось этим же самым.
//                  ^^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 n=ТоЭто_УказМест : export { ПАДЕЖ РОД:СР ОДУШ:НЕОДУШ node:root_node }
 p=частица:же{}
 a=прилагательное:самый{ =n:падеж род:ср число:ед }
} : links
{
 n.{
    <POSTFIX_PARTICLE>p
    <ATTRIBUTE>a
   }
}



// хочу все это знать
//      ^^^^^^^
pattern ГруппаМестоимСущ
{
 adj=прилагательное:весь{ род:ср число:ед }
 p=местоим_сущ:это{ =Adj:ПАДЕЖ }:export{ ПАДЕЖ РОД:СР ОДУШ:НЕОДУШ node:root_node }
} : links { p.<ATTRIBUTE>adj }


// -----------------------------------------------------------------------------

// вы все его знали
//        ^^^
wordentry_set НеПравАтрСР=прилагательное:{ этот, тот, весь }

tree_scorers ВалентностьМестоим

// Некоторые местоименные слова регулярно употребляются с правым прилагательным.
//
// Мы столкнулись с чем-то непонятным
//                  ^^^^^^^^^^^^^^^^^
// Даже среди их свиты или слуг не обнаружилось никого, достойного наказания.
//                                              ^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаМестоимСущ 
{
 n=УказМест : export { РОД ПАДЕЖ ОДУШ node:root_node }
 comma1=','
 a=ГруппаПрил2 { ЧИСЛО:ЕД =n:ПАДЕЖ =n:РОД =n:ОДУШ ~НеПравАтрСР }
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
} : links
{
 n.{
    <PUNCTUATION>comma1
    <ATTRIBUTE>a
    ~<PUNCTUATION>comma2
   }
}
: ngrams { ВалентностьМестоим(n) }


// - Я что-то не то сказал?
//     ^^^^^^^^^^^^
//
// Мы заметили кого-то необычного
//             ^^^^^^^^^^^^^^^^^^
//
// кто-то более внимательный сообщил о находке
// ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаМестоимСущ 
{
 n=УказМест : export { РОД ПАДЕЖ ОДУШ node:root_node }
 a=ГруппаПрил2 { ЧИСЛО:ЕД =n:ПАДЕЖ =n:РОД =n:ОДУШ ~НеПравАтрСР }
}
: links { n.<ATTRIBUTE>a }
: ngrams { ВалентностьМестоим(n) }

// Для суда нам требуется нечто большее.
//                        ^^^^^^^^^^^^^
pattern ГруппаМестоимСущ 
{
 n=УказМест : export { РОД ПАДЕЖ ОДУШ node:root_node }
 a=ГруппаСравнПрил
}
: links { n.<ATTRIBUTE>a }
: ngrams { -2 ВалентностьМестоим(n) }


wordentry_set МодифДляНичего=наречие:{ уже, абсолютно, совсем, практически, буквально }

// у меня нет абсолютно ничего подходящего!
//            ^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 adv=МодифДляНичего
 obj=местоим_сущ:ничто{}:export{ node:root_node ПАДЕЖ РОД:СР ОДУШ:НЕОДУШ }
 a=ГруппаПрил2{ ~НеПравАтрСР род:ср число:ед =obj:падеж }
} : links
{
 obj.{
      <ATTRIBUTE>adv
      <ATTRIBUTE>a
     }
}
: ngrams { ВалентностьМестоим(obj) }



// =============================================================


// В кабинете не было совсем никого, даже ректора.
//                    ^^^^^^
wordentry_set НаречияДляНикого={ наречие:совсем{} }

// В кабинете не было никого, даже ректора.
//                    ^^^^^^^^^^^^^^^^^^^^  
pattern ГруппаМестоимСущ
{
 adv=@optional(ГруппаНареч1{ НаречияДляНикого })
 n=местоим_сущ:никто{} : export { ПАДЕЖ ОДУШ:ОДУШ РОД:МУЖ node:root_node }
 comma=','
 conj=союз:даже{}
 n2=ГруппаСущ4{ =n:ПАДЕЖ }
 comma2=@coalesce(',')
} : links
{
 n.{
    ~<ATTRIBUTE>adv
    <RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2.~<PUNCTUATION>comma2
   }
}

// За  его  пределами не было ничего, даже космоса.
//                            ^^^^^^^^^^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 adv=@optional(ГруппаНареч1{ НаречияДляНикого })
 n=местоим_сущ:ничто{} : export { ПАДЕЖ ОДУШ:НЕОДУШ РОД:СР node:root_node }
 comma=','
 conj=союз:даже{}
 n2=ГруппаСущ4
 comma2=@coalesce(',')
} : links
{
 n.{
    ~<ATTRIBUTE>adv
    <RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2.~<PUNCTUATION>comma2
   }
}




// Прямое дополнение с паттерном ОДНО И ТО ЖЕ
// Изо дня в день мы думаем и делаем одно и то же.
//                                   ^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 a1=прилагательное:один{ род:ср число:ед } : export { ПАДЕЖ ОДУШ:НЕОДУШ РОД:СР node:root_node }
 conj=союз:и{}
 a2=прилагательное:тот{ род:ср число:ед =a1:падеж }
 p=частица:же{}
} : links { a1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a2.<POSTFIX_PARTICLE>p }
  : ngrams { 2 }

// ============================================

wordentry_set Местоим_ЧТО = местоим_сущ:{ "что-то", "кое-что", нечто, "что-либо", "что-нибудь" }

// - Я что-то не то сказал?
//     ^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 obj=Местоим_ЧТО:export{ node:root_node ПАДЕЖ РОД:СР ОДУШ:НЕОДУШ }
 p=частица:не{}
 adj=прилагательное:тот{ число:ед =obj:падеж род:ср }
} : links { obj.<ATTRIBUTE>adj.<NEGATION_PARTICLE>p }


wordentry_set Местоим_КТО = местоим_сущ:{ "кое-кто", кто, некто, "кто-либо", "кто-нибудь", "кто-то" }

// Ты встретил кого-то не того
//             ^^^^^^^^^^^^^^^
pattern ГруппаМестоимСущ
{
 obj=Местоим_КТО:export{ node:root_node ПАДЕЖ РОД:МУЖ ОДУШ:ОДУШ }
 p=частица:не{}
 adj=прилагательное:тот{ число:ед =obj:падеж род:муж }
} : links { obj.<ATTRIBUTE>adj.<NEGATION_PARTICLE>p }

// ---------------------------------------------------------------