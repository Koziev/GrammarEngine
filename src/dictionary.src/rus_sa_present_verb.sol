// *******************************************************************************
// глагольное сказуемое прошедшего, настоящего или аналитического будущего времени
// *******************************************************************************


// --------------------------------------------------------
// Несколько глаголов участвуют в роли квазивспомогательных
// в конструкциях императива:
//
// Дайте-ка я взгляну.
// ^^^^^^^^

patterns ВспомогГлаголИмператив export { node:root_node }

wordentry_set ВспомогГлаголИмператив0=глагол:{ давать, дать, пойти, идти, сходить, попробовать }
pattern ВспомогГлаголИмператив
{
 ГлСПостфиксом{ НАКЛОНЕНИЕ:ПОБУД ВспомогГлаголИмператив0 } : export{ node:root_node }
}


// Бомбы рвались и на берегу.
//                 ~~~~~~~~~
wordentry_set НеВспомогГлаголИмператив0=глагол:{ на }

// На всякий случай, если в ВспомогГлаголИмператив0 перечислены не все возможные
// вспомогательные глаголы - для любых глаголов с небольшим штрафом разрешаем тоже.
pattern ВспомогГлаголИмператив
{
 ГлСПостфиксом{ НАКЛОНЕНИЕ:ПОБУД ~ВспомогГлаголИмператив0 ~НеВспомогГлаголИмператив0 } : export{ node:root_node }
} : ngrams { -4 }


// - Можно я сяду?
//   ^^^^^
pattern ВспомогГлаголИмператив
{
 безлич_глагол:можно{} : export{ node:root_node }
}


// ----------------------------------------------------------


patterns ГлНаст0 export { node:root_node ЛИЦО ЧИСЛО РОД МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }

// кенгуру спал
//         ^^^^
pattern ГлНаст0
{
 v=Гл1{ ВРЕМЯ:ПРОШЕДШЕЕ } : export { РОД ЛИЦО ЧИСЛО МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
}

// кенгуру спит
//         ^^^^
pattern ГлНаст0
{
 v=Гл1{ ВРЕМЯ:НАСТОЯЩЕЕ } : export { РОД ЛИЦО ЧИСЛО МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
}

// кенгуру уснёт
//         ^^^^^
pattern ГлНаст0
{
 v=Гл1{ ВРЕМЯ:БУДУЩЕЕ } : export { РОД ЛИЦО ЧИСЛО МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
}


// ты поищи!
//    ^^^^^
pattern ГлНаст0
{
 v=Гл1{ НАКЛОНЕНИЕ:ПОБУД } : export { РОД ЛИЦО ЧИСЛО МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
}

// ############################################################


// ===========================================================
// Восходящая сборка модального ядра настояще-будущего времени
// ===========================================================

patterns ГлНастМодальн_Восх { bottomup  } export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }

pattern ГлНастМодальн_Восх
{
 Гл1{ ВРЕМЯ:ПРОШЕДШЕЕ МОДАЛЬНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}

pattern ГлНастМодальн_Восх
{
 Гл1{ ВРЕМЯ:НАСТОЯЩЕЕ МОДАЛЬНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}

pattern ГлНастМодальн_Восх
{
 Гл1{ ВРЕМЯ:БУДУЩЕЕ МОДАЛЬНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}

// Для образования синтетического будущего времени:
// мы будем плясать
//    ^^^^^^^^^^^^^
pattern ГлНастМодальн_Восх
{
 Гл1{ ГлаголБыть ВРЕМЯ:БУДУЩЕЕ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}


// Попробуйте-ка это отрицать!
// ^^^^^^^^^^^^^
pattern ГлНастМодальн_Восх
{
 Гл1{ НАКЛОНЕНИЕ:ПОБУД МОДАЛЬНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}


pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх{ [-1]ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ }
 obj=ГлОбъект{ [-10]=v:ПАДЕЖ ~ПАДЕЖ:ИМ } : export {
                                                   @except(v:ПАДЕЖ,ПАДЕЖ)
                                                   @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                                  }
} : links
{
 v.<OBJECT>obj
}
: ngrams
{
 -2 // для модальных конструкций это нетипично,
    // чтобы дополнение прикреплялось к модальному
 v_obj_score(v,obj)
}



pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх{ ВРЕМЯ:БУДУЩЕЕ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
 aux=ВспомогГлаголИмператив
}
: links { v.<AUX2IMPERATIVE_VERB>aux }


patterns ОбстГлагСказ export { node:root_node }


pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
 adv=ОбстГлагСказ
}
: links { v.<ATTRIBUTE>adv }
: ngrams
{
 -1
 adv_verb_score( adv, v )
}


pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
 pn=ПредлогИСущ{ гл_предл( v, _.prepos, _.n2 ) }
} : links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 -1
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}



// Одиночный предлог:
// Буду считать до...
pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
 p=предлог:*{}
}
: links { v.<ATTRIBUTE>p }
: ngrams { -6 }



pattern ГлНастМодальн_Восх
{
 v=ГлНастМодальн_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
 q2=ЗакрывающаяКавычка
}
: links { v.<APPEND_QUOTE>q2 }
: ngrams { -1 }



// --------------------------------------------------------

patterns ГлНастМодальн_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }

pattern ГлНастМодальн_Нисх
{
 ГлНастМодальн_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}

pattern ГлНастМодальн_Нисх
{
 aux=ВспомогГлаголИмператив
 v=ГлНастМодальн_Нисх{ ВРЕМЯ:БУДУЩЕЕ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}
: links { v.<AUX2IMPERATIVE_VERB>aux }


// Мы весело играли
pattern ГлНастМодальн_Нисх
{
 adv=ОбстГлагСказ
 v=ГлНастМодальн_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
} 
: links { v.<ATTRIBUTE>adv }
: ngrams
{
 -1
 adv_verb_score( adv, v )
}



pattern ГлНастМодальн_Нисх
{
 pn=ПредлогИСущ
 v=ГлНастМодальн_Нисх { гл_предл(_,pn.prepos,pn.n2) } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
} 
: links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 -1
 prepos_score( v, pn.prepos )
 ngram3(v,pn.prepos,pn.n2)
}



pattern ГлНастМодальн_Нисх
{
 obj=ГлОбъект
 v=ГлНастМодальн_Нисх { ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ =Obj:ПАДЕЖ }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -2 // обычно в модальном обороте прямое дополнение прикрепляется именно к инфинитиву
 v_obj_score( v, obj )
}


pattern ГлНастМодальн_Нисх
{
 obj=ГлОбъект{ ~ПАДЕЖ:ИМ }
 v=ГлНастМодальн_Нисх { !=Obj:ПАДЕЖ }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -11
 v_obj_score( v, obj )
}


pattern ГлНастМодальн_Нисх
{
 q1=ОткрывающаяКавычка
 v=ГлНастМодальн_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
} 
: links { v.<PREPEND_QUOTE>q1 }
: ngrams { -1 }


// -------------------------------------------

patterns ГлНастМодальн export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ }
pattern ГлНастМодальн
{
 v=ГлНастМодальн_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ }
}
: ngrams
{
 ВалентностьГлагола(v)
}


patterns ГлНастМодальн_НЕ export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ }
pattern ГлНастМодальн_НЕ
{
 p=частица:не{}
 v=Гл1{ МОДАЛЬНЫЙ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН } : export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ }
} : links { v.<NEGATION_PARTICLE>p }


// --------------------------------------------------------

// разговор получается интересный
//          ^^^^^^^^^^^^^^^^^^^^^
pattern ГлНаст export { ЧИСЛО ЛИЦО РОД ПЕРЕХОДНОСТЬ (ПАДЕЖ) ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
{
 adv=@optional(ГруппаНареч2)
 v=ПолучатьсяКакСвязка : export{ ЧИСЛО ЛИЦО РОД ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ ВРЕМЯ НАКЛОНЕНИЕ node:root_node }
 obj=ГруппаПрил2{ =V:ЧИСЛО =V:РОД ПАДЕЖ:ИМ }
} : links
{
 v.{
    ~<ATTRIBUTE>adv
    <ATTRIBUTE>obj
   }
}
: ngrams
{
 -1
 ВалентностьГлагола(v)
}








// -----------------------------------------------------------------------
// ПОЛНЫЙ ПРЕДИКАТ (СКАЗУЕМОЕ+ПОДЛЕЖАЩЕЕ) НАСТОЯЩЕГО ИЛИ БУДУЩЕГО ВРЕМЕНИ,
// В КОТОРОМ ГЛАГОЛ В ЛИЧНОЙ ФОРМЕ СТОИТ В КОРНЕ
// (сравни с ГлНастИнфНисх_Сказ).


patterns ГлНастСказ export { ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }

pattern ГлНастСказ export { ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
{
 ГлНаст0 : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ @add(ПАДЕЖВАЛ:ИМ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}


// -----------------------
// Восходящая сборка
// -----------------------

patterns ГлНастСказ_Восх { bottomup  } export { ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }

pattern ГлНастСказ_Восх
{
 ГлНастСказ : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}

// Давайте мы поставим кровать сюда.
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 aux=ВспомогГлаголИмператив
}
: links { v.<AUX2IMPERATIVE_VERB>aux }


// Подлежащее справа от глагола:
// У него было свое мнение
//        ^^^^ ^^^^^^^^^^^
pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 v=ГлНастСказ_Восх{ ПАДЕЖВАЛ:ИМ } : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 sbj=Подлежащее{ =v:ЛИЦО =v:ЧИСЛО =v:РОД } : export { ЛИЦО ЧИСЛО РОД }
}
: links { v.<SUBJECT>sbj }
: ngrams { sbj_v_score(sbj,v) }


// Обстоятельство (обычно наречие) справа от глагола
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 adv=ОбстГлагСказ
}
: links { v.<ATTRIBUTE>adv }
: ngrams { adv_verb_score( adv, v ) }







// Инфинитив в роли подлежащего среднего рода единственного числа:
// При этом, у него было что рассказать.
//                  ^^^^ ^^^^^^^^^^^^^^
// Здесь есть на что посмотреть.
//            ^^^^^^^^^^^^^^^^^

word_set БылоГлаг = { 'было', 'будет', 'есть', 'стало', 'станет', 'становится', 'становилось', 'оказалось' }

pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 v=ГлНастСказ_Восх{ ПАДЕЖВАЛ:ИМ БылоГлаг } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 sbj=Инф2
}
: links { v.<SUBJECT>sbj }
: ngrams
{
 -3
 sbj_v_score(sbj,v)
}


// ----------------------------------------

patterns Что_Подлеж export { node:root_node }

pattern Что_Подлеж
{
 n=местоим_сущ:что{ падеж:им } : export { node:root_node }
}

// Что же это был за человек?
// ^^^^^^
pattern Что_Подлеж
{
 n=местоим_сущ:что{ падеж:им } : export { node:root_node }
 p=ЧастицаЖе
} : links { n.<POSTFIX_PARTICLE>p }

// Особая конструкция "составного подлежащего":
// И что это будут за люди?
//   ^^^^^^^       ^^^^^^^
pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 w1=Что_Подлеж
 w2=ЭТО_Местоим
 v=ГлНастСказ_Восх{ ПАДЕЖВАЛ:ИМ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 p=частица:за{}
 sbj=Подлежащее{ [-4]=v:ЧИСЛО [-4]=v:ЛИЦО [-4]=v:РОД }
}
: links
{
 v.<SUBJECT>sbj.{
                 <NEXT_COLLOCATION_ITEM>w1
                 <NEXT_COLLOCATION_ITEM>w2
                 <PREFIX_PARTICLE>p
                }
}
: ngrams
{
 1
 sbj_v_score(sbj,v)
}


// Я занял твое место?
//   ^^^^^      ^^^^^
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 obj=ГлОбъект{ =v:ПАДЕЖ } : export {
                                    @except(v:ПАДЕЖ,ПАДЕЖ)
                                    @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                   }
}
: links { v.<OBJECT>obj }
: ngrams { v_obj_score(v,obj) }





// Индуцированная модальность:
// Иметь смелость сказать правду
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^^

wordentry_set ИметьЧтотоСделать=глагол:{ иметь }
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН ИметьЧтотоСделать } : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 obj=ГлОбъект{ ПАДЕЖ:ВИН } : export {
                                    @except(v:ПАДЕЖ,ПАДЕЖ)
                                    @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                   }
 inf=Инф2
}
: links { v.{ <OBJECT>obj <INFINITIVE>inf } }










// День выдался ненастный.
// ^^^^         ^^^^^^^^^
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 attr=ОбособленныйАтрибут
} : links
{
 v.<SEPARATE_ATTR>attr
}


pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 attr=ВводнАктант
} : links
{
 v.<BEG_INTRO>attr
}

pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 attr=Детализатор
} : links
{
 v.<DETAILS>attr
}


pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 x=ПрямоеОбращение
} : links { v.<APPEAL>x }


// он прыгнул и зацепился за край ограды
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 pn=ПредлогИСущ{ гл_предл( v, _.prepos, _.n2 ) }
} : links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}

// Одиночный предлог:
// Считаю до...
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 p=предлог:*{}
}
: links { v.<ATTRIBUTE>p }
: ngrams { -6 }




// Правое придаточное:
//
// Он несет такую ахинею, что уши вянут
//                      ^^^^^....
pattern ГлНастСказ_Восх export { ЛИЦО ЧИСЛО РОД (ПАДЕЖ) ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ (МОДАЛЬНЫЙ) НАКЛОНЕНИЕ node:root_node }
{
 v=ГлНастСказ_Восх : export { ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ ПАДЕЖВАЛ НАКЛОНЕНИЕ node:root_node }
 comma=','
 conj=СоединДляПридаточного
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
} : links
{
 v.<SUBORDINATE_CLAUSE>comma.
    <NEXT_COLLOCATION_ITEM>conj.
     <NEXT_COLLOCATION_ITEM>p.
      ~<PUNCTUATION>comma2
}



// Расспросишь, как все было.
//            ^^^^^^^^^^^^^^
pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ (МОДАЛЬНЫЙ) (ПАДЕЖ) НАКЛОНЕНИЕ ПЕРЕХОДНОСТЬ }
{
 v=ГлНастСказ_Восх{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ НАКЛОНЕНИЕ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ }
 comma=','
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
} : links
{
 v.<SUBORDINATE_CLAUSE>comma.
    <NEXT_COLLOCATION_ITEM>p.
     ~<PUNCTUATION>comma2
}

// Придаточный клоз без запятой:
// сам видел как ему дали бумагу.
//     ~~~~~~~~~~~~
pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ (МОДАЛЬНЫЙ) (ПАДЕЖ) НАКЛОНЕНИЕ ПЕРЕХОДНОСТЬ }
{
 v=ГлНастСказ_Восх{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ НАКЛОНЕНИЕ ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ }
 p=ПридаточноеВОбороте
 comma2=@coalesce(',')
}
: ngrams { -6 }
: links
{
 v.<NEXT_COLLOCATION_ITEM>p.
     ~<PUNCTUATION>comma2
}




// ------------------------------------------

// я понимаю, о чем ты говоришь
pattern ГлНастСказ_Восх export { ЛИЦО ЧИСЛО РОД (ПАДЕЖ) ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ (МОДАЛЬНЫЙ) НАКЛОНЕНИЕ node:root_node }
{
 v=ГлНастСказ_Восх{  ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН } : export { ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ:НЕПЕРЕХОДНЫЙ ПАДЕЖВАЛ НАКЛОНЕНИЕ node:root_node }
 p=БессоюзноеПридаточное
}
: links { v.<SUBORDINATE_CLAUSE>p }
: ngrams
{
 -1
 ВалентностьБессоюзнПридаточн(v)
}

// --------------------------------------

// Выбился он в лидеры заезда
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ГлагСДопВИмП } : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 prep=предлог:в{}
 obj=ГруппаСущ4{ ПАДЕЖ:ИМ одуш:одуш число:мн }
}
: links { v.<PREPOS_ADJUNCT>prep.<OBJECT>obj }
: ngrams { ngram3(v,prep,obj) }


// притворился кот спящим
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ПритворитьсяТакимтоГл ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ТВОР } : export{ ЧИСЛО ЛИЦО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 obj=ГлДопПрил{ =V:ЧИСЛО =V:ЛИЦО =V:РОД } : export {
                                                    @except(v:ПАДЕЖ,ПАДЕЖ)
                                                    @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                                   }
}
: links { v.<OBJECT>obj }
: ngrams { v_obj_score(v,obj) }



// ----------------------------------------------------

// +++++ составное сказуемое


// -----------------------------------------


// Говорит он и смеётся
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 conj=СочинитСоюзПредикат
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО [-1]=v:НАКЛОНЕНИЕ }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>v2 }
: ngrams { 1 }


// Обычно союзные паттерны связывают глаголы в одинаковой форме.
// Говорит он, хотя и не подаёт руки
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 comma=','
 conj=СочинитСоюзПредикат
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО }
 @noshift(ПравыйОграничительОборота)
}
: links
{
 v.<RIGHT_LOGIC_ITEM>comma.
    <NEXT_COLLOCATION_ITEM>conj.
     <NEXT_COLLOCATION_ITEM>v2
}
: ngrams { 2 }

pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 comma=','
 conj=СочинитСоюзПредикат
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО } // ??? нужно ли проверять согласование?
 @noshift(ПравыйОграничительОборота)
}
: links
{
 v.<RIGHT_LOGIC_ITEM>comma.
    <NEXT_COLLOCATION_ITEM>conj.
     <NEXT_COLLOCATION_ITEM>v2
}




// Говорит он, зевает и закрывает глаза
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 comma=','
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО } // ??? нужно ли проверять согласование?
 @noshift(ПравыйОграничительОборота)
}
: links
{
 v.<RIGHT_LOGIC_ITEM>comma.
    <NEXT_COLLOCATION_ITEM>v2
}
: ngrams { 1 }



pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 q2=ЗакрывающаяКавычка
}
: links { v.<APPEND_QUOTE>q2 }
: ngrams { -1 }


// Особый случай присоединения обстоятельства справа - через противительный союз:
// Сближаюсь, но медленно.
//          ^^^^^^^^^^^^^
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 comma=','
 conj=союз:но{}
 adv=Обст
 @noshift(ПравыйОграничительОборота)
}
: links { v.<ATTRIBUTE>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>adv }
: ngrams { adv_verb_score( adv, v ) }


// Покупаем, но только на мясокомбинате.
//         ^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 comma=','
 conj=союз:но{}
 pn=ПредлогИСущ{ гл_предл( v, _.prepos, _.n2 ) }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<PREPOS_ADJUNCT>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>pn }
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}


// дательное дополнение+предложное премыкание НА/В+предл:
// садится на спину второму
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ~ПАДЕЖ:ДАТ } : export { node:root_node ЛИЦО ЧИСЛО РОД МОДАЛЬНЫЙ ПАДЕЖВАЛ ПАДЕЖ ПЕРЕХОДНОСТЬ НАКЛОНЕНИЕ }
 obj=ДатОбъект
}
: links { v.<OBJECT>obj }
: ngrams { -10 }


// Объект в творительном падеже может рассматриваться обстоятельством способа:
// Добраться автобусом
//           ^^^^^^^^^
pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх{ ~ПАДЕЖ:ТВОР } : export { node:root_node ЛИЦО ЧИСЛО РОД МОДАЛЬНЫЙ ПАДЕЖВАЛ ПАДЕЖ ПЕРЕХОДНОСТЬ НАКЛОНЕНИЕ }
 obj=ГруппаСущ4{ ПАДЕЖ:ТВОР ОДУШ:НЕОДУШ }
}
: links { v.<OBJECT>obj }
: ngrams { -3 }





// Не согласовенные подлежащее и сказуемое.
pattern ГлНастСказ_Восх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 v=ГлНастСказ_Восх : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 sbj=Подлежащее : export { ЛИЦО ЧИСЛО РОД }
}
: links { v.<SUBJECT>sbj }
: ngrams
{
 -10 sbj_v_score(sbj,v)
}

pattern ГлНастСказ_Восх
{
 v=ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
 obj=ГлОбъект{ !=v:ПАДЕЖ } : export {
                                     @except(v:ПАДЕЖ,ПАДЕЖ)
                                     @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                    }
}
: links { v.<OBJECT>obj }
: ngrams
{
 -11
 v_obj_score(v,obj)
}





// --------------------------------------------------------
// Нисходящая сборка
// --------------------------------------------------------

patterns ГлНастСказ_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }

pattern ГлНастСказ_Нисх
{
 ГлНастСказ_Восх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}


// -----------------------------------------------------

// Он то плачет, то смеётся
pattern ГлНастСказ_Нисх
{
 conj1=ЛогичСоюз2
 v=ГлНастСказ_Нисх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 comma=','
 conj2=ЛогичСоюз2
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО }
 @noshift(ПравыйОграничительОборота)
}
: links
{
 v.{
    <PREFIX_CONJUNCTION>conj1
    <RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj2.<NEXT_COLLOCATION_ITEM>v2
   }
}
: ngrams { 2 }


// Вы либо помните, либо нет.
//    ^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастСказ_Нисх
{
 conj1=ЛогичСоюз2
 v=ГлНастСказ_Нисх : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
 comma=','
 conj2=ЛогичСоюз2
 v2=частица:нет{}
 @noshift(ПравыйОграничительОборота)
}
: links
{
 v.{
    <PREFIX_CONJUNCTION>conj1
    <RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj2.<NEXT_COLLOCATION_ITEM>v2
   }
}
: ngrams { 3 }


// -----------------------------------------------------

pattern ГлНастСказ_Нисх
{
 adv=ЛевАтрибутСказуем
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links { v.<ATTRIBUTE>adv }
: ngrams { -2 }


// Кровать сюда поставим давайте
pattern ГлНастСказ_Нисх
{
 aux=ВспомогГлаголИмператив
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}
: links { v.<AUX2IMPERATIVE_VERB>aux }


// -----------------------------------------------------


pattern ГлНастСказ_Нисх
{
 adv=ОбстГлагСказ
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<ATTRIBUTE>adv
}
: ngrams
{
 adv_verb_score( adv, v )
}




/*
// ??? УДАЛИТЬ ???
// Он сесть на пол приказал ему
pattern ГлНастСказ_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ (МОДАЛЬНЫЙ) }
{
 inf=Инф2
 v=ГлНастСказ_Нисх{ МОДАЛЬНЫЙ НАКЛОНЕНИЕ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ }
}  
: links { v.<INFINITIVE>inf }
: ngrams
{
 -5
 v_inf_score( v, inf )
}
*/


pattern ГлНастСказ_Нисх
{
 attr=ОбособленныйАтрибут
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<SEPARATE_ATTR>attr
}

pattern ГлНастСказ_Нисх
{
 attr=ВводнАктант
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<BEG_INTRO>attr
}


pattern ГлНастСказ_Нисх
{
 attr=Детализатор
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<DETAILS>attr
}


pattern ГлНастСказ_Нисх
{
 x=ПрямоеОбращение
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<APPEAL>x
}

pattern ГлНастСказ_Нисх
{
 pn=ПредлогИСущ
 v=ГлНастСказ_Нисх { гл_предл(_,pn.prepos,pn.n2) } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
} 
: links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}



pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект
 v=ГлНастСказ_Нисх { ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ =Obj:ПАДЕЖ }
     : export {
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -1
 v_obj_score( v, obj )
}

// Мужчине такое не сошло бы.
// ^^^^^^^
// Его радости не было предела.
// ^^^^^^^^^^^
pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект{ ПАДЕЖ:ДАТ }
 v=ГлНастСказ_Нисх{ ~ПАДЕЖ:ДАТ }
     : export {
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -8
 v_obj_score( v, obj )
}




pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект{ ~ПАДЕЖ:ИМ ~ПАДЕЖ:ПРЕДЛ }
 v=ГлНастСказ_Нисх { !=Obj:ПАДЕЖ }
     : export {
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -10
 v_obj_score( v, obj )
}





// Отдельно обрабатываем случай заполнения винительной
//  валентности родительным дополнением:
//
// - А ты, братец, чего-то недоговариваешь.
//                 ^^^^^^^
pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект{ ПАДЕЖ:РОД }
 v=ГлНастСказ_Нисх { ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН }
     : export {
               node:root_node
               ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               ПАДЕЖ
               @remove(ПАДЕЖ:ВИН)
               @remove(ПАДЕЖ:РОД)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -4
 v_obj_score( v, obj )
}


/*
pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект
 v=ГлНастСказ_Нисх { !=Obj:ПАДЕЖ }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
} : links { v.<OBJECT>obj }
: ngrams
{
 -11
 v_obj_score( v, obj )
}
*/


patterns ЧтоМестВин export { node:root_node }

pattern ЧтоМестВин
{
 местоим_сущ:что{ падеж:вин } :export { node:root_node }
}

// Что же ты наделал?
// ^^^^^^
pattern ЧтоМестВин
{
 n=местоим_сущ:что{ падеж:вин } :export { node:root_node }
 p=ЧастицаЖе
}
: links { n.<POSTFIX_PARTICLE>p }


// Вопросительное местоимение ЧТО с непереходными глаголами:
// Что ты думаешь на этот счет?
// ^^^
pattern ГлНастСказ_Нисх
{
 obj=ЧтоМестВин
 v=ГлНастСказ_Нисх { ~ПАДЕЖ:ВИН }
     : export { node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ ПАДЕЖ ПЕРЕХОДНОСТЬ НАКЛОНЕНИЕ }
}
: links { v.<OBJECT>obj }
: ngrams { -5 }



// он дракону на спину садится
pattern ГлНастСказ_Нисх
{
 obj=ДатОбъект
 v=ГлНастСказ_Нисх { ~ПАДЕЖ:ДАТ }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               ПАДЕЖ ПЕРЕХОДНОСТЬ
              }
}
: links { v.<OBJECT>obj }
: ngrams { -10 }



// я не буду удерживать вас
// ^.....
pattern ГлНастСказ_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 sbj=Подлежащее
 v=ГлНастСказ_Нисх{ [-7]ПАДЕЖВАЛ:ИМ [-7]=sbj:ЛИЦО [-7]=sbj:ЧИСЛО [-7]=sbj:РОД } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}
: links { v.<SUBJECT>sbj }
: ngrams { sbj_v_score(sbj,v) }


// Инфинитив в роли подлежащего среднего рода единственного числа:
//
// а вот поговорить с девушкой было интересно
//       ^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастСказ_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 sbj=Инф2
 v=ГлНастСказ_Нисх{ ПАДЕЖВАЛ:ИМ БылоГлаг } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}
: links { v.<SUBJECT>sbj }
: ngrams
{
 sbj_v_score(sbj,v)
 -3
}







// Безличная конструкция:
// Тебе что, плохо живется?
pattern ГлНастСказ_Нисх export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
{
 obj=ДатОбъект
 v=ГлНастСказ_Нисх{ ПАДЕЖВАЛ:ИМ ЧИСЛО:ЕД ЛИЦО:3 } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}
: links { v.<OBJECT>obj }
: ngrams
{
 v_obj_score(v,obj)
 -7
}



// Выбился он в лидеры заезда
pattern ГлНастСказ_Нисх
{
 prep=предлог:в{}
 obj=ГруппаСущ4{ ПАДЕЖ:ИМ одуш:одуш число:мн }
 v=ГлНастСказ_Нисх{ ГлагСДопВИмП } : export { ЛИЦО ЧИСЛО РОД ПАДЕЖ ПАДЕЖВАЛ ПЕРЕХОДНОСТЬ МОДАЛЬНЫЙ НАКЛОНЕНИЕ node:root_node }
}
: links { v.<PREPOS_ADJUNCT>prep.<OBJECT>obj }
: ngrams { -1 ngram3(v,prep,obj) }


// бедным и несчастным притворился кот
pattern ГлНастСказ_Нисх
{
 obj=ГлДопПрил
 v=ГлНастСказ_Нисх{ ПритворитьсяТакимтоГл ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ТВОР =obj:ЧИСЛО =obj:ЛИЦО =obj:РОД }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }
}
: links { v.<OBJECT>obj }
: ngrams { v_obj_score(v,obj) }




pattern ГлНастСказ_Нисх
{
 q1=ОткрывающаяКавычка
 v=ГлНастСказ_Нисх : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ }
}
: links { v.<PREPEND_QUOTE>q1 }
: ngrams { -1 }



// Индуцированная модальность:
// Честь имею кланяться.
// ^^^^^^^^^^^^^^^^^^^^
pattern ГлНастСказ_Нисх
{
 obj=ГлОбъект{ ПАДЕЖ:ВИН }
 v=ГлНастСказ_Нисх { ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН ИметьЧтотоСделать }
     : export { 
               node:root_node ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ МОДАЛЬНЫЙ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
              }

 inf=Инф2
}
: links { v.{ <OBJECT>obj <INFINITIVE>inf } }




// -------------------------------------------

// Сказуемое, слот подлежащего свободен.
pattern ГлНаст
{
 v=ГлНастСказ_Нисх{ ПАДЕЖВАЛ:ИМ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}
: ngrams
{
 ВалентностьГлагола(v)
}


// -------------------------------------------

// Полный предикат: слот подлежащего заполнен.
pattern ГлПредикат
{
 v=ГлНастСказ_Нисх{ ~ПАДЕЖВАЛ:ИМ НАКЛОНЕНИЕ:ИЗЪЯВ } : export { node:root_node }
}
: ngrams
{
 1 // но: ~~~Меняйте правила по ходу игры.
 ВалентностьПредиката(v)
 ВалентностьГлагола(v)
}

// Для императива не поощряем наличие подлежащего.
pattern ГлПредикат
{
 v=ГлНастСказ_Нисх{ ~ПАДЕЖВАЛ:ИМ НАКЛОНЕНИЕ:ПОБУД } : export { node:root_node }
}
: ngrams
{
 ВалентностьПредиката(v)
 ВалентностьГлагола(v)
}



// Неполный предикат: нет подлежащего.
// Кажется, немцы выдохлись.
// ^^^^^^^
pattern ГлПредикат
{
 v=ГлНастСказ_Нисх{ ПАДЕЖВАЛ:ИМ } : export { node:root_node }
}
: ngrams
{ 
 -1
 ВалентностьПредиката(v)
 ВалентностьГлагола(v)
}


// ###############################################################
// ПОЛНЫЙ ПРЕДИКАТ (СКАЗУЕМОЕ+ПОДЛЕЖАЩЕЕ) для случаев, когда
// личная форма глагола является модальным глаголом и присоединяет
// инфинитив

// --------------------------------------------------------

patterns СледИнфВСказуемом export { node:root_node }

pattern СледИнфВСказуемом
{
 Инф0 : export { node:root_node }
}

pattern СледИнфВСказуемом
{
 Инф2 : export { node:root_node }
 @noshift(ПравыйОграничительОборота)
}

pattern СледИнфВСказуемом
{
 Инф2 : export { node:root_node }
} : ngrams { -2 }


// --------------------------------------------------------


// KEYFEATURE_DESIRABLE:1 обнаружен союз И, ИЛИ и т.д.
// KEYFEATURE_DETECTED:1 обнаружена конструкция с союзом или запятой (перечисление)

patterns ГлНастИнфВосх_Сказ { bottomup }
export
{
 node:root_node
 ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ // описание незаполненных слотов у инфинитива-корня
 AUX_VERB_REQUIRED // =0 если модальный глагол еще не присоединен к инфинитиву-корню
 KEYFEATURE_DETECTED
 KEYFEATURE_DESIRABLE // присутствует союз в перечислении инфинитивов
 ПАДЕЖВАЛ
 ЛИЦО
 ЧИСЛО
 РОД
 ВРЕМЯ
 НАКЛОНЕНИЕ
}

// Они будут процветать
//           ^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED МОДАЛЬНЫЙ (ЛИЦО) (ЧИСЛО) (РОД) (ВРЕМЯ) (ПАДЕЖВАЛ) (НАКЛОНЕНИЕ) KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE }
{
 Инф0 : export {
                node:root_node
                ПЕРЕХОДНОСТЬ
                ПАДЕЖ
                МОДАЛЬНЫЙ
                AUX_VERB_REQUIRED:1
                @add(ПАДЕЖВАЛ:ИМ)
                KEYFEATURE_DETECTED:0
                KEYFEATURE_DESIRABLE:0
               }
}



// Дополнительные инфинитивы могут прикрепляться справа через союз или перечисляться через
// запятую.
// будут развиваться и процветать люди теперь
//       ^^^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 inf1=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ МОДАЛЬНЫЙ }
 conj=ЛогичСоюз : export { KEYFEATURE_DETECTED:1 KEYFEATURE_DESIRABLE:1 }
 inf2=СледИнфВСказуемом
}
: links { inf1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>inf2 }


// Они будут читать, рисовать и петь.
//           ^^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 inf1=ГлНастИнфВосх_Сказ : export { node:root_node AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ НАКЛОНЕНИЕ ПАДЕЖВАЛ ЛИЦО ЧИСЛО РОД ВРЕМЯ KEYFEATURE_DETECTED:1 KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 comma=','
 inf2=СледИнфВСказуемом
}
: links { inf1.<RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>inf2 }


// Они будут петь, но не плясать
//           ^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 inf1=ГлНастИнфВосх_Сказ : export { node:root_node AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ KEYFEATURE_DETECTED:1 МОДАЛЬНЫЙ }
 comma=','
 conj=СочинитСоюзПредикат : export { KEYFEATURE_DESIRABLE:1 }
 inf2=СледИнфВСказуемом
}
: links
{
 inf1.<RIGHT_LOGIC_ITEM>comma.
  <NEXT_COLLOCATION_ITEM>conj.
   <NEXT_COLLOCATION_ITEM>inf2
}




// Подлежащее может стоять справа от инфинитива и модального глагола:
//
// Будут ли развиваться операции теперь
//                      ^^^^^^^^
pattern ГлНастИнфВосх_Сказ export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ МОДАЛЬНЫЙ AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE }
{
 v=ГлНастИнфВосх_Сказ{ ПАДЕЖВАЛ:ИМ } : export { node:root_node AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 sbj=Подлежащее{ =v:ЛИЦО =v:ЧИСЛО =v:РОД } : export { ЛИЦО ЧИСЛО РОД }
} 
: links { v.<SUBJECT>sbj }
: ngrams { sbj_v_score(sbj,v) }


// Без проверки согласования - для разбора неграмнотной речи
pattern ГлНастИнфВосх_Сказ export { node:root_node ЛИЦО ЧИСЛО РОД ВРЕМЯ МОДАЛЬНЫЙ AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE }
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node AUX_VERB_REQUIRED ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 sbj=Подлежащее : export { ЛИЦО ЧИСЛО РОД }
} 
: links { v.<SUBJECT>sbj }
: ngrams
{
 -10
 sbj_v_score(sbj,v)
}





tree_scorers ГлагИнф

// Прикрепляем модальный глагол к инфинитиву:
//
// Будут ли они возражать?
// ^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ{AUX_VERB_REQUIRED:1} : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED:0 ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 aux=ГлНастМодальн{ =v:ЛИЦО =v:ЧИСЛО =v:РОД  } : export { ЛИЦО ЧИСЛО РОД ВРЕМЯ } 
} : links
{
 v.<LEFT_AUX_VERB>aux
}
: ngrams { ГлагИнф(v) }







// Прямое дополнение справа от инфинитива:
// Будут ли они подавать жалобу?
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ{ [-6]ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ } : export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 obj=ГлОбъект { [-6]=v:ПАДЕЖ } : export {
                                         @except(v:ПАДЕЖ,ПАДЕЖ)
                                         @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                        }
} : links
{
 v.<OBJECT>obj
}
: ngrams
{
 v_obj_score(v,obj)
}


// Родительная валентность появляется у корневого инфинитива в случае,
// когда модальный глагол в отрицательной форме:
//
// Ломать вы ничего не хотите
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН }
    : export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ ПАДЕЖ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 obj=ГлОбъект{ ПАДЕЖ:РОД } : export {
                                     @remove(ПАДЕЖ:ВИН)
                                     @remove(ПАДЕЖ:РОД)
                                     @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                    }
} : links
{
 v.<OBJECT>obj
}
: ngrams
{
 -3
 v_obj_score(v,obj)
}





// Второй инфинитив прикрепляется к корневому модальному инфинитиву:
//
// Я не буду пытаться удержать вас
//                    ^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ{ МОДАЛЬНЫЙ } : export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ ПЕРЕХОДНОСТЬ ПАДЕЖ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
 inf=Инф2
}
: links { v.<INFINITIVE>inf }
: ngrams { v_inf_score(v,inf) }






// Обстоятельство справа от глагольной части:
//
// Они будут работать завтра?
//                    ^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 adv=ОбстГлагСказ
} : links
{
 v.<ATTRIBUTE>adv
}
: ngrams
{
 adv_verb_score( adv, v )
}


// Рассчитывать-то может, но без гарантий.
//                      ^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 comma=','
 conj=союз:но{}
 pn=ПредлогИСущ{ гл_предл( v, _.prepos, _.n2 ) }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<PREPOS_ADJUNCT>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>pn }
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}




/*
// Прямое дополнение, которое пытается заполнить уже занятый или отсутствующий слот.
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 obj=ГлОбъект{ !=v:ПАДЕЖ } : export {
                                     @except(v:ПАДЕЖ,ПАДЕЖ)
                                     @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                    }
} : links
{
 v.<OBJECT>obj
}
: ngrams
{
 -11
 v_obj_score(v,obj)
}
*/

// разрешим присоединение родительного объекта к переходному
// инфинитиву с винительной валентностью для обработки конструкций
// с отрицанием модального глагола:
// я не могу сделать этого
//   ^^^^^^^ ^^^^^^  ^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН } : export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖ @remove(ПАДЕЖ:ВИН) @remove(ПАДЕЖ:РОД) ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
 obj=ГлОбъект{ ПАДЕЖ:РОД } : export {
                                     @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
                                    }
}
: links { v.<OBJECT>obj }
: ngrams
{
 -3
 v_obj_score(v,obj)
}



pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 attr=ОбособленныйАтрибут
} : links
{
 v.<SEPARATE_ATTR>attr
}


pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 attr=ВводнАктант
} : links
{
 v.<BEG_INTRO>attr
}

pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 attr=Детализатор
} : links
{
 v.<DETAILS>attr
}

// Начинайте, друзья, загружать провиант.
//          ^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 x=ПрямоеОбращение
} : links
{
 v.<APPEAL>x
}


pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 pn=ПредлогИСущ{ гл_предл( v, _.prepos, _.n2 ) }
} : links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}


// Одиночный висящий предлог.
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 p=предлог:*{}
}
: links { v.<ATTRIBUTE>p }
: ngrams { -6 }





pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
 q2=ЗакрывающаяКавычка
}
: links { v.<APPEND_QUOTE>q2 }
: ngrams { -1 }


// Второе сказуемое, присоединяемое через союз:
// Я буду работать и рад этому
//                 ^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
 conj=СочинитСоюзПредикат
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>v2 }


// Я буду учиться, затем буду работать
//               ^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
 comma=','
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>v2 }


// Он будет учиться, но не будет работать
//                 ^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфВосх_Сказ
{
 v=ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
 comma=','
 conj=СочинитСоюзПредикат
 v2=Сказуемое{ =v:РОД =v:ЧИСЛО =v:ЛИЦО }
 @noshift(ПравыйОграничительОборота)
}
: links { v.<RIGHT_LOGIC_ITEM>comma.<NEXT_COLLOCATION_ITEM>conj.<NEXT_COLLOCATION_ITEM>v2 }




// --------------------------------------------------------

patterns ГлНастИнфНисх_Сказ
export
{
 node:root_node
 ПЕРЕХОДНОСТЬ ПАДЕЖ ПАДЕЖВАЛ МОДАЛЬНЫЙ
 ЛИЦО ЧИСЛО РОД ВРЕМЯ НАКЛОНЕНИЕ
 AUX_VERB_REQUIRED
 KEYFEATURE_DETECTED
 KEYFEATURE_DESIRABLE
}

pattern ГлНастИнфНисх_Сказ
{
 ГлНастИнфВосх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
}

// я хочу это знать
// ^
pattern ГлНастИнфНисх_Сказ export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
{
 sbj=Подлежащее : export { ЛИЦО ЧИСЛО РОД }
 v=ГлНастИнфНисх_Сказ{ ПАДЕЖВАЛ:ИМ =sbj:ЛИЦО =sbj:ЧИСЛО =sbj:РОД } : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ AUX_VERB_REQUIRED НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
}
: links { v.<SUBJECT>sbj }
: ngrams { sbj_v_score(sbj,v) }


// Несогласованное подлежащее и сказуемое
pattern ГлНастИнфНисх_Сказ export { node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПЕРЕХОДНОСТЬ ПАДЕЖ (ПАДЕЖВАЛ) НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
{
 sbj=Подлежащее
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ ЛИЦО ЧИСЛО РОД ВРЕМЯ AUX_VERB_REQUIRED НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
}
: links { v.<SUBJECT>sbj }
: ngrams
{
 -10
 sbj_v_score(sbj,v)
}


// Модальный глагол стоит слева от инфинитива:
// Они будут петь
//     ^^^^^ ^^^^
pattern ГлНастИнфНисх_Сказ
{
 aux=ГлНастМодальн : export { ВРЕМЯ ЛИЦО ЧИСЛО РОД } 
 v=ГлНастИнфНисх_Сказ{ AUX_VERB_REQUIRED:1 =aux:ЛИЦО =aux:ЧИСЛО =aux:РОД } : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED:0 ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} : links
{
 v.<LEFT_AUX_VERB>aux
}


// Особо обрабатываем ситуацию с появлением РОДИТЕЛЬНОЙ валентности в случае,
// когда модальный глагол стоит в отрицательной форме:
//
// Вы не хотите ничего ломать.
//    ^^^^^^^^^^^^^^^^^^^^^^^
pattern ГлНастИнфНисх_Сказ
{
 ne=ЧАСТИЦА:НЕ{}
 aux=ГлНастМодальн : export { ВРЕМЯ ЛИЦО ЧИСЛО РОД } 
 v=ГлНастИнфНисх_Сказ{ AUX_VERB_REQUIRED:1 =aux:ЛИЦО =aux:ЧИСЛО =aux:РОД ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН }
  : export {
            node:root_node
            ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ
            ПАДЕЖ
            @remove(ПАДЕЖ:ВИН)
            @add(ПАДЕЖ:РОД)
            AUX_VERB_REQUIRED:0
            ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ
           }
} : links
{
 v.<LEFT_AUX_VERB>aux.<NEGATION_PARTICLE>ne
}




// Инфинитив, прикрепляемый к корневому модальному инфинитиву, может стоять слева, но
// это нетипичная конфигурация актантов и мы ее штрафуем:
//
// Читать книгу начинать я не буду
// ^^^^^^^^^^^^
pattern ГлНастИнфНисх_Сказ
{
 inf=Инф2
 v=ГлНастИнфНисх_Сказ{ МОДАЛЬНЫЙ } : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ:0 }
} 
: links { v.<INFINITIVE>inf }
: ngrams { v_inf_score(v,inf) }



// Теперь мы будем читать сказку
// ^^^^^^....
pattern ГлНастИнфНисх_Сказ
{
 adv=ОбстГлагСказ
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<ATTRIBUTE>adv
}
: ngrams
{
 adv_verb_score( adv, v )
}

// Тебе что, приходится экономить?
pattern ГлНастИнфНисх_Сказ
{
 adv=ЛевАтрибутСказуем
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links { v.<ATTRIBUTE>adv }
: ngrams { -1 }



pattern ГлНастИнфНисх_Сказ
{
 attr=ОбособленныйАтрибут
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<SEPARATE_ATTR>attr
}

pattern ГлНастИнфНисх_Сказ
{
 attr=ВводнАктант
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<BEG_INTRO>attr
}


pattern ГлНастИнфНисх_Сказ
{
 attr=Детализатор
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<DETAILS>attr
}


pattern ГлНастИнфНисх_Сказ
{
 x=ПрямоеОбращение
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<APPEAL>x
}

pattern ГлНастИнфНисх_Сказ
{
 pn=ПредлогИСущ
 v=ГлНастИнфНисх_Сказ{ гл_предл(_,pn.prepos,pn.n2) } : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE МОДАЛЬНЫЙ }
} 
: links
{
 v.<PREPOS_ADJUNCT>pn
}
: ngrams
{
 prepos_score( v, pn.prepos )
 ngram3( v, pn.prepos, pn.n2 )
}



pattern ГлНастИнфНисх_Сказ
{
 obj=ГлОбъект
 v=ГлНастИнфНисх_Сказ{ [-6]ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ [-6]=Obj:ПАДЕЖ }
     : export { 
               node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               МОДАЛЬНЫЙ
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
               KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE
              }
}
: links { v.<OBJECT>obj }
: ngrams
{
 -1
 v_obj_score( v, obj )
}

/*
pattern ГлНастИнфНисх_Сказ
{
 obj=ГлОбъект
 v=ГлНастИнфНисх_Сказ{ !=Obj:ПАДЕЖ }
     : export { 
               node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ
               @except(ПАДЕЖ,obj:ПАДЕЖ)
               МОДАЛЬНЫЙ
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
               KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE
              }
}
: links { v.<OBJECT>obj }
: ngrams
{
 -11
 v_obj_score( v, obj )
}
*/

// разрешим присоединение родительного объекта к переходному
// инфинитиву с винительной валентностью для обработки конструкций
// с отрицанием модального глагола:
// я не могу этого сделать
//   ^^^^^^^ ^^^^^ ^^^^^^^
pattern ГлНастИнфНисх_Сказ
{
 obj=ГлОбъект{ ПАДЕЖ:РОД }
 v=ГлНастИнфНисх_Сказ{ ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ ПАДЕЖ:ВИН }
     : export { 
               node:root_node AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ ПАДЕЖ НАКЛОНЕНИЕ
               @remove(ПАДЕЖ:ВИН)
               @remove(ПАДЕЖ:РОД)
               МОДАЛЬНЫЙ:0
               @if_exported(ПЕРЕХОДНОСТЬ:ПЕРЕХОДНЫЙ,ПАДЕЖ)
               KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE
              }
}
: links { v.<OBJECT>obj }
: ngrams
{
 -3
 v_obj_score( v, obj )
}




pattern ГлНастИнфНисх_Сказ
{
 q1=ОткрывающаяКавычка
 v=ГлНастИнфНисх_Сказ : export { node:root_node ПЕРЕХОДНОСТЬ ПАДЕЖ МОДАЛЬНЫЙ AUX_VERB_REQUIRED ЛИЦО ЧИСЛО РОД ВРЕМЯ ПАДЕЖВАЛ НАКЛОНЕНИЕ KEYFEATURE_DETECTED KEYFEATURE_DESIRABLE }
} 
: links { v.<PREPEND_QUOTE>q1 }
: ngrams { -1 }


// -------------------------------------------

patterns ГлНастСоюзн export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ НАКЛОНЕНИЕ AUX_VERB_REQUIRED ПАДЕЖВАЛ ВРЕМЯ }

// В составе сказуемого нет перечисления глаголов
pattern ГлНастСоюзн
{
 ГлНастИнфНисх_Сказ{ KEYFEATURE_DETECTED:0 } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ AUX_VERB_REQUIRED ПАДЕЖВАЛ }
}

// В составе сказуемого есть перечисление глаголов и использован союз.
pattern ГлНастСоюзн
{
 ГлНастИнфНисх_Сказ{ KEYFEATURE_DETECTED:1 KEYFEATURE_DESIRABLE:1 } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ AUX_VERB_REQUIRED ПАДЕЖВАЛ }
}

// Если инфинитивы перечисляются через запятую, и ни одного союза нет,
// то налагаем небольшой штраф.
pattern ГлНастСоюзн
{
 ГлНастИнфНисх_Сказ{ KEYFEATURE_DETECTED:1 KEYFEATURE_DESIRABLE:1 } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ AUX_VERB_REQUIRED ПАДЕЖВАЛ }
}
: ngrams { -1 }


// -------------------------------------------------

// Сказуемое без подлежащего
pattern ГлНаст
{
 v=ГлНастСоюзн{ AUX_VERB_REQUIRED:0 ПАДЕЖВАЛ:ИМ } : export { node:root_node ЛИЦО ЧИСЛО РОД ПЕРЕХОДНОСТЬ ПАДЕЖ ВРЕМЯ НАКЛОНЕНИЕ }
}
: ngrams
{
 ВалентностьГлагола(v)
}


// -------------------------------------------

pattern ГлПредикат
{
 v=ГлНастСоюзн{ AUX_VERB_REQUIRED:0 } : export { node:root_node }
}
: ngrams
{
 ВалентностьПредиката(v)
 ВалентностьГлагола(v)
}


// -------------------------------------------

