// Обороты, выступающие в роли обстоятельства времени (или продолжительности)

wordentry_set ДеньНедели =
{
 существительное:понедельник{},
 существительное:вторник{},
 существительное:среда{},
 существительное:четверг{},
 существительное:пятница{},
 существительное:суббота{},
 существительное:воскресенье{}
}


wordentry_set НазваниеВремениСуток = 
{
 существительное:утро{},
 существительное:день{},
 существительное:вечер{},
 существительное:ночь{}
}



wordentry_set СущСоЗначВрем = существительное:{
время,
момент,
день, // В День Воли белорусов призвали побороть страх и лень
//полдня,
минута,
минутка,
полминуты,
секунда, // секундой позже донесся шелест крыльев.
секундочка,
час, // часом позже они услышали эхо выстрелов.
полчаса, // Полчаса его бомбили.
часик,
мгновение, // мгновением позже голос перешел к дракону
столетие, // Столетием позже таким же образом переписали классическую электродинамику
вечность,
пора,
год,
полгода,
годик,
тысячелетие, // все было известно еще тысячелетия назад.
век, // четверть века вы трудились
десятилетие, // Этот кошмар продолжался целое десятилетие.
месяц
}

wordentry_set СущСоЗначВрем_Лет =
{
 СущСоЗначВрем,
 существительное:лето{}
}

patterns ЯдроНаречВрем export { node:root_node }



// спустя час за ним пришли.
// ^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:спустя{} : export { node:root_node }
 n=ЯдроНаречВрем
} : links { p.<OBJECT>n }
: ngrams { 4 }


/*
// Прикрыл на секунду глаза, вспоминая.
//         ^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:на{} : export { node:root_node }
 n=ЯдроНаречВрем
}
: links { p.<OBJECT>n }
: ngrams { 1 }
*/


// Уехали назад в 4 часа.
//              ^^^^^^^^
pattern ОбстФраза0
{
 mod=@optional(наречие:*{})
 p=предлог:в{} : export { node:root_node }
 c=ЧислоЦифрами
 n=Сущ0{ СущСоЗначВрем ПАДЕЖ:РОД }
} : links
{
 p.{ ~<ATTRIBUTE>mod <OBJECT>n.<ATTRIBUTE>c }
}


// К десяти часам улицы опустели.
// ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:к{} : export { node:root_node }
 n=ЧислСущ{ СущСоЗначВрем ПАДЕЖ:ДАТ }
} : links
{
 p.<OBJECT>n
}




// мы часиков эдак в 12 придём
//    ^^^^^^^^^^^^^^^^^
// Контпример:
// Начало концерта в 18 часов.
//        ~~~~~~~~~~~~~
pattern ОбстФраза0
{
 n=Сущ0{ ПАДЕЖ:РОД }
 mod=@optional(наречие:*{})
 p=предлог:*{} : export { node:root_node }
 c=ЧислоЦифрами
} : links
{
 p.{ ~<ATTRIBUTE>mod <OBJECT>n.<ATTRIBUTE>c }
}
: ngrams { -2 }



wordentry_set МодификаторПриблизит=наречие:{ почти, примерно, приблизительно, всего }

// --------------------------------------------------------------------

wordentry_set ПрефиксНаречДляВрем0=наречие:{
 "примерно", почти, лишь, "всего", "всего лишь", "всего-то", только, "буквально", "еще"
}

patterns ПрефиксНаречДляВрем export { node:root_node }

pattern ПрефиксНаречДляВрем
{
 ГруппаНареч1{ ПрефиксНаречДляВрем0 } : export { node:root_node }
}


// --------------------------------------------------------------------


// это произошло только спустя полчаса.
//               ^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p1=ПрефиксНаречДляВрем
 p=предлог:спустя{} : export { node:root_node }
 n=существительное:полчаса{}
} : links { p.{ <OBJECT>n <ATTRIBUTE>p1 } }
: ngrams { 5 }


// только спустя некоторое время пришла волна облегчения.
// ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p1=ПрефиксНаречДляВрем
 p=предлог:спустя{} : export { node:root_node }
 n=ЯдроНаречВрем
} : links { p.{ <OBJECT>n <ATTRIBUTE>p1 } }
: ngrams { 6 }


// час спустя за ним пришли.
// ^^^^^^^^^^
pattern ОбстФраза0
{
 n=ЯдроНаречВрем
 p=послелог:спустя{} : export { node:root_node }
} : links { p.<OBJECT>n }
: ngrams { 15 }


// целую вечность спустя полетели ракеты.
// ^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:*{ ПАДЕЖ:ВИН ЧИСЛО:ЕД СТЕПЕНЬ:АТРИБ }
 n=существительное:*{ СущСоЗначВрем ПАДЕЖ:ВИН ЧИСЛО:ЕД =a:РОД }
 p=послелог:спустя{} : export { node:root_node }
} : links { p.<OBJECT>n.<ATTRIBUTE>a }
: ngrams { 5 }


// спустя час за ним пришли.
//        ^^^
pattern ЯдроНаречВрем
{
 ИнтервалВремени{ падеж:вин число:ед } : export { node:root_node }
}

// Зеленая химия 20 лет спустя
//               ^^^^^^
// За два дня до этого умерла его секретарь.
//    ^^^
pattern ЯдроНаречВрем
{
 ЧислСущ{ ИнтервалВремени [-1]~ПАДЕЖ:ИМ } : export { node:root_node }
}


// отряд выехал из замка полчаса спустя.
//                       ^^^^^^^
pattern ЯдроНаречВрем
{
 'полчаса'{ падеж:вин } : export { node:root_node }
}


// спустя еще немного времени она услышала слабый грохот.
//        ^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a1=наречие:еще{}
 a2=наречие:немного{}
 n=существительное:время{ падеж:род число:ед } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a2.<ATTRIBUTE>a1 }





// три дня спустя перед нами возник таинственный остров.
// ^^^^^^^^
pattern ЯдроНаречВрем
{
 a=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }


// результат был получен спустя почти две минуты
//                              ^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 mod=МодификаторПриблизит
 a=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a.<ATTRIBUTE>mod }





// ее нашли минут пятнадцать спустя.
//          ^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
 a=числительное:*{ падеж:вин }
}
: links { n.<ATTRIBUTE>a }



// остановить собственную руку удалось лишь несколько секунд спустя.
//                                     ^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a0=ПрефиксНаречДляВрем
 a=наречие:несколько{}
 n=ИнтервалВремени{ падеж:род число:мн } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a.<ATTRIBUTE>a0 }


// результаты были получены лишь пять часов спустя
//                          ^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a0=ПрефиксНаречДляВрем
 a=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a.<ATTRIBUTE>a0 }


// результат был получен спустя две с половиной минуты
//                              ^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a=числительное:*{ падеж:вин }
 p1=предлог:*{}
 n1=существительное:*{ падеж:твор число:ед }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a.<PREPOS_ADJUNCT>p1.<OBJECT>n1 }


// результат был получен спустя примерно две с половиной минуты
//                              ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a0=ПрефиксНаречДляВрем
 a=числительное:*{ падеж:вин }
 p1=предлог:*{}
 n1=существительное:*{ падеж:твор число:ед }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a.{
                          <ATTRIBUTE>a0
                          <PREPOS_ADJUNCT>p1.<OBJECT>n1
                         }
        }


// результат был получен спустя две или три минуты
//                              ^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 c1=числительное:*{ падеж:вин }
 conj=союз:или{}
 c2=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links { n.<ATTRIBUTE>c1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>c2 }


// результат был получен почти год спустя
//                       ^^^^^^^^^
pattern ЯдроНаречВрем
{
 a0=ПрефиксНаречДляВрем
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
} : links { n.<ATTRIBUTE>a0 }


// это произошло спустя некоторое время
//                      ^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a=прилагательное:*{ падеж:вин ~краткий степень:атриб род:ср число:ед }
 n=существительное:время{ падеж:вин } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }


// встретились они спустя четверть часа.
pattern ЯдроНаречВрем
{
 n1=существительное:четверть{} : export { node:root_node }
 n2=ИнтервалВремени{ падеж:род }
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }


// спустя немногим более двух часов земля показалась знакомой.
//        ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 w1='немногим'
 w2='более'
 w3=числительное:*{ /*падеж:РОД*/ }
 w4=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
}
: links
{
 w4.<ATTRIBUTE>w3.<ATTRIBUTE>w1.<ATTRIBUTE>w2
}


// столько лет спустя она была очень красивой женщиной.
// ^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a=наречие:столько{}
 n=ГруппаСущ1{ ИнтервалВремени ~падеж:им } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }


// секунду или две спустя все было кончено.
pattern ЯдроНаречВрем
{
 w1=ИнтервалВремени{ падеж:вин число:ед } : export { node:root_node }
 conj=союз:или{}
 w2=числительное:*{ падеж:ВИН }
}
: links
{
 w1.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>w2
}


// сто девятнадцать часов спустя вещество распадется
// ^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 ЧислСущ{ ИнтервалВремени } : export { node:root_node }
}

// примерно шестьдесят восемь секунд спустя аккумуляторы разрядились
pattern ЯдроНаречВрем
{
 p=ПрефиксНаречДляВрем
 n=ЧислСущ{ ИнтервалВремени } : export { node:root_node }
} : links { n.<ATTRIBUTE>p }





// --------------------------

// смеялась она лишь мгновение
//              ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 n=ИнтервалВремени{ ПАДЕЖ:ВИН ЧИСЛО:ЕД } : export { node:root_node }
} : links { n.<ATTRIBUTE>p }


// Джим колебался лишь одно мгновение.
//                ^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 c=числительное:один{ падеж:вин число:ед }
 n=ИнтервалВремени { падеж:вин число:ед =c:РОД } : export { node:root_node }
} : links { n.<ATTRIBUTE>c.<ATTRIBUTE>p }


// Джим колебался лишь две секунды
//                ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 n=ЧислСущ{ ИнтервалВремени } : export { node:root_node }
}
: links { n.<ATTRIBUTE>p }
: ngrams { 4 }



// ------------------------------------------------------

patterns ЯдроНаречВрем2 export { node:root_node }

wordentry_set НазадТомуНазад={ послелог:назад{}, послелог:тому назад{} }

// видел его сегодня три часа тому назад.
//                   ^^^^^^^^^^^^^^^^^^^
// дочь потеряла зрение два года назад.
//                      ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=ЯдроНаречВрем2
 p=НазадТомуНазад : export { node:root_node }
} : links { p.<OBJECT>n }
: ngrams { 10 }


// полчаса назад
// ^^^^^^^^^^^^^
// им следовало подняться сюда полчаса тому назад.
//                             ^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n='полчаса'{ падеж:вин }
 p=НазадТомуНазад : export { node:root_node }
} : links { p.<OBJECT>n }
: ngrams { 5 }


// все было известно еще тысячелетия назад.
//                       ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=СущСоЗначВрем{ падеж:вин число:мн }
 p=НазадТомуНазад : export { node:root_node }
} : links { p.<OBJECT>n }
: ngrams { 8 }



// а всего миг назад была зима
//   ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=ПрефиксНаречДляВрем
 n=ЯдроНаречВрем2
 p=НазадТомуНазад : export { node:root_node }
} : links { p.<OBJECT>n.<ATTRIBUTE>mod }
: ngrams { 9 }


// два года тому назад брат погиб.
// ^^^^^^^^
pattern ЯдроНаречВрем2
{
 ЧислСущ{ ИнтервалВремени ПАДЕЖ:ВИН } : export { node:root_node }
}


wordentry_set СчетноеСлово=
{
 наречие:много{},
 наречие:несколько{},
 наречие:сколько{},
 наречие:сколько-то{}
}

// несколько мгновений спустя пришел контакт.
// ^^^^^^^^^^^^^^^^^^^
// вы вызывали его несколько минут тому назад.
//                 ^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем
{
 a=ГруппаНареч1{ СчетноеСлово }
 n=ИнтервалВремени{ падеж:род число:мн } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }



// две полицейские машины прибыли сюда минут сорок назад.
//                                     ^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
 a=числительное:*{ падеж:вин }
}
: links { n.<ATTRIBUTE>a }


// последние люди покинули эту местность около четырех лет назад.
//                                       ^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 p=предлог:около{} : export { node:root_node }
 n=ЧислСущ{ ИнтервалВремени падеж:род }
} : links { p.<OBJECT>n }
  : ngrams { 10 }


wordform_set НаречиеБолееВремя={
'более'{ class:НАРЕЧИЕ },
'больше'{ class:НАРЕЧИЕ },
'менее'{ class:НАРЕЧИЕ },
'меньше'{ class:НАРЕЧИЕ }
}

// это было больше тридцати лет назад
//          ^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 a=НаречиеБолееВремя : export { node:root_node }
 n=ЧислСущ{ ИнтервалВремени падеж:род }
} : links { a.<OBJECT>n }
  : ngrams { 10 }

// это было немногим больше трех недель назад
//          ^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 mod='немногим'
 a=НаречиеБолееВремя : export { node:root_node }
 n=ЧислСущ{ ИнтервалВремени падеж:род }
} : links {
           a.{
              <ATTRIBUTE>mod
              <OBJECT>n
             } }
: ngrams { 15 }


// это было немногим больше недели назад
//          ^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 mod='немногим'
 a=НаречиеБолееВремя : export { node:root_node }
 n=ИнтервалВремени{ число:ед падеж:род }
} : links {
           a.{
              <ATTRIBUTE>mod
              <OBJECT>n
             } }
: ngrams { 5 }

// приблизительно семь лет назад.
// ^^^^^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 mod=МодификаторПриблизит
 n=ЧислСущ{ ИнтервалВремени } : export { node:root_node }
} : links { n.<ATTRIBUTE>mod }




// примерно лет семь назад.
// ^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 mod=МодификаторПриблизит
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
 a=числительное:*{ падеж:вин }
} : links { n.<ATTRIBUTE>a.<ATTRIBUTE>mod }


// более ста лет назад.
// ^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 mod='более'{ class:наречие }
 a=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
} : links { n.<ATTRIBUTE>a.<ATTRIBUTE>mod }


// вы видели его некоторое время назад.
//               ^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 a=прилагательное:*{ падеж:вин число:ед род:ср }
 n=существительное:время{ падеж:вин число:ед } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }


wordentry_set ПолВремени=существительное:{ полгода, полчаса, полминуты }

// неужели это случилось всего полгода назад?
//                             ^^^^^^^
pattern ЯдроНаречВрем2
{
 n=существительное:*{ ПолВремени падеж:вин } : export { node:root_node }
}


// лет этак двести назад.
// ^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 n=ИнтервалВремени{ ~падеж:им } : export { node:root_node }
 x=наречие:этак{}
 a=числительное:*{ падеж:вин }
} : links { n.<ATTRIBUTE>a.<ATTRIBUTE>x }


// это написано полтора месяца назад.
//              ^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 w1='полтора'
 n=ИнтервалВремени{ падеж:род число:ед } : export { node:root_node }
} : links { n.<ATTRIBUTE>w1 }


// месяц назад началась война.
// ^^^^^
pattern ЯдроНаречВрем2
{
 ИнтервалВремени{ падеж:вин число:ед } : export { node:root_node }
}

wordentry_set СчетноеДляВремени=наречие:{ много, несколько, сколько }

// много веков назад.
// ^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 a=СчетноеДляВремени
 n=ИнтервалВремени{ падеж:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }


wordentry_set СущДоля=существительное:{ четверть, треть }

// все это произошло четверть дня назад
//                   ^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 n=СущДоля{ падеж:вин } : export { node:root_node }
 n2=ИнтервалВремени{ падеж:род число:ед }
} : links { n.<RIGHT_GENITIVE_OBJECT>n2 }


// всего несколько дней назад.
pattern ЯдроНаречВрем2
{
 a1=наречие:всего{}
 a=наречие:несколько{}
 n=ИнтервалВремени{ падеж:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a.<ATTRIBUTE>a1 }
  : ngrams { 14 }


// еще год назад.
// ^^^^^^^
pattern ЯдроНаречВрем2
{
 a=наречие:еще{}
 n=ИнтервалВремени{ падеж:вин } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
  : ngrams { 1 }

// еще пять минут назад.
// ^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 a=наречие:еще{}
 n=ЧислСущ{ ИнтервалВремени } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
  : ngrams { 1 }

// еще несколько минут назад.
// ^^^^^^^^^^^^^^^^^^^
pattern ЯдроНаречВрем2
{
 a1=наречие:еще{}
 a=наречие:несколько{}
 n=ИнтервалВремени{ падеж:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a.<ATTRIBUTE>a1 }
  : ngrams { 14 }


// ---------------------------------

patterns Каждый_Чтото export { node:root_node род число }
pattern Каждый_Чтото
{
 прилагательное:каждый{ падеж:вин } : export { число род node:root_node }
}

// с тех пор они встречались почти каждый день.
//                           ^^^^^^^^^^^^
pattern Каждый_Чтото
{
 mod=наречие:почти{}
 a=прилагательное:каждый{ падеж:вин } : export { число род node:root_node }
} : links { a.<ATTRIBUTE>mod }
 : ngrams { 1 }


// часовые каждое мгновение могут вернуться.
//         ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=Каждый_Чтото{ число:ед }
 n=ИнтервалВремени{ падеж:вин =a:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
: ngrams { 2 }


// Он каждое утро учит новые слова.
//    ^^^^^^^^^^^
pattern ОбстФраза0
{
 a=Каждый_Чтото{ число:ед }
 n=ЧастьСуток{ падеж:вин =a:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
: ngrams { 2 }

// Антонио ел пюре каждую среду
//                 ^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=Каждый_Чтото{ число:ед }
 n=ДеньНедели{ падеж:вин =a:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
: ngrams { 2 }

// Антонио ел пюре каждый вторник и пятницу
//                 ^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=Каждый_Чтото{ число:ед }
 n1=ДеньНедели{ падеж:вин =a:род } : export { node:root_node }
 conj=союз:и{}
 n2=ДеньНедели{ падеж:вин }
} : links
{
 n1.{
     <ATTRIBUTE>a 
     <RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2
    }
}
: ngrams { 4 }

// Антонио ел пюре каждые вторник и пятницу
//                 ^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=Каждый_Чтото{ число:мн }
 n1=ДеньНедели{ падеж:вин } : export { node:root_node }
 conj=союз:и{}
 n2=ДеньНедели{ падеж:вин }
} : links
{
 n1.{
     <ATTRIBUTE>a 
     <RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>n2
    }
}
: ngrams { 4 }


// ----------------------------------

// Антонио ел пюре век за веком
//                 ^^^^^^^^^^^^
pattern ОбстФраза0
{
 n1=ИнтервалВремени{ падеж:вин } : export { node:root_node }
 p=предлог:за{}
 n2=ИнтервалВремени{ падеж:твор }
} : links { n1.<PREPOS_ADJUNCT>p.<NEXT_COLLOCATION_ITEM>n2 }
  : ngrams { 1 }


// -----------------------------------

// ==================================
// НАРЕЧНЫЕ ОБОРОТЫ СО СЛОВОМ "РАЗ"
// ==================================


// потом вздохнул еще раз.
//                ^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 n=существительное:раз{ падеж:вин } : export { node:root_node }
}
: links { n.<ATTRIBUTE>p }
: ngrams { 5 }

// он выстрелил еще несколько раз
//              ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 c=наречие:несколько{}
 n=существительное:раз{ падеж:род } : export { node:root_node }
}
: links { n.<ATTRIBUTE>c.<ATTRIBUTE>p }
: ngrams { 12 }


// он выстрелил несколько раз
//              ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 c=наречие:несколько{}
 n=существительное:раз{ падеж:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>c }
: ngrams { 10 }

// она пробовала еще много раз
//               ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 c=наречие:много{}
 n=существительное:раз{ падеж:род } : export { node:root_node }
}
: links { n.<ATTRIBUTE>c.<ATTRIBUTE>p }
: ngrams { 10 }


wordentry_set СловоРаз={ существительное:раз{} }

// она попробовала еще три раза
//                 ^^^^^^^^^^^^
pattern ОбстФраза0
{
 c=ПрефиксНаречДляВрем
 n=ЧислСущ{ СловоРаз падеж:вин } : export { node:root_node }
} : links { n.<ATTRIBUTE>c }
: ngrams { 10 }

// они попробовали еще пару раз
//                 ^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=ПрефиксНаречДляВрем
 c='пару'{ падеж:вин } : export { node:root_node }
 n=существительное:раз{ падеж:род }
} : links { c.{ <ATTRIBUTE>p <RIGHT_GENITIVE_OBJECT>n } }
: ngrams { 8 }

// они попробовали пару раз
//                 ^^^^^^^^
pattern ОбстФраза0
{
 p='пару'{ падеж:вин } : export { node:root_node }
 n=существительное:раз{ падеж:род }
} : links { p.<RIGHT_GENITIVE_OBJECT>n }
: ngrams { 5 }

// он проверял расчет много раз
//                    ^^^^^^^^^
pattern ОбстФраза0
{
 p=наречие:много{} : export { node:root_node }
 n=существительное:раз{ падеж:род }
} : links { p.<RIGHT_GENITIVE_OBJECT>n }
: ngrams { 5 }


// он проверял расчет шесть раз
//                    ^^^^^^^^^
pattern ОбстФраза0
{
 ЧислСущ{ СловоРаз падеж:вин } : export { node:root_node }
}
: ngrams { 5 }

// он проверял расчет раз двадцать
//                    ^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=существительное:раз{ падеж:род } : export { node:root_node }
 c=числительное:*{ падеж:вин }
} : links { n.<RIGHT_GENITIVE_OBJECT>c }
: ngrams { 5 }

// он проверял расчет раз этак двадцать
//                    ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=существительное:раз{ падеж:род } : export { node:root_node }
 w='этак'
 c=числительное:*{ падеж:вин }
} : links { n.<RIGHT_GENITIVE_OBJECT>c.<ATTRIBUTE>w }
: ngrams { 6 }


// скажи это хотя бы один только раз.
//                   ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 c=числительное:*{ падеж:вин }
 a=наречие:только{} : export { node:root_node }
 n=существительное:раз{ падеж:вин }
} : links { a.<NEXT_COLLOCATION_ITEM>n.<RIGHT_GENITIVE_OBJECT>c }
: ngrams { 5 }

// мы спорили уже много раз.
//            ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=наречие:уже{}
 a=наречие:много{} : export { node:root_node }
 n=существительное:раз{ падеж:вин }
} : links {
 a.{
    <ATTRIBUTE>mod
    <RIGHT_GENITIVE_OBJECT>n
   }
}
: ngrams { 6 }


// ты уже прошел его так много раз.
//                   ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=наречие:так{}
 a=наречие:много{} : export { node:root_node }
 n=существительное:раз{ падеж:вин }
} : links { a.{ <RIGHT_GENITIVE_OBJECT>n <ATTRIBUTE>mod } }
: ngrams { 15 }


// В Абакане площадь городского пляжа увеличится в 2 раза
//                                               ^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:в{} : export { node:root_node }
 n=ЧислСущ{ СловоРаз падеж:вин }
}
: links { p.<OBJECT>n }
: ngrams { 6 }


wordentry_set СчетнСущДляНареч = {
                                  существительное:день{},
                                  существительное:ночь{},
                                  существительное:сутки{},
                                  существительное:месяц{},
                                  существительное:неделя{},
                                  существительное:квартал{},
                                  существительное:лето{},
                                  существительное:секунда{},
                                  существительное:минута{},
                                  существительное:час{},
                                  существительное:раз{},
                                  существительное:год{}
                                 }



// Антонио опять ловит мышь несколько ночей подряд
//                          ^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a1=наречие:несколько{} 
 n=СчетнСущДляНареч{ падеж:род }
 a2=наречие:подряд{}:  export { node:root_node }
} : links { a2.<NEXT_COLLOCATION_ITEM>n.<ATTRIBUTE>a1 }
: ngrams { 3 }



// через полчаса наша машина была в пути
// ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:через{} : export { node:root_node }
 n=ИнтервалВремени{ падеж:вин }
} : links { p.<OBJECT>n }
: ngrams { 3 }





// ********************************************
// словосочетания, играющие роль наречий
// ********************************************
wordentry_set НаречиеКакЧисло = { наречие:несколько{}, наречие:много{}, наречие:сколько{}, наречие:много-много{} }
wordentry_set КакНаречиеПодряд = { наречие:подряд{}, послелог:назад{}, послелог:"тому назад"{} }

wordentry_set МодифНаречКакЧисло=наречие:{
лишь, // для этого потребовалось бы лишь несколько минут.
уже
}

// уже много лет
pattern ОбстФраза0
{
 mod1=МодифНаречКакЧисло
 a=НаречиеКакЧисло : export { node:root_node }
 n=СчетнСущДляНареч{ падеж:род число:мн }
} : links
{
 a.{
    <ATTRIBUTE>mod1
    <OBJECT>n
   }
} 
: ngrams { 12 }

// уже много лет подряд
pattern ОбстФраза0
{
 mod1=МодифНаречКакЧисло
 a=НаречиеКакЧисло
 n=СчетнСущДляНареч{ падеж:род число:мн }
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.<OBJECT>a.{
                 <ATTRIBUTE>mod1
                 <OBJECT>n
                }
} 
: ngrams { 12 }



// много лет подряд
// ^^^^^^^^^^^^^^^^
//
// именно этого взгляда ждала она много лет
//                                ^^^^^^^^^
pattern ОбстФраза0
{
 a=НаречиеКакЧисло
 n=СчетнСущДляНареч{ падеж:род число:мн } : export { node:root_node }
} : links { n.<ATTRIBUTE>a } 
: ngrams { 10 }


wordentry_set КвантификаторЧисла=наречие:{ всего, только, лишь }


// вы вызывали его несколько минут тому назад.
pattern ОбстФраза0
{
 a=НаречиеКакЧисло
 n=СчетнСущДляНареч{ падеж:род число:мн }
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.<OBJECT>n.<ATTRIBUTE>a
} 
: ngrams { 11 }



wordentry_set МодифДляСчетнНареч=наречие:{
всего, // оставалось всего несколько минут.
лишь // осталось лишь несколько секунд.
}

pattern ОбстФраза0
{
 mod=МодифДляСчетнНареч
 a=НаречиеКакЧисло : export { node:root_node }
 n=СчетнСущДляНареч{ падеж:род число:мн }
} : links
{
 a.{
    <ATTRIBUTE>mod
    <OBJECT>n
   }
} 
: ngrams { 14 }


// слишком много лет прошло
// ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=МодифДляМного
 a=наречие:много{} : export { node:root_node }
 n=СчетнСущДляНареч{ падеж:род число:мн }
} : links
{
 a.{
    <ATTRIBUTE>mod
    <OBJECT>n
   }
} 
: ngrams { 12 }



// много раз подряд
pattern ОбстФраза0
{
 mod1=@optional(наречие:уже{})
 a=НаречиеКакЧисло : export { node:root_node }
 n=существительное:раз{ падеж:род число:мн }
} : links
{
 a.{
    ~<ATTRIBUTE>mod1
    <OBJECT>n
   }
}
: ngrams { 12 }


// В Московской области стадо сократилось в несколько  раз.
//                                        ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:в{} : export { node:root_node }
 mod=НаречиеКакЧисло
 n=существительное:раз{ падеж:род }
}
: links { p.<OBJECT>n.<ATTRIBUTE>mod }
: ngrams { 13 }



// много раз подряд
pattern ОбстФраза0
{
 mod1=@optional(наречие:уже{})
 a=НаречиеКакЧисло
 n=существительное:раз{ падеж:род число:мн }
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.<OBJECT>a.{
                 ~<ATTRIBUTE>mod1
                 <OBJECT>n
                }
}
: ngrams { 13 }

// несколько раз в течении прошедших суток
pattern ОбстФраза0
{
 mod=@optional(наречие:уже{})
 a=НаречиеКакЧисло : export { node:root_node }
 n=существительное:раз{ падеж:род число:мн }
 adjunct=ПредлогИСущ
} : links
{
 a.{
    ~<ATTRIBUTE>mod
    <OBJECT>n
    <PREPOS_ADJUNCT>adjunct
   }
}
: ngrams { 3 }

wordentry_set МодифДляЧисл=наречие:{ уже, ровно, точно, почти /*, 'почти что'*/ }

// 5 лет подряд
pattern ОбстФраза0
{
 mod1=@optional(МодифДляЧисл)
 c=num_word:*{}
 n=СчетнСущДляНареч{ [-1]падеж:род } : export { node:root_node }
} : links
{
 n.{
    ~<ATTRIBUTE>mod1
    <ATTRIBUTE>c
   }
}
: ngrams { 4 }


// Женщина минут 15 сидела и плакала.
//         ^^^^^^^^
pattern ОбстФраза0
{
 mod1=@optional(МодифДляЧисл)
 n=СчетнСущДляНареч : export { node:root_node }
 c=num_word:*{}
} : links
{
 n.{
    ~<ATTRIBUTE>mod1
    <ATTRIBUTE>c
   }
}
: ngrams { 4 }


// 5 лет подряд
pattern ОбстФраза0
{
 mod1=@optional(МодифДляЧисл)
 c=num_word:*{}
 n=СчетнСущДляНареч
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.<OBJECT>n.{
                 ~<ATTRIBUTE>mod1
                 <ATTRIBUTE>c
                }
}
: ngrams { 5 }


// пятнадцать лет подряд
pattern ОбстФраза0
{
 mod1=@optional(МодифДляЧисл)
 c=числительное:*{ падеж:вин }
 n=СчетнСущДляНареч
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.<OBJECT>n.<ATTRIBUTE>c.~<ATTRIBUTE>mod1
}
: ngrams { 4 }



// ведь эти люди знали меня одиннадцать лет
//                          ^^^^^^^^^^^^^^^
// В столице Сибири министр обороны пробудет два дня.
//                                           ^^^^^^^
pattern ОбстФраза0
{
 c=числительное:*{ падеж:вин }
 n=СчетнСущДляНареч{ [-1]=c:РОД } : export { node:root_node }
} : links
{
 n.<ATTRIBUTE>c
}
: ngrams { 3 }


// Говорили часа полтора.
//          ^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=СчетнСущДляНареч{} : export { node:root_node }
 c=числительное:*{  [-1]=c:РОД падеж:вин }
} : links
{
 n.<ATTRIBUTE>c
}
: ngrams { 3 }



// ведь эти люди знали меня ровно одиннадцать лет
//                          ^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod1=МодифДляЧисл
 c=числительное:*{ падеж:вин }
 n=СчетнСущДляНареч{ [-1]=c:РОД } : export { node:root_node }
} : links
{
 n.<ATTRIBUTE>c.~<ATTRIBUTE>mod1
}
: ngrams { 4 }





// пару уроков подряд
pattern ОбстФраза0
{
 mod1=@optional(МодифДляЧисл)
 c='пару'{ падеж:вин }
 n=существительное:*{ падеж:род число:мн }
 mod2=КакНаречиеПодряд : export { node:root_node }
} : links
{
 mod2.{
       ~<ATTRIBUTE>mod1
       <OBJECT>c.<RIGHT_GENITIVE_OBJECT>n
      }
}
: ngrams { 2 }


// -------------------------------------

// где ты был столько времени?
//            ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=наречие:столько{} : export { node:root_node }
 n=существительное:время{ падеж:род число:ед }
} : links { a.<RIGHT_GENITIVE_OBJECT>n }
: ngrams { 1 }

// --------------------------------------

// уже до обеда
pattern ОбстФраза0
{
 a=наречие:уже{}
 p=предлог:до{} : export { node:root_node }
 n=существительное:*{ одуш:неодуш падеж:род число:ед }
} : links
{
 p.{
    <RIGHT_GENITIVE_OBJECT>n
    <ATTRIBUTE>a
   }
}

// уже после обеда
pattern ОбстФраза0
{
 a=наречие:уже{}
 p=предлог:после{} : export { node:root_node }
 n=существительное:*{ одуш:неодуш падеж:род число:ед }
} : links
{
 p.{
    <RIGHT_GENITIVE_OBJECT>n
    <ATTRIBUTE>a
   }
}

// Еще до обеда Антонио получил задание
// ^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=наречие:еще{}
 p=предлог:до{} : export { node:root_node }
 n=существительное:*{ одуш:неодуш падеж:род число:ед }
} : links
{
 p.{
    <RIGHT_GENITIVE_OBJECT>n
    <ATTRIBUTE>a
   }
}


// некоторое время девушки бежали молча.
// ^^^^^^^^^^^^^^^
// Он всё время торопит меня
//    ^^^^^^^^^
/*
wordentry_set АтрибутВремениДляОбст=прилагательное:{ некоторый, долгий, "какой-то", "весь" }
pattern ОбстФраза0
{
 a=прилагательное:*{ АтрибутВремениДляОбст падеж:вин род:ср число:ед степень:атриб ~краткий }
 n=существительное:время{ падеж:вин число:ед } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
*/

// вы долгое время были вдали от мира.
//    ^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:*{ падеж:вин ~краткий степень:атриб род:ср число:ед }
 n=существительное:время{ падеж:вин } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams { СущКакОбстВремени(n) }



// чем ты занимался все это время?
//                  ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a0=прилагательное:весь{ падеж:вин число:ед род:ср }
 a=прилагательное:*{ падеж:вин ~краткий степень:атриб род:ср число:ед }
 n=существительное:время{ падеж:вин } : export { node:root_node }
} : links
{
 n.{
    <ATTRIBUTE>a0
    <ATTRIBUTE>a
   }
}
: ngrams
{
 СущКакОбстВремени(n)
}




// а вот мучить можно целую жизнь
//                    ^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:целый{ падеж:вин ~краткий степень:атриб род:жен число:ед }
 n=существительное:жизнь{ падеж:вин } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }

wordentry_set МодификаторВремени = наречие:{ лишь, уже, только }

// Антоша затих лишь к утру.
//              ^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=МодификаторВремени
 p=предлог:к{} : export { node:root_node }
 n=ЧастьСуток{ падеж:дат число:ед }
}
: links
{
 p.{
    <OBJECT>n
    <ATTRIBUTE>adv
   }
}
: ngrams { 1 }


// Антоша затих к утру.
//              ^^^^^^
pattern ОбстФраза0
{
 p=предлог:к{} : export { node:root_node }
 n=ЧастьСуток{ падеж:дат число:ед }
} : links { p.<OBJECT>n }
: ngrams { 1 }


// уже под утро камень наконец вышел.
// ^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=МодификаторВремени
 p=предлог:под{} : export { node:root_node }
 n=существительное:утро{ падеж:вин число:ед }
}
: links
{
 p.{
    <OBJECT>n
    <ATTRIBUTE>adv
   }
}
: ngrams { 2 }

// Собака затихла только под вечер
//                ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=МодификаторВремени
 p=предлог:под{} : export { node:root_node }
 n=существительное:вечер{ падеж:вин число:ед }
}
: links
{
 p.{
    <OBJECT>n
    <ATTRIBUTE>adv
   }
}
: ngrams { 2 }


// хочешь пойти туда прямо сейчас?
//                   ^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv1=наречие:прямо{}
 adv2=наречие:сейчас{} : export { node:root_node }
}
: links { adv2.<ATTRIBUTE>adv1 }
: ngrams { 1 }


// земли давно уже нет!
//       ^^^^^^^^^
pattern ОбстФраза0
{
 adv1=наречие:давно{} : export { node:root_node }
 adv2=наречие:уже{}
}
: links { adv1.<ATTRIBUTE>adv2 }
: ngrams { 1 }


// Его целый год держали в тюрьме.
//     ^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:целый{ падеж:вин число:ед }
 n=СущСоЗначВрем{ =a:род падеж:вин число:ед } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams { 2 }

// Четверть века вы трудились
// ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n1=существительное:четверть{ падеж:вин число:ед } : export { node:root_node }
 n2=СущСоЗначВрем{ падеж:род число:ед }
}
: links { n1.<RIGHT_GENITIVE_OBJECT>n2 }
: ngrams { 2 }


wordentry_set ПрилагДляДнейНедел = 
{
 прилагательное:весь{},
 прилагательное:каждый{}
}
 
pattern ОбстФраза0
{
 A=ПрилагДляДнейНедел{падеж:им}
 n=ДеньНедели{ =A:число =A:род падеж:им }:export { node:root_node }
} : links { n.<ATTRIBUTE>a }

wordentry_set ВремяГода =
{
 существительное:лето{},
 существительное:зима{},
 существительное:осень{},
 существительное:весна{}
}

wordentry_set ПрилДляВремениГода =
{
 прилагательное:этот{},
 прилагательное:тот{},
 прилагательное:поздний{},
 прилагательное:прошлый{},
 прилагательное:будущий{},
 прилагательное:нынешний{}
}


// этим летом
pattern ОбстФраза0
{
 a=прилагательное:*{ /*ПрилДляВремениГода*/ ПАДЕЖ:ТВОР ЧИСЛО:ЕД }
 n=ВремяГода{ ПАДЕЖ:ТВОР ЧИСЛО:ЕД =A:РОД }:export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams
{
 2
 СущКакОбстВремени(n)
}


// Вечером Зина зашла в избу партизанского госпиталя к сестренке.
// ^^^^^^^
// она проснулась глубокой ночью
//                ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=ГруппаСущ4{ НазваниеВремениСуток ПАДЕЖ:ТВОР }:export { node:root_node }
}
: ngrams
{
 2
 СущКакОбстВремени(n)
}





// а мне целыми днями приходится терпеть
//       ^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:*{ /*ПрилДляВремениГода*/ ПАДЕЖ:ТВОР ЧИСЛО:ЕД }
 n=СущСоЗначВрем{ ПАДЕЖ:ТВОР ЧИСЛО:ЕД =A:РОД }:export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams
{
 1
 adj_noun_score(a,n)
 СущКакОбстВремени(n)
}



wordentry_set НеМодифЧерезКак=наречие:{ чай }

// меня как будто ударило!
//      ^^^^^^^^^
pattern Обст
{
 a1=наречие:как{}
 a2=наречие:*{ степень:атриб ~НеМодифЧерезКак } : export { node:root_node }
} : links { a2.<ATTRIBUTE>a1 }
  : ngrams { 5 }

// вот как сильно она мне нравится
// ^^^^^^^
pattern Обст
{
 p=частица:вот{}
 a1=наречие:как{}
 a2=наречие:*{ степень:атриб ~НеМодифЧерезКак } : export { node:root_node }
} : links
{
 a2.{
     <PREFIX_PARTICLE>p
     <ATTRIBUTE>a1
    }
}
  : ngrams { 5 }


// трое суток
// ^^^^^^^^^^
pattern ОбстФраза0
{
 c=ЧИСЛИТЕЛЬНОЕ:*{ ПАДЕЖ:ИМ ЧИСЛО:МН КАТЕГОРИЯ_ЧИСЛ:СОБИР } : export { node:root_node }
 n=существительное:сутки{ ПАДЕЖ:РОД }
} : links { c.<RIGHT_GENITIVE_OBJECT>n }
  : ngrams { 5 }


// мне надо было продержаться еще трое суток
//                            ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=ПрефиксНаречДляВрем
 c=ЧИСЛИТЕЛЬНОЕ:*{ ПАДЕЖ:ИМ ЧИСЛО:МН КАТЕГОРИЯ_ЧИСЛ:СОБИР } : export { node:root_node }
 n=существительное:сутки{ ПАДЕЖ:РОД }
} : links { c.{
               <RIGHT_GENITIVE_OBJECT>n
               <ATTRIBUTE>mod
              }
          }
  : ngrams { 6 }


// это заняло около десяти лет
pattern ОбстФраза0
{
 mod=предлог:около{} : export { node:root_node }
 a=числительное:*{ падеж:род }
 n=ИнтервалВремени{ ~падеж:им }
}
: links { mod.<OBJECT>n.<ATTRIBUTE>a }
: ngrams
{
 5
 СущКакОбстВремени(n)
}


// прошло всего около десяти секунд.
//        ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=наречие:всего{}
 mod=предлог:около{} : export { node:root_node }
 a=числительное:*{ падеж:род }
 n=ИнтервалВремени{ ~падеж:им }
}
: links
{
 mod.{
      <ATTRIBUTE>adv
      <OBJECT>n.<ATTRIBUTE>a
     }
}
: ngrams
{
 5
 СущКакОбстВремени(n)
}

// -------------------------------------------------

// Наречие времени:
// В 9:15 раздался школьный звонок
// ^^^^^^
// Избирательные участки работают до 20:00.
//                                ^^^^^^^^
pattern ОбстФраза0
{
 prep=предлог:*{} : export { node:root_node }
 d=@regex("[[:digit:]]{1,2}:[[:digit:]]{1,2}")
}
: links { prep.<OBJECT>d }


// -------------------------------------------

// Голосование завершится в 20:00 мск.
//                        ^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 t=@regex("[0-9]{1,2}:[0-9]{2}")
 z='мск'
} : links { p.<OBJECT>t.<ATTRIBUTE>z }


// -------------------------------------------------

// В 23 часа 00 минут пожар был полностью потушен.
// ^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 h=@regex("[0-9]{1,2}")
 nh=существительное:час{}
 m=@regex("[0-9]{1,2}")
 nm=существительное:минута{}
} : links
{
 p.<OBJECT>nh.{
               <ATTRIBUTE>h
               <NEXT_COLLOCATION_ITEM>nm.<ATTRIBUTE>m
              }
}
: ngrams { 5 }


// -------------------------------------------------

// окончил в 1942 г. среднюю школу с золотой медалью
//         ^^^^^^^^^
pattern ОбстФраза0
{
 prep=предлог:в{} : export { node:root_node }
 d=@regex("[[:digit:]]{2,4}")
 y='г.'
}
: links { prep.<OBJECT>d.<NEXT_COLLOCATION_ITEM>y }
: ngrams { ВалентностьПредлога(prep) }


// Королевский замок в Кенигсберге был возведен в XIII веке
//                                              ^^^^^^^^^^^
// В XIX веке изготавливались крупные вазы.
// ^^^^^^^^^^
// Это перешло и к XIX веку.
//               ^^^^^^^^^^
// Вятичи использовали трупосожжение вплоть до XI века.
//                                          ^^^^^^^^^^
pattern ОбстФраза0
{
 prep=предлог:*{} : export { node:root_node }
 num=@regex("[IVXM]+")
 obj=существительное:век{ =prep:ПАДЕЖ }
}
: ngrams
{
 3
}
: links
{
 prep.<OBJECT>obj.<ATTRIBUTE>num
}






// молодые люди вернулись часа через два
//                        ^^^^^^^^^^^^^^
// часов в десять он уснул
// ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 t=ИнтервалВремени{ падеж:РОД }
 p=предлог:*{} : export { node:root_node }
 n=ГруппаЧисл { =p:ПАДЕЖ [-1]=t:РОД }
} : links { p.<OBJECT>t.<ATTRIBUTE>n }
  : ngrams { 2 }


// Часов в пять утра грянули первые выстрелы.
// ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 t=ИнтервалВремени{ падеж:РОД }
 p=предлог:*{} : export { node:root_node }
 n=ГруппаЧисл { =p:ПАДЕЖ [-1]=t:РОД }
 n2=ГруппаСущ4{ ПАДЕЖ:РОД НазваниеВремениСуток }
} : links { p.<OBJECT>t.{ <ATTRIBUTE>n <RIGHT_GENITIVE_OBJECT>n2 } }
  : ngrams { 9 }


// Часу в девятом в нашу дверь несмело постучали.
// ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 t=ИнтервалВремени{ падеж:МЕСТ }
 p=предлог:*{ ПАДЕЖ:ПРЕДЛ } : export { node:root_node }
 n=ГруппаПрил0 { ТИП_ПРИЛ:ПОРЯДК ПАДЕЖ:ПРЕДЛ =t:РОД =t:ЧИСЛО }
} : links { p.<OBJECT>t.<ATTRIBUTE>n }
  : ngrams { 1 }


pattern ОбстФраза0
{
 t=ИнтервалВремени{ падеж:ПРЕДЛ }
 p=предлог:*{ ПАДЕЖ:ПРЕДЛ } : export { node:root_node }
 n=ГруппаПрил0 { ПАДЕЖ:ПРЕДЛ =t:РОД =t:ЧИСЛО }
} : links { p.<OBJECT>t.<ATTRIBUTE>n }
  : ngrams { 1 }


// несколько мгновений стояла полная тишина
// ^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=наречие:несколько{}
 n='мгновений'{ падеж:род } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }


// через несколько часов тебе понадобятся твои способности
// ^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:через{} : export { node:root_node }
 a=наречие:несколько{}
 n=ИнтервалВремени{ падеж:род число:мн }
} : links { prepos.<OBJECT>n.<ATTRIBUTE>a }


// через полтора часа долина кипела
// ^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:через{} : export { node:root_node }
 a='полтора' { ПАДЕЖ:ВИН }
 n=ИнтервалВремени{ падеж:род число:ЕД }
} : links { prepos.<OBJECT>n.<ATTRIBUTE>a }


// ---------------------------------

patterns ТокенГода { language=Russian } export { node:root_node ПАДЕЖ }

#pragma language_mix off
pattern ТокенГода export { node:root_node (ПАДЕЖ) }
{
 @regex("[[:digit:]]{3,4}г\\.") : export { node:root_node }
}

pattern ТокенГода export { node:root_node (ПАДЕЖ) }
{
 @regex("[[:digit:]]{3,4}г") : export { node:root_node }
}
#pragma language_mix on


// Индия запустит к Марсу свой космический аппарат в 2013 г
//                                                   ^^^^^^
pattern ТокенГода
{
 y=@regex("[[:digit:]]{3,4}")
 n='г' : export { node:root_node ПАДЕЖ }
} : links { n.<ATTRIBUTE>y }

// Проект начался в 1996 году
//                  ^^^^^^^^^
pattern ТокенГода
{
 y=@regex("[[:digit:]]{3,4}")
 n=существительное:год{} : export { node:root_node ПАДЕЖ }
} : links { n.<ATTRIBUTE>y }


// Осенью 2002 г. здесь при загадочных обстоятельствах умер рыбак.
//        ^^^^^^^
pattern ТокенГода
{
 y=@regex("[[:digit:]]{3,4}")
 n=существительное:"г."{} : export { node:root_node ПАДЕЖ }
} : links { n.<ATTRIBUTE>y }

// ------------------

patterns МесяцОбъектДляПредл { language=Russian } export { node:root_node }

// Это началось после 15 октября 2004 года
//                    ^^^^^^^^^^
pattern МесяцОбъектДляПредл
{
 c1=num_word:*{}
 m1=НазваниеМесяца{ ПАДЕЖ:РОД } : export { node:root_node }
} : links { m1.<ATTRIBUTE>c1 }



// Опрос проводился с 21 по 29 июня 2008 года.
//                    ^^^^^^^^^^^^^
pattern МесяцОбъектДляПредл
{
 c1=num_word:*{}
 p2=предлог:*{}
 c2=num_word:*{}
 m=НазваниеМесяца{ ПАДЕЖ:РОД } : export { node:root_node }
} : links
{
 m.{ <ATTRIBUTE>c1.<PREPOS_ADJUNCT>p2.<OBJECT>c2 }
}
: ngrams { 8 }

/*
// Испытания продолжались с 1 ноября до 8 декабря 1993 года
//                          ^^^^^^^^^^^^^^^^^^^^^
pattern МесяцОбъектДляПредл
{
 c1=num_word:*{}
 m1=НазваниеМесяца{ ПАДЕЖ:РОД } : export { node:root_node }
 p2=предлог:*{}
 c2=num_word:*{}
 m2=НазваниеМесяца{ ПАДЕЖ:РОД }
} : links
{
 m1.{ <ATTRIBUTE>c1 <PREPOS_ADJUNCT>p2.<OBJECT>m2.<ATTRIBUTE>c2 }
}
: ngrams { 8 }
*/

// ------------------

// Первый серийный ё-мобиль выпустят в марте 2015г.
//                                   ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:*{} : export { node:root_node }
 m=НазваниеМесяца{ =prepos:ПАДЕЖ число:ед }
 y=ТокенГода{ [-1]ПАДЕЖ:РОД }
} : links { prepos.<OBJECT>m.<RIGHT_GENITIVE_OBJECT>y }



// Президент Болгарии назначил досрочные парламентские выборы на 12 мая 2013г.
//                                                            ^^^^^^^^^^^^^^^^
// Президент Латвии официально одобрил введение евро с 1 января 2014г
//                                                   ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:*{} : export { node:root_node }
 m=МесяцОбъектДляПредл
 y=ТокенГода{ [-1]ПАДЕЖ:РОД }
}
: links { prepos.<OBJECT>m.<RIGHT_GENITIVE_OBJECT>y }
: ngrams { 15 }


// ---------------------------------------------------------------------




// Италия оставит войска в Афганистане после 2014г.
//                                     ^^^^^^^^^^^^
// Индия планирует отправить беспилотный космический аппарат к Марсу в 2013г.
//                                                                   ^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:*{} : export { node:root_node }
 y=ТокенГода{ [-1]=prepos:ПАДЕЖ }
}
: links { prepos.<OBJECT>y }
: ngrams { ВалентностьПредлога(prepos) }



// Обнаженные российские сноубордистки катались в 30-градусный мороз
//                                              ^^^^^^^^^^^^^^^^^^^^
wordentry_set СущДляОборотаВремени=существительное:{
 мороз, стужа, жара
}
pattern ОбстФраза0
{
 prepos=предлог:*{} : export { node:root_node }
 n=ГруппаСущ1{ СущДляОборотаВремени падеж:вин }
} : links { prepos.<OBJECT>n }
  : ngrams { -2 }


// Много интересного случилось в последнее время
//                             ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:в{} : export { node:root_node }
 n=СущСРодДоп{ СущСоЗначВрем падеж:вин }
} : links { prepos.<OBJECT>n }
  : ngrams { -2 }



// Оборот С ЭТОГО МОМЕНТА:
// резиденты могут с этого момента осуществлять планировку своих объектов с привязкой к местности
pattern ОбстФраза0
{
 prepos=предлог:с{} : export { node:root_node }
 n=ГруппаСущ1{ СущСоЗначВрем падеж:род }
} : links { prepos.<OBJECT>n }
  : ngrams { -2 }

// в марте 2003 года
pattern ОбстФраза0
{
 prepos=предлог:в{} : export { node:root_node }
 m=НазваниеМесяца{ падеж:предл }
 n=num_word:*{}
 y='года'{падеж:род}
} : links { prepos.<OBJECT>m.<NEXT_COLLOCATION_ITEM>n.<NEXT_COLLOCATION_ITEM>y }
  : ngrams { 2 }


// Россия - единственный рынок , который полтора десятилетия спасал Молдавию
//                                       ^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a='полтора'{ПАДЕЖ:ВИН}
 n=ИнтервалВремени{ падеж:род число:ед =a:РОД } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }

// Однако в следующем году цены на хлеб начнут расти уже весной
//        ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 prepos=предлог:в{} : export { node:root_node }
 a=ГруппаПрил2{ падеж:предл число:ед род:муж }
 d='года'{падеж:мест}
} : links { prepos.<OBJECT>d.<ATTRIBUTE>a }
  : ngrams { 2 }

// В этом месяце
pattern ОбстФраза0
{
 prepos=предлог:в{} : export { node:root_node }
 a=ГруппаПрил2{ падеж:предл число:ед }
 d=ИнтервалВремени{ падеж:предл число:ед =a:РОД }
} : links { prepos.<OBJECT>d.<ATTRIBUTE>a }
  : ngrams { 2 }


// где ж ты был прошлой ночью?
//              ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:*{ падеж:твор число:ед род:жен степень:атриб }
 d=существительное:ночь{ падеж:твор число:ед } : export { node:root_node }
} : links { d.<ATTRIBUTE>a }
  : ngrams { 5 }


// следующим вечером собралось еще меньше мальчиков
// ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:*{ падеж:твор число:ед род:муж степень:атриб }
 d=существительное:вечер{ падеж:твор число:ед } : export { node:root_node }
} : links { d.<ATTRIBUTE>a }
  : ngrams { 3 }


// мы сами узнали о нем буквально за пару минут до начала
//                                ^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p1=предлог:за{} : export { node:root_node }
 c='пару'{ род:жен падеж:вин }
 d=ИнтервалВремени{ падеж:род число:мн }
 p2=предлог:до{}
 n2=ГруппаСущ4 { ПАДЕЖ:РОД }
} : links
{
 p1.<OBJECT>c.<RIGHT_GENITIVE_OBJECT>d.<PREPOS_ADJUNCT>p2.<OBJECT>n2
}
: ngrams { 2 }

// события, начавшие происходить в то время в их городке
//                               ^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:в{} : export { node:root_node }
 a='то'{ class:прилагательное падеж:вин }
 n=существительное:время{ ПАДЕЖ:ВИН }
} : links
{
 p.<OBJECT>n.<ATTRIBUTE>a
}
: ngrams { 2 }



// жить ему осталось считанные дни
//                   ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a=прилагательное:считанный{ падеж:вин число:мн одуш:неодуш }
 n=ИнтервалВремени{ падеж:вин число:мн } : export { node:root_node }
} : links
{
 n.<ATTRIBUTE>a
}


// Он с минуту помолчал
//    ^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:с{} : export { node:root_node }
 n=ГруппаСущ1{ ИнтервалВремени падеж:вин }
} : links
{
 p.<OBJECT>n
}
: ngrams { 2 }



// -------------

/*
collocation_set СловосочетКакНаречия=
{
 { весь день напролет },
 { всю ночь напролет }
}


pattern ОбстФраза0
{
 СловосочетКакНаречия:export { node:root_node }
} : ngrams { 5 }
*/

// Бои здесь шли круглые сутки.
//               ^^^^^^^^^^^^^
pattern ОбстФраза0
{
 a='круглые'{ падеж:вин }
 n='сутки'{ падеж:вин } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams { 5 }


// Пушистый пуфик играется целыми днями.
//                         ^^^^^^^^^^^^
pattern ОбстФраза0
{
 a='целыми'{ падеж:твор }
 n='днями'{ падеж:твор } : export { node:root_node }
}
: links { n.<ATTRIBUTE>a }
: ngrams { 5 }

pattern ОбстФраза0
{
 a='весь'{ падеж:вин }
 n='день'{ падеж:вин } : export { node:root_node }
 adv='напролет'
}
: links { n.{ <ATTRIBUTE>a <ATTRIBUTE>adv } }
: ngrams { 5 }

pattern ОбстФраза0
{
 a='всю'{ падеж:вин }
 n='ночь'{ падеж:вин } : export { node:root_node }
 adv='напролет'
}
: links { n.{ <ATTRIBUTE>a <ATTRIBUTE>adv } }
: ngrams { 5 }





// он шатал взад-вперед по запертой палате.
//          ^^^^^^^^^^^
pattern ОбстФраза0
{
 adv1=наречие:*{ ОБСТ_ВАЛ:НАПРАВЛЕНИЕ }:export { node:root_node }
 t='-'
 adv2=наречие:*{ ОБСТ_ВАЛ:НАПРАВЛЕНИЕ }
} : links { adv1.<RIGHT_LOGIC_ITEM>t.<NEXT_COLLOCATION_ITEM>adv2 }


// Люси мыла окно еще до наступления ночи
//                ^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p1=@optional(частица:еще{})
 p2=предлог:до{} : export { node:root_node }
 n1=существительное:наступление{ падеж:род }
 n2=ГруппаСущ1{ ПАДЕЖ:РОД }
} : links
{
 p2.{
     ~<PREFIX_PARTICLE>p1
     <OBJECT>n1.<RIGHT_GENITIVE_OBJECT>n2
    }
}
: ngrams { 1 }



// числительное в роли обстоятельства места или времени

//wordentry_set ПредлогиДляЧислНареч=предлог:{ в, около, до, после }

// мы начинаем работу в пять
//                    ^^^^^^
pattern ОбстФраза0
{
 p=/*ПредлогиДляЧислНареч*/предлог:*{}:export { node:root_node }
 @and(@mark(числительное:*{=P:ПАДЕЖ},n),@not(числительное:один{}))
}
: links { p.<OBJECT>n }
: ngrams { -1 } // штраф за отсутствие явного указания 'часов'


// мы начинаем работу в 5
//                    ^^^
pattern ОбстФраза0
{
 p=предлог:*{}:export { node:root_node }
 n=num_word
}
: links { p.<OBJECT>n }
: ngrams { -1 } // штраф за отсутствие явного указания 'часов'




// мы начинаем работу в пять утра
pattern ОбстФраза0
{
 @mark(предлог:в{},P):export { node:root_node }
 @and(@mark(числительное:*{=P:ПАДЕЖ},n1),@not(числительное:один{}))
 n2=НазваниеВремениСуток{ПАДЕЖ:РОД ЧИСЛО:ЕД}
} : links { p.<OBJECT>n1.<RIGHT_GENITIVE_OBJECT>n2 }


// Урок начинается в девять часов.
//                 ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:в{}:export { node:root_node }
 c=числительное:*{ падеж:вин }
 n=существительное:час{}
} : links { p.<OBJECT>c.<RIGHT_GENITIVE_OBJECT>n }



// ведь прошло уже целых пятнадцать лет!
//             ^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=@optional(наречие:уже{})
 a=прилагательное:целый{ ПАДЕЖ:РОД }
 c=числительное:*{ падеж:вин }
 n=ИнтервалВремени{ ПАДЕЖ:РОД ЧИСЛО:МН }:export { node:root_node }
} : links
{
 n.{
    <ATTRIBUTE>a.~<ATTRIBUTE>mod
    <ATTRIBUTE>c
   }
}

// Обстоятельственный оборот - продолжительность:
// Анатолий был мертв больше двух часов.
//                    ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=БолееМенееНаречВремя:export { node:root_node }
 c=числительное:*{ падеж:род }
 n=СчетнСущДляНареч{ ПАДЕЖ:РОД }
} : links
{
 mod.<OBJECT>n.<ATTRIBUTE>c
}
: ngrams { 4 }


// мгновением позже голос перешел к дракону
pattern ОбстФраза0
{
 n=ГруппаСущ1{ СущСоЗначВрем ПАДЕЖ:ТВОР }
 p=послелог:позже{} : export { node:root_node }
} : links { p.<OBJECT>n }
: ngrams { 5 }


// за последние десять минут вас убили раз десять
pattern ОбстФраза0
{
 p=предлог:за{}:export { node:root_node }
 a=прилагательное:*{ СТЕПЕНЬ:АТРИБ ПАДЕЖ:ВИН ЧИСЛО:МН }
 n=ЯдроНаречВрем
}
: links { p.<OBJECT>n.<ATTRIBUTE>a }
: ngrams { 11 }

// уже больше года сидят
//     ^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=БолееМенееНаречВремя:export { node:root_node }
 n=СчетнСущДляНареч{ ПАДЕЖ:РОД }
} : links
{
 mod.<OBJECT>n
}
: ngrams { 4 }

// В ближайшее время США будут расширять свое присутствие в Африке
// ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:в{}:export { node:root_node }
 a=прилагательное:*{ ПАДЕЖ:ВИН ЧИСЛО:ЕД РОД:СР }
 n=существительное:время{ ПАДЕЖ:ВИН ЧИСЛО:ЕД }
}
: links { p.<OBJECT>n.<ATTRIBUTE>a }
: ngrams { 5 }


// Мы приехали в город за день до того, как произошел взрыв.
//                     ^^^^^^^^^^^^^^^^^...
pattern ОбстФраза0
{
 p=предлог:за{}:export { node:root_node }
 n=ЯдроНаречВрем
 p2=предлог:до{}
 n2=СущСПредложДоп{ ПАДЕЖ:РОД }
}
: links { p.<OBJECT>n.<PREPOS_ADJUNCT>p2.<OBJECT>n2 }
: ngrams { 16 }


// девушек отправили днем раньше.
//                   ^^^^^^^^^^^
pattern ОбстФраза0
{
 w1='днем'
 w2='раньше'{ class:наречие }:export { node:root_node }
}
: links { w2.<ATTRIBUTE>w1 }
: ngrams { 2 }


// немного спустя они услышали рассказ полностью.
// ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 w1=наречие:немного{}
 w2=послелог:спустя{}:export { node:root_node }
}
: links { w2.<OBJECT>w1 }
: ngrams { 2 }

// +++++++++++++++++++++++++++++++++++++++++++++++

// Оборот с семантикой длительности:
//
// Всю лекцию он спокойно лежал на полу у ног хозяина.
// ^^^^^^^^^^
// хотя всю жизнь мечтал им стать.
//      ^^^^^^^^^

pattern ОбстФраза0
{
 a=ГруппаПрил1{ падеж:вин }
 n=ГруппаСущ4{ <в_класс>ThesaurusEntries:HasDuration{} падеж:вин =a:ЧИСЛО =a:РОД } : export { node:root_node }
} : links { n.<ATTRIBUTE>a }
: ngrams { -3 }

// Ехали всего ночь.
//       ^^^^^^^^^^
pattern ОбстФраза0
{
 mod=Наречия_Лишь
 n=ГруппаСущ4{ <в_класс>ThesaurusEntries:HasDuration{} падеж:вин } : export { node:root_node }
} : links { n.<ATTRIBUTE>mod }
: ngrams { -3 }



/*
// Весь остаток дня
pattern Обст
{
 a=ГруппаПрил1{ падеж:вин }
 n=существительное:*{ одуш:неодуш падеж:вин =a:РОД =a:ЧИСЛО } : export { node:root_node }
 n2=ГруппаСущ4{ <в_класс>ThesaurusEntries:HasDuration{} падеж:род }
}
: links
{
 n.{
    <ATTRIBUTE>a
    <RIGHT_GENITIVE_OBJECT>n2
   }
}
: ngrams
{
 -6
 СущКакОбстВремени(n)
}
*/

// Всю первую часть пути я спал
// ^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n1=ГруппаСущ1{ падеж:вин одуш:неодуш } : export { node:root_node }
 n2=ГруппаСущ4{ <в_класс>ThesaurusEntries:HasDuration{} падеж:род }
} : links { n1.<RIGHT_GENITIVE_OBJECT>n2 }
: ngrams
{
 -5
 СущКакОбстВремени(n1)
}



// Придаточное в обороте времени:
// С тех пор,  как  она попала в аварию,  все только и  говорят про ее игру
// ^^^^^^^^^^^^^^^^ ...
pattern ОбстФраза0
{
 prep=предлог:с{} : export { node:root_node }
 n=ГруппаСущ1{ СущСоЗначВрем ПАДЕЖ:РОД }
 comma1=','
 conj=союз:как{}
 p=ПридаточноеВОбороте
 @noshift(ПравыйОграничительОборота)
 comma2=@coalesce(',')
}
: links
{
 prep.<OBJECT>n.
  <SUBORDINATE_CLAUSE>comma1.
   <NEXT_COLLOCATION_ITEM>conj.
    <NEXT_COLLOCATION_ITEM>p.
     ~<PUNCTUATION>comma2
}
: ngrams { 1 }

// +++++++++++++++++++++++++++++++++++++++++++

wordentry_set СущСоЗначВремКакОбст1 = существительное:{
день,
денек,
денечек,
вечер,
вечерок, // Чудно посидели вечерок.
вечерочек,
утро,
утречко,
ночь,
ночка,
минута,
минутка,
минуточка,
секунда,
секундочка,
час,
часок,
часик,
мгновение,
столетие,
вечность,
год,
годик,
век,
тысячелетие,
месяц
}


// мгновение они стояли оба лицом к лицу.
// ^^^^^^^^^
pattern ОбстФраза0
{
 СущСоЗначВремКакОбст1{ ПАДЕЖ:ВИН ЧИСЛО:ЕД } : export { node:root_node }
}
: ngrams { -2 }

// Я и часа здесь не останусь!
//   ^^^^^^
pattern ОбстФраза0
{
 p=частица:и{}
 СущСоЗначВремКакОбст1{ ПАДЕЖ:РОД ЧИСЛО:ЕД } : export { node:root_node }
}
: ngrams { -2 }


// Он и часу не продержался.
//    ^^^^^^
pattern ОбстФраза0
{
 p=частица:и{}
 СущСоЗначВремКакОбст1{ ПАДЕЖ:ПАРТ ЧИСЛО:ЕД } : export { node:root_node }
}
: ngrams { -2 }



// +++++++++++++++++++++++++++++++++++++++++++

wordentry_set ЕдиницыВремени=существительное:
{
 секунда, минута, час, день, сутки, год, лето, месяц, квартал
}


// минут пять они молчали
// ^^^^^^^^^^
pattern ОбстФраза0
{
 n=ГруппаСущ4{ ЕдиницыВремени ПАДЕЖ:ВИН }:export { node:root_node }
}
: ngrams
{
 -2
 СущКакОбстВремени(n)
}


// ----------------------------------

// Бамбук цветёт раз в жизни.
//               ^^^^^^^^^^^
// выходящий четыре раза в год
//           ^^^^^^^^^^^^^^^^^
wordentry_set СущРаз={ существительное:раз{} }
pattern ОбстФраза0
{
 n1=ГруппаСущ4{ СущРаз }:export { node:root_node }
 prep=предлог:в{}
 n2=ГруппаСущ1{ ПАДЕЖ:ВИН ЧИСЛО:ЕД ОДУШ:НЕОДУШ }
}
: links
{
 n1.<PREPOS_ADJUNCT>prep.<OBJECT>n2
}
: ngrams { 1 }

// ------------------------------------

// На второе июня заказаны билеты
//    ^^^^^^^^^^^
patterns ЧислоМесяц export { node:root_node ПАДЕЖ }

pattern ЧислоМесяц
{
 a = ПРИЛАГАТЕЛЬНОЕ:*{ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ЧИСЛО:ЕД РОД:СР ТИП_ПРИЛ:ПОРЯДК }
 n = НазваниеМесяца{ ЧИСЛО:ЕД ПАДЕЖ:РОД } : export { node:root_node ПАДЕЖ }
}
: links { n.<ATTRIBUTE>a }
: ngrams { 1 }


// Это надо доделать до третьего или четвертого августа
//                      ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ЧислоМесяц
{
 a = ПРИЛАГАТЕЛЬНОЕ:*{ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ЧИСЛО:ЕД РОД:СР ТИП_ПРИЛ:ПОРЯДК }
 conj=ЛогичСоюз
 a2 = ПРИЛАГАТЕЛЬНОЕ:*{ =a:ПАДЕЖ СТЕПЕНЬ:АТРИБ ~КРАТКИЙ ЧИСЛО:ЕД РОД:СР ТИП_ПРИЛ:ПОРЯДК }
 n = НазваниеМесяца{ ЧИСЛО:ЕД ПАДЕЖ:РОД } : export { node:root_node ПАДЕЖ }
}
: links
{
 n.<ATTRIBUTE>a.<RIGHT_LOGIC_ITEM>conj.<NEXT_COLLOCATION_ITEM>a2
}
: ngrams { 1 }

pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 obj=ЧислоМесяц{ =p:ПАДЕЖ }
} : links { p.<OBJECT>obj }

// Третьего августа мы уезжаем
// ^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 ЧислоМесяц{ ПАДЕЖ:РОД } : export { node:root_node }
}

// -----------------------------------------

// Оборот с придаточным: ДО ТЕХ ПОР, ПОКА xxx
// Мы играли в саду до тех пор, пока нас не позвала мама
//                  ^^^^^^^^^^^^^^^^^ ...
pattern ОбстФраза0
{
 p=предлог:до{} : export { node:root_node }
 a='тех'{ ПАДЕЖ:РОД }
 n='пор'
 comma=','
 adv=наречие:пока{}
 clause=ПридаточноеВОбороте
 comma2=@coalesce(ПравыйОграничительОборота)
}
: links
{
 p.<OBJECT>n
   .<ATTRIBUTE>a
    .<SUBORDINATE_CLAUSE>comma
     .<NEXT_COLLOCATION_ITEM>adv
      .<NEXT_COLLOCATION_ITEM>clause
       .~<PUNCTUATION>comma2
}
: ngrams { 5 }



pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 obj=ЧислоМесяц{ =p:ПАДЕЖ }
 n=num_word:*{}
 y='года'{ ПАДЕЖ:РОД ЧИСЛО:ЕД }
} : links
{
 p.<OBJECT>obj.<RIGHT_GENITIVE_OBJECT>y.<ATTRIBUTE>n
}
: ngrams { 8 }


// Субсидия была погашена в январе 2008 года.
//                        ^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 obj=ГруппаСущ4{ НазваниеМесяца =p:ПАДЕЖ }
 n=num_word:*{}
 y='года'{ ПАДЕЖ:РОД ЧИСЛО:ЕД }
} : links
{
 p.<OBJECT>obj.<RIGHT_GENITIVE_OBJECT>y.<ATTRIBUTE>n
}
: ngrams { 8 }


// В январе 2013 г. продажи новых легковых автомобилей на европейском рынке сократились на 9,6%
// ^^^^^^^^^^^^^^^^
// В начале 1757 года
// ^^^^^^^^^^^^^^^^^^
// Инцидент произошел в начале марта 2006 года.
//                    ^^^^^^^^^^^^^^^^^^^^^^^^
// В декабре 1469 г. Жан умер.
// ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 n=ГруппаСущ4{ =p:ПАДЕЖ одуш:неодуш }
 y=ТокенГода{ [-1]ПАДЕЖ:РОД }
} : links
{
 p.<OBJECT>n.<RIGHT_GENITIVE_OBJECT>y
}
: ngrams { 6 }



// Наиболее интенсивное строительство планируется в 2009-2012 годах.
//                                                ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 c1=num_word:*{}
 t='-'
 c2=num_word:*{}
 y='годах'
} : links
{
 p.<OBJECT>y.<ATTRIBUTE>c1.<NEXT_COLLOCATION_ITEM>t.<NEXT_COLLOCATION_ITEM>c2
}
: ngrams { 6 }


// Весной 1757 года в войну вступила Франция
// ^^^^^^^^^^^^^^^^
// Осенью 2002 г. здесь при загадочных обстоятельствах умер рыбак.
// ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=ГруппаСущ1{ ВремяГода ПАДЕЖ:ТВОР } : export { node:root_node }
 y=ТокенГода
} : links
{
 n.<RIGHT_GENITIVE_OBJECT>y
}
: ngrams { 6 }



// С 27.06.2003 г. предполагается практически полное прекращение подачи электроэнергии.
// ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 c=@regex('[0-9]{2}\.[0-9]{2}\.[0-9]{2,4}')
 y='г.'
} : links
{
 p.<OBJECT>y.<ATTRIBUTE>c
}
: ngrams { 6 }


// Важно учесть и то, что эти люди рождены от отцов и матерей, женившихся в 1980-х годах
//                                                                        ^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 c=@regex('[0-9]{4}-х')
 y='годах'
} : links
{
 p.<OBJECT>y.<ATTRIBUTE>c
}
: ngrams { 6 }


// ----------------------------

// *************************************************
// ДАТЫ
// *************************************************


wordentry_set ПрилагДляГода={
                             прилагательное:этот{},
                             прилагательное:сей{},
                             прилагательное:прошлый{},
                             прилагательное:тот{},
                             прилагательное:будущий{}
                            }

patterns Дата1 export { node:root_node }


// мы приступили к работе 30 мая
//                        ^^^^^^
pattern Дата1
{
 c=num_word:*{}
 m=РусМесяц{ ПАДЕЖ:РОД ЧИСЛО:ЕД }:export { node:root_node }
}
: links { m.<ATTRIBUTE>c }
: ngrams { 1 }


// Проверить 31 марта 2012
//           ^^^^^^^^^^^^^
pattern Дата1
{
 c1=num_word:*{}
 m=РусМесяц{ ПАДЕЖ:РОД ЧИСЛО:ЕД }
 c2=num_word:*{}:export { node:root_node }
}
: links { c2.<CONOUN>m.<ATTRIBUTE>c1 }
: ngrams { 1 }


// Договор был подписан 17 мая 2002 года.
pattern Дата1
{
 c1=num_word:*{}
 m=РусМесяц{ ПАДЕЖ:РОД ЧИСЛО:ЕД }
 c2=num_word:*{}
 y=существительное:год { ПАДЕЖ:РОД ЧИСЛО:ЕД }:export { node:root_node }
} : links
{
 y.{
    <ATTRIBUTE>c2
    <CONOUN>m.<ATTRIBUTE>c1
   }
}
: ngrams { 9 }


// 1 сентября этого года
pattern Дата1
{
 c=num_word:*{}
 m=РусМесяц{ ПАДЕЖ:РОД ЧИСЛО:ЕД }:export { node:root_node }
 a=ПрилагДляГода{ ПАДЕЖ:РОД ЧИСЛО:ЕД РОД:МУЖ }
 y=существительное:год { ПАДЕЖ:РОД ЧИСЛО:ЕД }
} : links
{
 m.{
    <ATTRIBUTE>c
    <RIGHT_GENITIVE_OBJECT>y.<ATTRIBUTE>a
   }
}


pattern ОбстФраза0
{
 Дата1:export { node:root_node }
}

// Подобное нападение на таксиста было совершено и 8 апреля.
//                                               ^^^^^^^^^^
pattern ОбстФраза0
{
 p=частица:и{}
 n=ОбстФраза0:export { node:root_node }
}
: links { n.<PREFIX_PARTICLE>p }
: ngrams { -3 }



// до 1 августа
pattern ОбстФраза0
{
 prep=предлог:*{}:export { node:root_node }
 d=Дата1
 mod=@optional(наречие:включительно{})
} : links
{
 prep.<OBJECT>d.~<ATTRIBUTE>mod
}

// в 1960 году
pattern ОбстФраза0
{
 prep=предлог:*{}:export { node:root_node }
 c=num_word:*{}
 y=существительное:год{}
} : links
{
 prep.<OBJECT>y.<ATTRIBUTE>c
}

// 12 апреля 1961 года навсегда останется в памяти человечества
// ^^^^^^^^^^^^^^^^^^^
pattern ГруппаСущ4 export { РОД ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 Дата1 : export { РОД:СР ЧИСЛО:ЕД ПАДЕЖ:ИМ node:root_node }
}

// --------------------------------------------

wordentry_set МодифПредлЧерез=наречие:{ примерно, почти, всего, уже, еще, только }

// примерно через час будет темно.
// ^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=МодифПредлЧерез
 p=предлог:через{} : export { node:root_node }
 n=ЯдроНаречВрем
}
: links { p.{ <ATTRIBUTE>adv <OBJECT>n } }
: ngrams { 4 }


// --------------------------------------------

// сидел так впервые за много лет
//           ^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 adv=наречие:впервые{}
 p=предлог:за{} : export { node:root_node }
 n=ЯдроНаречВрем
}
: links { p.{ <ATTRIBUTE>adv <OBJECT>n } }
: ngrams { 4 }

// ------------------------------------------------------

patterns ЧислоДляВремени export { node:root_node }

pattern ЧислоДляВремени
{
 Числ1 : export { node:root_node }
}

pattern ЧислоДляВремени
{
 num_word:*{} : export { node:root_node }
}


// ребята прошли их минут за двадцать
//                  ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=СущСоЗначВрем_Лет{ ПАДЕЖ:РОД }
 p=предлог:за{} : export { node:root_node }
 c=ЧислоДляВремени
}
: links { p.<OBJECT>n.<ATTRIBUTE>c }
: ngrams { 4 }

// И продавайте перед закрытием, минут за десять  до  колокола. 
//                               ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=СущСоЗначВрем_Лет{ ПАДЕЖ:РОД }
 p=предлог:за{} : export { node:root_node }
 c=ЧислоДляВремени
 p2=предлог:до{}
 obj2=ГлДополнение{ ПАДЕж:РОД }
}
: links { p.<OBJECT>n.{ <ATTRIBUTE>c <PREPOS_ADJUNCT>p2.<OBJECT>obj2 } }
: ngrams { 5 }

// ------------------------------------------------------

// Мне надо подумать, а ты помучайся часок-другой.
//                                   ^^^^^^^^^^^^
pattern ОбстФраза0
{
 n=СущСоЗначВремКакОбст1{ ПАДЕЖ:ВИН ЧИСЛО:ЕД } : export { node:root_node }
 t='-'
 n2=прилагательное:другой{ ПАДЕЖ:ВИН ЧИСЛО:ЕД =n:РОД }
}
: links { n.<RIGHT_LOGIC_ITEM>t.<NEXT_COLLOCATION_ITEM>n2 }
: ngrams { 1 }

// -----------------------------------------------------

// После 2010 года рынок откроется
// ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 p=предлог:*{} : export { node:root_node }
 c=num_word:*{}
 n=существительное:год{ =p:ПАДЕЖ }
} : links
{
 p.<OBJECT>n.<ATTRIBUTE>c
}
: ngrams { 5 }

// -----------------------------------------------------

// Она берёт уроки пения два раза в неделю
//                       ^^^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 c=ГруппаЧисл{ ПАДЕЖ:ВИН }
 n=существительное:раз{ падеж:род } : export { node:root_node }
 p=предлог:*{ ПАДЕЖ:ВИН }
 t=ГруппаСущ1{ ИнтервалВремени ПАДЕЖ:ВИН }
} : links
{
 n.{
    <ATTRIBUTE>c
    <PREPOS_ADJUNCT>p.<OBJECT>t
   }
}
: ngrams { 10 }




wordentry_set КванторВремени={ наречие:столько{}, наречие:сколько{} }

// Столько времени учиться.
// ^^^^^^^^^^^^^^^
pattern ОбстФраза0
{
 mod=КванторВремени
 n=существительное:время{ падеж:род число:ед } : export { node:root_node }
} : links
{
 n.<ATTRIBUTE>mod
}
: ngrams { 1 }


// В эфир выходили на несколько секунд.
//                 ^^^^^^^^^^^^^^^^^^^
// вот время и остановилось на века.
//                          ^^^^^^^
pattern ОбстФраза0
{
 prep=предлог:на{} : export { node:root_node }
 n=ГруппаСущ4{ СущСоЗначВремКакОбст1 =prep:ПАДЕЖ }
} : links
{
 prep.<OBJECT>n
}
: ngrams { 2 }


