
// ОБСТОЯТЕЛЬСТВО "ГДЕ"
// todo: создавать корневой узел с нетерминалом НАРЕЧИЕ:_ГДЕТО_
//       чтобы отличать от "уни уехали в трех машинах отсюда"

wordentry_set ПредлогиРасстояния=предлог:{ за, в, через }

// за десять шагов отсюда стояла его лошадь
pattern Обст
{
 prep=ПредлогиРасстояния:export { node:root_node }
 n=ЧислСущ
 mod=наречие:отсюда{}
} : links
{
 prep.{
       <OBJECT>n
       <ATTRIBUTE>mod
      }
}
: ngrams { 1 }


// ++++++++++++++++++++++++++++++++++

wordentry_set ЕдиницыРасстояния=существительное:
{
 миля, метр, километр, верста, миллиметр, сантиметр, дециметр, микрон, шаг
}

// за ночь она могла пройти километров двадцать
//                          ^^^^^^^^^^^^^^^^^^^
pattern Обст
{
 ГруппаСущ4{ ЕдиницыРасстояния ПАДЕЖ:ВИН }:export { node:root_node }
} : ngrams { -2 }


// Не  доходя  шагов  семидесяти, старик приостановился.
//             ^^^^^^^^^^^^^^^^^
pattern Обст
{
 ГруппаСущ4{ ЕдиницыРасстояния ПАДЕЖ:РОД }:export { node:root_node }
} : ngrams { -3 }


// метров через сто они подошли к новой двери
// ^^^^^^^^^^^^^^^^
pattern Обст
{
 n=существительное:*{ ЕдиницыРасстояния ПАДЕЖ:РОД ЧИСЛО:МН }
 p=предлог:*{} : export { node:root_node }
 c=ГруппаЧисл{ =p:ПАДЕЖ }
}
: links { p.<OBJECT>n.<ATTRIBUTE>c }
: ngrams { -1 }

