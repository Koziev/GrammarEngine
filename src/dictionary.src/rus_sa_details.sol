// ---------------------------------------------------------

// Детализаторы - уточняют основную мысль или фрагмент фразы



patterns СодержимоеДетализатора export { node:root_node }
pattern СодержимоеДетализатора
{
 НеполнПредлож : export { node:root_node }
} : ngrams { -2 }

pattern СодержимоеДетализатора
{
 Предлож1 : export { node:root_node }
}


word_set НачалоДетализатора={ '-', '—', '–' }
word_set КонецДетализатора={ '-', '—', '–' }

// Девочки решили (они всегда так делали) уйти
//                ^^^^^^^^^^^^^^^^^^^^^^^
// серебристые облака (также называемые полярными мезосферными облаками) являются самыми высокими образованиями облаков в атмосфере Земли.
//                    ^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern Детализатор
{
 beg='('
 z=СодержимоеДетализатора : export { node:root_node }
 end=')'
} : links
{
 z.{
    <PUNCTUATION>beg
    <PUNCTUATION>end
   }
}
: ngrams { -15 }


// Девочки решили - а они всегда так делали - уйти
//                ^^^^^^^^^^^^^^^^^^^^^^^^^^^
pattern Детализатор
{
 beg=НачалоДетализатора
 z=СодержимоеДетализатора : export { node:root_node }
 end=КонецДетализатора
} : links
{
 z.{
    <PUNCTUATION>beg
    <PUNCTUATION>end
   }
}
: ngrams { -15 }


// ... посредством специальных образований — синапсов.
//                                         ^^^^^^^^^^
pattern Детализатор
{
 beg=НачалоДетализатора
 z=СодержимоеДетализатора : export { node:root_node }
 @noshift(ПравыйОграничительОборота)
} : links
{
 z.<PUNCTUATION>beg
}
: ngrams { -18 }



// Миша (один! без разрешения!) ушел в кино
//      ^^^^^^
// Мы думаем (т.е. конечно не все) об одном и том же
//           ^^^^^^^^^^^^^^^^^^^^^
pattern Детализатор
{
 beg='('
 z=@untill(')') : export { node:root_node }
 end=')'
} : links
{
 z.{
    <PUNCTUATION>beg
    <PUNCTUATION>end
   }
}
: ngrams { -20 }


pattern Детализатор
{
 beg=НачалоДетализатора
 z=@untill(КонецДетализатора) : export { node:root_node }
 end=КонецДетализатора
} : links
{
 z.{
    <PUNCTUATION>beg
    <PUNCTUATION>end
   }
}
: ngrams { -30 }


pattern Детализатор
{
 beg=НачалоДетализатора
 z=@untill(ПравыйОграничительОборота) : export { node:root_node }
} : links
{
 z.<PUNCTUATION>beg
}
: ngrams { -32 }

