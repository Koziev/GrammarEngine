// Цепочка прилагательных, рассматриваемых как группа прилагательного
// во множественном числе:
//
// Мы увидели зеленый, желтый и синий сигналы
//            ^^^^^^^^^^^^^^^^^^^^^^^


patterns ГруппаПрилМнож_Элем export { node:root_node ПАДЕЖ }

pattern ГруппаПрилМнож_Элем
{
 ГруппаПрил1 : export { node:root_node ПАДЕЖ }
}

pattern ГруппаПрилМнож_Элем
{
 ПричОборот0 : export { node:root_node ПАДЕЖ }
}

// -------------------------------------------------------------

patterns ГруппаПрилМнож0 export { node:root_node ПАДЕЖ }

pattern ГруппаПрилМнож0
{
 a1=ГруппаПрилМнож_Элем : export { node:root_node ПАДЕЖ }
 junc=СоединПрилМнож1
 a2=ГруппаПрилМнож_Элем{ =a1:ПАДЕЖ }
} : links { a1.<NEXT_ADJECTIVE>junc.<NEXT_COLLOCATION_ITEM>a2 }

// -------------------------------------------------------------

patterns ГруппаПрилМнож_Восх { bottomup } export { node:root_node ПАДЕЖ }

pattern ГруппаПрилМнож_Восх
{
 ГруппаПрилМнож0 : export { node:root_node ПАДЕЖ }
}

pattern ГруппаПрилМнож_Восх
{
 a1=ГруппаПрилМнож_Восх : export { node:root_node ПАДЕЖ }
 junc=СоединПрилМнож1
 a2=ГруппаПрилМнож_Элем{ =a1:ПАДЕЖ } 
} : links { a1.<NEXT_ADJECTIVE>junc.<NEXT_COLLOCATION_ITEM>a2 }


// -------------------------------------------------------------

patterns ГруппаПрилМнож2 export { node:root_node ПАДЕЖ }

pattern ГруппаПрилМнож2
{
 ГруппаПрилМнож_Восх : export { node:root_node ПАДЕЖ }
}

// -------------------------------------------------------------

// Прилагательное в единственном числе может быть подлежащим множественного числа:
// Какими-то будут второй, третий?..
//                 ^^^^^^^^^^^^^^
pattern ГруппаПрил2 export { (РОД) ПАДЕЖ ЧИСЛО (ОДУШ) node:root_node }
{
 ГруппаПрилМнож2 : export { node:root_node ПАДЕЖ ЧИСЛО:МН }
} : ngrams { -2 }

